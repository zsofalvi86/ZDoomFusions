actor PPE_Q2ProximityLauncher : RocketLauncher
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "PPE_Grenades"
	Weapon.SlotNumber 6
	Inventory.PickupSound "items/q2weapon"
	Inventory.Pickupmessage "$GOT_PPE_PXL"
	Tag "$TAG_PPE_PXL"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		2PLG A 1 A_WeaponReady
		Loop
	Deselect:
		2PLG A 1 A_Lower
		Loop
	Select:
		2PLG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/grenade/fire",1,1-(CountInv("PowerSilencer")*0.7))
		TNT1 A 0 A_FireCustomMissile("PPE_Q2Prox",0,1,0,0,1,10)
		2PLF AB 2 bright
		TNT1 A 0 A_PlaySound("weapons/grenade/reload",6,1-(CountInv("PowerSilencer")*0.7))
		2PLF CDE 2 bright
		2PLG BCDEEEEFGHI 2
		2PLG A 10
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.5)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.0)
		Goto LightDone
	Spawn:
		2PLW A -1
		Stop
   }
}

actor PPE_Q2Prox : PPE_LauncherGrenade
{
  SeeSound ""
  BounceSound "weapons/grenbnc"
  DeathSound ""
  -USEBOUNCESTATE
  +HITTRACER
  BounceType None
  BounceFactor 0.0
  WallBounceFactor 0.0
  BounceCount 0
  Damage (100)
  DamageType "Quake2Proxy"
  States
  {
  Death:
    TNT1 A 0 A_PlaySound("weapons/proximity/open",1)
	TNT1 A 0 A_NoGravity
	DGRD A 45
	TNT1 A 0 A_SpawnItem("PPE_Q2ProxStuck")
	stop
  Crash:
  XDeath:
	TNT1 A 0 A_NoGravity
	TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
	TNT1 A 0 A_PlaySound("weapons/grenade/explode",1) // 60 - upscaled to... well..
	TNT1 A 0 A_Explode(60,128)
	TNT1 A 0 A_Quake(4,5,0,256,"")
	TNT1 A 0 A_Quake(2,6,0,720,"")
	TNT1 A 0 A_Quake(1,6,0,4096,"")
	TNT1 A 0 A_CheckFloor("Floor")
	BMWP AB 3 bright
	BMWP CDEFG 3 bright
	Stop
  Floor:
	BMFP AB 3 bright
	BMFP CDEFG 3 bright
	Stop
  }
}

Actor PPE_Q2ProxStuck : PPE_Q2Prox
{
	+NOGRAVITY
	+DONTTHRUST
	-GRENADETRAIL
	-HITTRACER
	Speed 0
	DamageType "Quake2Proxy"
	States
	{
	Spawn:
		DGRD A 1
		loop
	Crash:
	XDeath:
	Death:
		TNT1 A 0 A_PlaySound("weapons/proximity/warn")
		DGRD A 15
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		TNT1 A 0 A_PlaySound("weapons/grenade/explode",1)
		TNT1 A 0 A_Explode(60,128)
		TNT1 A 0 A_Quake(4,5,0,256,"")
		TNT1 A 0 A_Quake(2,6,0,720,"")
		TNT1 A 0 A_Quake(1,6,0,4096,"")
		TNT1 A 0 A_CheckFloor("Floor")
		BMWP AB 3 bright
		BMWP CDEFG 3 bright
		Stop
	Floor:
		BMFP AB 3 bright
		BMFP CDEFG 3 bright
		Stop
	}
}
