ACTOR PPE_Q2Stimpack : Stimpack replaces Stimpack
{
	Inventory.PickupSound "items/health2"
	Inventory.PickupMessage "$GOTMED1"
	Inventory.Amount 10
	States
	{
	Spawn:
		2MED A -1
		Stop
	}
}

ACTOR PPE_Q2MediKit : MediKit
{
	Inventory.PickupSound "items/health3"
	Inventory.Amount 25
	States
	{
	Spawn:
		2MED B -1
		Stop
	}
}

ACTOR PPE_Q2HealthBonus : HealthBonus replaces HealthBonus
{
  Inventory.PickupSound "items/health1"
  Inventory.PickupMessage "$GOTSTIM"
  Inventory.Amount 2
  Inventory.MaxAmount 250
  States
  {
  Spawn:
		HBSP A -1
		Stop
  }
}

ACTOR PPE_Adrenaline_Upgrade : UpgradeStamina replaces Berserk
{
  +COUNTITEM
  Inventory.PickupMessage "$GOTADRENALINE"
  Inventory.PickupSound "items/q2pickup"
  Inventory.Amount 1
  Inventory.MaxAmount 200
  States
  {
  Spawn:
		2ADR A -1
		Stop
  }
}

ACTOR PPE_Q2MegaHealth : CustomInventory replaces Soulsphere
{
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage "$GOTMEGA"
  Inventory.PickupSound "items/megahealth"
  States
  {
  Spawn:
    2MGH A -1
    Stop
  Pickup:
	TNT1 A 0 A_GiveInventory("PPE_TempHealth",min(100,250-health))
    TNT1 A 0 A_GiveInventory("PPE_Q2HealthBonus",50) // will give 100 as one gives 2
    Stop
  }
}

Actor PPE_25HealthSpawner : CustomInventory replaces MediKit
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteWithResult("SetSpawn",12) //use WithResult otherwise it'll run in parellel
		TNT1 A 0 A_JumpIf(CallAcs("GetSpawn",12) == 4,"Spawn2")
		TNT1 A 0 A_SpawnItem("PPE_Q2MediKit")
		stop
	Spawn2:
		TNT1 A 0 A_SpawnItem("PPE_Q2GreenArmor")
		stop
	}
}

Actor PPE_BackpackSpawner : CustomInventory replaces Backpack
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteWithResult("SetSpawn",11) //use WithResult otherwise it'll run in parellel
		TNT1 A 0 A_JumpIf(CallAcs("GetSpawn",11) == 2,"Spawn2")
		TNT1 A 0 A_SpawnItem("PPE_Q2Bandolier")
		stop
	Spawn2:
		TNT1 A 0 A_SpawnItem("PPE_Q2Backpack")
		stop
	}
}

Actor PPE_GreenArmorSpawner : CustomInventory
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(CallAcs("GetLevelNum") >= 7,"Spawn3")
		TNT1 A 0 A_JumpIf(CallAcs("GetLevelNum") >= 4,"Spawn2")
		TNT1 A 0 A_SpawnItem("PPE_Q2GreenArmor")
		stop
	Spawn2:
		TNT1 A 0 A_Jump(256,1,2)
		TNT1 A 0 A_SpawnItem("PPE_Q2GreenArmor")
		stop
		TNT1 A 0 A_SpawnItem("PPE_Q2YellowArmor")
		stop
	Spawn3:
		TNT1 A 0 A_SpawnItem("PPE_Q2YellowArmor")
		stop
	}
}

Actor PPE_BlueArmorSpawner : CustomInventory
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(CallAcs("GetLevelNum") >= 6,"Spawn3")
		TNT1 A 0 A_JumpIf(CallAcs("GetLevelNum") >= 3,"Spawn2")
		TNT1 A 0 A_SpawnItem("PPE_Q2YellowArmor")
		stop
	Spawn2:
		TNT1 A 0 A_Jump(256,1,2)
		TNT1 A 0 A_SpawnItem("PPE_Q2YellowArmor")
		stop
		TNT1 A 0 A_SpawnItem("PPE_Q2RedArmor")
		stop
	Spawn3:
		TNT1 A 0 A_SpawnItem("PPE_Q2RedArmor")
		stop
	}
}

Actor PPE_Q2PowerArmor : Megasphere replaces Megasphere
{
	-INVENTORY.AUTOACTIVATE
	+INVENTORY.KEEPDEPLETED
	+INVENTORY.INVBAR
	Inventory.PickupSound "items/q2pickup"
	Inventory.Icon "2ARMD0"
	Inventory.PickupMessage "$GOTPOWERSHIELD"
	Tag "$TAG_PPE_POWERSHIELD"
	States
	{
	Spawn:
		2ARM D -1
		Stop
	Pickup:
		TNT1 A 0 A_GiveInventory("PPE_Cells",200)
		TNT1 A 0 A_GiveInventory("PPE_Q2MediKit",10)
		TNT1 A 0 A_GiveInventory("PPE_Q2MegaHealth",1)
		Stop
	Use:
		TNT1 A 0 A_JumpIfInventory("PPE_PowerArmorOn",0,"TurnOff")
		goto TurnOn
	TurnOn:
		TNT1 A 0 A_SetInventory("PPE_PowerArmorOn",1)
		TNT1 A 0 A_PlaySound("items/powershield/on")
		Fail
	TurnOff:
		TNT1 A 0 A_SetInventory("PPE_PowerArmorOn",0)
		//TNT1 A 0 A_GiveInventory("PPE_Q2PowerArmor")
		TNT1 A 0 A_PlaySound("items/powershield/off")
		Fail
	Terminate:
		TNT1 A 0
		Fail
	}
}

ACTOR PPE_PowerArmorOn : Inventory
{
	Inventory.MaxAmount 1
}

Actor PPE_TempHealth : Inventory
{
	Inventory.MaxAmount 200
}