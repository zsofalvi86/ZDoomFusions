actor PPE_Q2RocketLauncher : RocketLauncher
{
	Weapon.SelectionOrder 2400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 7
	Inventory.PickupSound "items/q2weapon"
	Inventory.Pickupmessage "$GOT_PPE_RL"
	Tag "$TAG_PPE_RL"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		Q2RG A 1 A_WeaponReady
		Loop
	Deselect:
		Q2RG A 1 A_Lower
		Loop
	Select:
		Q2RG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_Q2Rocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/rocket/fire",1,1-(CountInv("PowerSilencer")*0.7))
		Q2RF ABC 2 bright
		TNT1 A 0 A_PlaySound("weapons/rocket/reload",6,1-(CountInv("PowerSilencer")*0.7))
		Q2RF DE 2 bright
		Q2RG A 2
		Q2RG BCDEF 3
		Q2RG A 6
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	Spawn:
		Q2RP A -1
		Stop
   }
}

actor PPE_Q2Rocket : PPE_SlowMissileBase
{
	Speed 22
	ProjectileKickBack 100
	Damage (100)
	DamageType "Quake2Rocket"
	Decal Scorch
	SeeSound ""
	DeathSound "weapons/q2exp"
	Obituary "$OB_MPROCKET"
	+ROCKETTRAIL
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/rocket/fly",6,1,1)
	Loopy:
		MISL A 1 bright
		loop
	Death:
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		TNT1 A 0 A_StopSound(6) // 8, 60, 52 - upscaled to match rocket
		TNT1 A 0 A_Explode(60,128,1,0,128)
		TNT1 A 0 A_Explode(52,128)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		TNT1 A 0 A_CheckFloor("Floor")
		BMWP AB 3 bright
		BMWP CDEFG 3 bright
		Stop
	Floor:
		BMFP AB 3 bright
		BMFP CDEFG 3 bright
		Stop
	}
}