Actor PPE_PowerSpawner : PPE_Spawner replaces BlurSphere
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteWithResult("SetSpawn",10) //use WithResult otherwise it'll run in parellel
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",2,1) == 1,"SpawnQuad") //quad trumps all
		TNT1 A 0 A_JumpIf(CallAcs("GetSpawn",10) == 3,"Spawn3")
		TNT1 A 0 A_JumpIf(CallAcs("GetSpawn",10) == 2,"Spawn2")
	Spawn1:
		TNT1 A 0 A_SpawnItem("PPE_Silencer")
		stop
	Spawn2:
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",0,101) == 1,"SpawnAdr") //try adrenaline first
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",1,100) == 1,"SpawnArm")
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",0,75) == 1,"SpawnMega")
		Goto Spawn1
	Spawn3:
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",1,100) == 1,"SpawnArm") //try armor first
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",0,101) == 1,"SpawnAdr")
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",0,75) == 1,"SpawnMega")
		Goto Spawn1
	SpawnArm:
		TNT1 A 0 A_SpawnPermanent("PPE_Q2RedArmor")
		stop
	SpawnAdr:
		TNT1 A 0 A_SpawnPermanent("PPE_Adrenaline_Upgrade")
		stop
	SpawnMega:
		TNT1 A 0 A_SpawnPermanent("PPE_Q2MegaHealth")
		stop
	SpawnQuad:
		TNT1 A 0 A_SpawnPermanent("PPE_QuadDamage")
		stop
	}
}

ACTOR PPE_Q2BerserkSpawner : PPE_Spawner replaces Berserk
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteWithResult("SetSpawn",9) //use WithResult otherwise it'll run in parellel
		TNT1 A 0 A_JumpIf(CallAcs("GetSpawn",9) == 2,"Spawn2")
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",2,1) == 1,"Spawn3Quad") //try quad first
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",0,1) == 1,"Spawn3Adr")
		Goto SpawnMega
	Spawn2:
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",0,1) == 1,"Spawn3Adr") //try adrenaline first
		TNT1 A 0 A_JumpIf(CallAcs("SpendExcess",2,1) == 1,"Spawn3Quad")
		Goto SpawnMega
	SpawnMega:
		TNT1 A 0 ACS_NamedExecuteWithResult("RegisterExcess",0,25)
		TNT1 A 0 ACS_NamedExecuteWithResult("RegisterExcess",2,1)
		TNT1 A 0 A_SpawnPermanent("PPE_Q2MegaHealth")
		stop
	SpawnAdr:
		TNT1 A 0 ACS_NamedExecuteWithResult("RegisterExcess",2,1)
		TNT1 A 0 A_SpawnPermanent("PPE_Adrenaline_Upgrade")
		stop
	SpawnQuad:
		TNT1 A 0 ACS_NamedExecuteWithResult("RegisterExcess",0,100)
		TNT1 A 0 A_SpawnPermanent("PPE_QuadDamage")
		stop
	}
}

Actor PPE_Silencer : PowerupGiver
{
	+INVENTORY.INVBAR
	+COUNTITEM
	Inventory.PickupSound "items/q2pickup"
	Inventory.Icon "SLNCA0"
	Inventory.PickupMessage "$GOTSILENCER"
	Inventory.MaxAmount 10
	Inventory.InterHubAmount 1
	Tag "$TAG_PPE_SILENCER"
	powerup.duration -60
	powerup.type PowerSilencer //zscript
	States
	{
	Spawn:
		SLNC A -1
		Stop
	}
}

Actor PPE_IRGoggles : PowerupGiver replaces Infrared
{
	+INVENTORY.INVBAR
	+COUNTITEM
	Inventory.PickupSound "items/q2pickup"
	Inventory.UseSound "items/visor/use"
	Inventory.Icon "2LITA0"
	Inventory.PickupMessage "$GOTIRGOGGLES"
	Inventory.MaxAmount 10
	Inventory.InterHubAmount 1
	Tag "$TAG_PPE_IRGOGGLES"
	powerup.duration -120
	powerup.type PowerIRGoggles
	States
	{
	Spawn:
		2LIT A -1 Bright
		Stop
	}
}

ACTOR PowerIRGoggles : PowerProtection
{
	Powerup.Color RedMap
	DamageFactor "Normal", 1.0
	Inventory.Icon "2LITA0"
}

Actor PPE_Invulnerability : CustomInventory replaces InvulnerabilitySphere
{
	+INVENTORY.INVBAR
	+COUNTITEM
	Inventory.PickupSound "items/q2pickup"
	Inventory.UseSound "items/invul/use"
	Inventory.Icon "2INVA0"
	Inventory.PickupMessage "$GOTINVULN"
	Inventory.MaxAmount 10
	Inventory.InterHubAmount 1
	Tag "$TAG_PPE_INVULN"
	States
	{
	Spawn:
		2INV A -1 Bright
		Stop
	Use:
		TNT1 A 0 A_GiveInventory("PPE_Q2Inv")
		TNT1 A 0 A_GiveInventory("PPE_Q2InvFake")
		stop
	}
}

Actor PPE_Q2Inv : PowerupGiver
{
	powerup.duration -30
	Powerup.Color 255, 0, 0, 0.125
	powerup.type PowerQ2Invulnerability
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
}

Actor PPE_Q2InvFake : PPE_Q2Inv
{
	powerup.duration -27
	Powerup.Color None
	powerup.type PowerQ2Invulnerability_Fake
}

Actor PowerQ2Invulnerability : PowerProtection
{
  DamageFactor "Normal", 0
  Inventory.Icon "2INVA0"
  ActiveSound "items/invul/absorb"
  SeeSound "items/invul/use"
}

Actor PowerQ2Invulnerability_Fake : PowerProtection
{
  DamageFactor "Normal", 1
  DeathSound "items/invul/fade"
}

Actor PPE_QuadDamage : CustomInventory
{
	+INVENTORY.INVBAR
	+COUNTITEM
	Inventory.PickupSound "items/q2pickup"
	Inventory.UseSound "items/quad/use"
	Inventory.Icon "2QDMA0"
	Inventory.PickupMessage "$GOTQUAD"
	Inventory.MaxAmount 10
	Inventory.InterHubAmount 1
	Tag "$TAG_PPE_QUAD"
	States
	{
	Spawn:
		2QDM A -1 Bright
		Stop
	Use:
		TNT1 A 0 A_GiveInventory("PPE_Quad")
		TNT1 A 0 A_GiveInventory("PPE_QuadFake")
		stop
	}
}

Actor PPE_Quad : PowerupGiver
{
	powerup.duration -50
	Powerup.Color 0, 0, 255, 0.125
	powerup.type PowerQuadDamage
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
}

Actor PPE_QuadFake : PPE_Quad
{
	powerup.duration -47
	Powerup.Color None
	powerup.type PowerQuadDamage_Fake
}

Actor PowerQuadDamage : PowerDamage
{
  DamageFactor "Normal", 4
  Inventory.Icon "2QDMA0"
  SeeSound "items/quad/pickup"
  //ActiveSound "items/quadshot"
}

Actor PowerQuadDamage_Fake : PowerDamage
{
  DamageFactor "Normal", 1
  DeathSound "items/quad/fade"
}

Actor PPE_EnviroSuit : CustomInventory replaces RadSuit
{
	+INVENTORY.INVBAR
	Inventory.PickupSound "items/q2pickup"
	Inventory.Icon "2ENVA0"
	Inventory.PickupMessage "$GOTENVIRO"
	Inventory.MaxAmount 10
	Inventory.InterHubAmount 1
	Tag "$TAG_PPE_ENVIRO"
	Height 46
	States
	{
	Spawn:
		2ENV A -1
		Stop
	Use:
		TNT1 A 0 A_GiveInventory("PPE_Q2Enviro")
		TNT1 A 0 A_GiveInventory("PPE_Q2Enviro_Fake")
		stop
	}
}

Actor PPE_Q2Enviro : PowerupGiver
{
	powerup.duration -60
	powerup.type PowerQ2Enviro
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
}

Actor PPE_Q2Enviro_Fake : RadSuit
{
	powerup.duration -57
	Powerup.Color None
	powerup.type PowerQ2Enviro_Fake
}

Actor PowerQ2Enviro : PowerIronFeet
{
  Powerup.Duration -60
  Powerup.Color 0, 255, 0, 0.125
}

Actor PowerQ2Enviro_Fake : PowerProtection
{
  DamageFactor "Normal", 1
  DeathSound "items/suit/fade"
}

Actor PPE_Q2Allmap : Allmap replaces Allmap
{
	Inventory.PickupSound "items/q2pickup"
	States
	{
	Spawn:
		Q2AM ABCDCB 6 Bright
		Loop
	}
}