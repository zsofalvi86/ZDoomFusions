Actor PPE_Q2RailGun : Chaingun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_RG"
	Tag "$TAG_PPE_RG"
	Inventory.PickupSound "items/q2weapon"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 7
	Weapon.AmmoType "PPE_Slugs"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 4
	Decal "PlasmaScorchLower"
	DamageType "Quake2Slug"
	States
	{
	Spawn:
		2RGW AAAABBCCDDDCCBB 1
		Loop
	Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"ReadyScope")
		2RGG EEEDDCCBBAAAABBCCDD 1 A_WeaponReady
		Loop
	ReadyScope:
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RailZoom",0)
	DeseLoop:
		2RGG A 1 A_Lower
		Loop
	Select:
		2RGG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/railgun/fire",1,1-(CountInv("PowerSilencer")*0.7)) // 150
		TNT1 A 0 A_RailAttack((240),0,1,6666FF,888888,RGF_FULLBRIGHT|RGF_SILENT,0,"PPE_Q2RailPuff")
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"FireScope")
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
	FireNormal:
		2RGF ABCDE 1 bright
		2RGG EEDCB 2
		2RGG A 31
		2RGG BCD 2
		TNT1 A 0 A_Refire
		Goto Ready
	FireScope:
		SSSS ZZZZZ 2 bright
		SSSS Z 16 bright
		SSSS ZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_RailZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RailZoom",0)
		SSSS Z 5 bright
		2RGG A 10
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor PPE_Q2RailPuff : BulletPuff//PPE_Q2BackstabPuff
{
	-ALLOWPARTICLES
	+ALWAYSPUFF
	+PUFFONACTORS
	RenderStyle "None"
}

Actor PPE_RailZoom : Inventory
{
	Inventory.MaxAmount 1
}