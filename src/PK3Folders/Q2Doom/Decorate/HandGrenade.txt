actor PPE_Q2HandGrenade : RocketLauncher
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "PPE_Grenades"
	Weapon.SlotNumber 2
	Inventory.Pickupmessage "$GOT_PPE_HG"
	Inventory.Icon "IGRNA0"
	Tag "$TAG_PPE_HG"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		2HGR A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
		2HGR A 1 A_Lower
		Loop
	Select:
		2HGR A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("weapons/handgrenade/twist",1,1-(CountInv("PowerSilencer")*0.7))
		2HGR BCD 2
		2HGR D 5
		TNT1 A 0 A_PlaySound("weapons/handgrenade/loop",6,1-(CountInv("PowerSilencer")*0.7),1)
		2HGR D 10
		TNT1 A 0 A_SetInventory("PPE_HG_Charge",0)
		TNT1 A 0 A_SetInventory("PPE_HG_UnloadTolerance",0)
		Goto GrenadeChargeInitial
	GrenadeChargeLoopy:
		2HGR D 1 
	GrenadeChargeInitial:
	    TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",0,"GrenadeChargeReleaseLoopy")
		TNT1 A 0 A_GiveInventory("PPE_HG_Charge",1)
		TNT1 A 0 A_ReFire("GrenadeChargeLoopy")
		Goto GrenadeChargeRelease
	GrenadeChargeReleaseLoopy:
		TNT1 A 0 A_JumpIfInventory("PPE_HG_UnloadTolerance",0,"GrenadeExplodeInHand")
		TNT1 A 0 A_GiveInventory("PPE_HG_UnloadTolerance",1)
		2HGR D 1 
		TNT1 A 0 A_ReFire("GrenadeChargeReleaseLoopy")
		Goto GrenadeChargeRelease
	GrenadeChargeRelease:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/handgrenade/fire",1,1-(CountInv("PowerSilencer")*0.7))
		TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",75,"GrenadeChargeRelease9")
		TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",72,"GrenadeChargeRelease8")
		TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",68,"GrenadeChargeRelease7")
		TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",64,"GrenadeChargeRelease6")
		TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",60,"GrenadeChargeRelease5")
		TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",56,"GrenadeChargeRelease4")
		TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",35,"GrenadeChargeRelease3")
		TNT1 A 0 A_JumpIfInventory("PPE_HG_Charge",14,"GrenadeChargeRelease2")
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade1",0,1,0,0,1,12)
		Goto Anim
	GrenadeChargeRelease2:
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade2",0,1,0,0,1,11)
		Goto Anim
	GrenadeChargeRelease3:
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade3",0,1,0,0,1,10)
		Goto Anim
	GrenadeChargeRelease4:
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade4",0,1,0,0,1,9)
		Goto Anim
	GrenadeChargeRelease5:
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade5",0,1,0,0,1,9)
		Goto Anim
	GrenadeChargeRelease6:
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade6",0,1,0,0,1,9)
		Goto Anim
	GrenadeChargeRelease7:
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade7",0,1,0,0,1,9)
		Goto Anim
	GrenadeChargeRelease8:
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade8",0,1,0,0,1,9)
		Goto Anim
	GrenadeChargeRelease9:
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenade9",0,1,0,0,1,9)
		Goto Anim
	GrenadeExplodeInHand:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireCustomMissile("PPE_Q2HandGrenadeX",0,1,0,0,1,9)
	Anim:
		2HGR EFGHI 2
		TNT1 A 10
		2HGR A 1 offset(-5,64)
		2HGR A 1 offset(-4,56)
		2HGR A 1 offset(-3,48)
		2HGR A 1 offset(-2,40)
		2HGR A 1 offset(-1,32)
		TNT1 A 0 A_ReFire
    	Goto Ready
	Spawn: stop
    }
}

Actor PPE_Q2HandGrenadeBase : PPE_SlowMissileBase
{
	var int user_countdown;
	+USEBOUNCESTATE
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+DONTBOUNCEONSKY
	+GRENADETRAIL
	-NOGRAVITY
	-BOUNCEAUTOOFF
	-BOUNCEAUTOOFFFLOORONLY
	SeeSound ""
	BounceSound ""
	BounceFactor 0.5
	WallBounceFactor 0.5
	Gravity 0.4
	BounceCount 99
	Speed 24
	ProjectileKickBack 100
	Damage (100)
	Decal Scorch
	SeeSound ""
	DeathSound "weapons/grenade/explode"
	Obituary "$OB_MPROCKET"
	Health 5
	Mass 10000
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",87)
		Goto FlyLoop
	Bounce:
		"####" "#" 1 
		{
			A_SetUserVar("user_countdown",user_countdown-1);
			If(user_countdown<1)
			{
				return state("ForceDeath");
			}
			Else If(vely>1 || velx>1 || velz>1 || vely<-1 || velx<-1 || velz<-1)
			{
				return state("RealBounce");
			}
			return state(0);
		}
		Loop
	RealBounce:
		TNT1 A 0 A_PlaySound("weapons/handgrenade/bounce")
	FlyLoop:
		HGRM CCDDEEFFGGHHAABB 1
		{
			A_SetUserVar("user_countdown",user_countdown-1);
			If(user_countdown<1)
			{
				return state("ForceDeath");
			}
			return state(0);
		}
		Loop
	ForceDeath:
		TNT1 A 0 A_Die
		TNT1 A 0 A_Scream
	Death:
		TNT1 A 0 A_SetInvulnerable
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_Explode(125,172,1,0,0,0,0,"None","Quake2Splash")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		TNT1 A 0 A_CheckFloor("Floor")
		BMWP AB 3 bright
		BMWP CDEFG 3 bright
		Stop
	Floor:
		BMFP AB 3 bright
		BMFP CDEFG 3 bright
		Stop
	}
}

Actor PPE_Q2HandGrenade1 : PPE_Q2HandGrenadeBase
{
	Speed 16
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",87)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenade2 : PPE_Q2HandGrenadeBase
{
	Speed 18
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",66)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenade3 : PPE_Q2HandGrenadeBase
{
	Speed 20
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",45)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenade4 : PPE_Q2HandGrenadeBase
{
	Speed 22
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",24)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenade5 : PPE_Q2HandGrenadeBase
{
	Speed 22
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",20)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenade6 : PPE_Q2HandGrenadeBase
{
	Speed 22
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",16)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenade7 : PPE_Q2HandGrenadeBase
{
	Speed 22
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",12)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenade8 : PPE_Q2HandGrenadeBase
{
	Speed 22
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",8)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenade9 : PPE_Q2HandGrenadeBase
{
	Speed 22
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",4)
		TNT1 A 0 A_Jump(256,"FlyLoop")
		Stop
	}
}

Actor PPE_Q2HandGrenadeX : PPE_Q2HandGrenadeBase
{
	Speed 1
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Death")
		Stop
	}
}

Actor PPE_HG_Charge : Inventory
{
	Inventory.MaxAmount 78
}

Actor PPE_HG_UnloadTolerance : Inventory
{
	Inventory.MaxAmount 8
}