Actor PPE_Bfg10k : BFG9000
{
+WEAPON.NOAUTOAIM
+WEAPON.NOALERT
Inventory.Pickupmessage "$GOT_PPE_BFG"
Tag "$TAG_PPE_BFG"
Weapon.SelectionOrder 2800
Weapon.SlotNumber 0
Weapon.AmmoUse1 10
Weapon.AmmoUse2 50
Weapon.AmmoType1 "PPE_Cells"
Weapon.AmmoType2 "PPE_Cells"
Inventory.PickupSound "items/weapon2"
Weapon.UpSound "weapons/switch"
Weapon.AmmoGive 50
States
	{
	Ready:
    	QBFG A 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
    	QBFG A 1 A_Lower
    	Loop
  	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
    	QBFG A 1 A_Raise
    	Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_Q3BfgBall",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/bfg/fire3",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QBFG BCDCB 1 bright
		QBFG A 2
		TNT1 A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("weapons/bfg/fire2",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
 		QBFG AAAAAAAAAAA 2 bright
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_Q2BFGBall",0,1,0,4,1,0)
		TNT1 A 0 A_PlaySound("items/quad/use",3,CountInv("PowerQuadDamage"))
    	QBFG BCD 3 bright
		QBFG CB 3 bright
		QBFG AAAAA 2
    	QBFG BCDCBA 2
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+1.2)
		Goto LightDone
  	AltFlash:
    	TNT1 A 1 A_Light1
		TNT1 AAA 1 A_SetPitch(pitch-1.4)
    	TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch-0.9)
		TNT1 A 0 A_Light(3)
		TNT1 AA 1 A_SetPitch(pitch+0.9)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.4)
		TNT1 A 0 A_Light1
    	Goto LightDone
  	Spawn:
		QBFW A 1
    	Loop
	}
}

Actor PPE_Q2BFGBall : BFGBall
{
	Speed 18
	Damage (0)
	RenderStyle Normal
	DeathSound "weapons/bfg/explode"
	DamageType "Quake2Cell"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0 A_PlaySound("weapons/bfg/fly",6,1.0,1)
	Loopy:
		BFGB ABCD 3 Bright 
		{
			A_RadiusGive("PPE_BFGBeamItem",760,RGF_MONSTERS|RGF_PLAYERS|RGF_NOTARGET);
			A_Explode((15),760,0,0,760); // 10
			A_SpawnParticle(green,SPF_FULLBRIGHT|SPF_RELATIVE,10,7,FRandom(0,360),0,0,0,Random(1,3),0,Random(-3,3));
			A_SpawnParticle(green,SPF_FULLBRIGHT|SPF_RELATIVE,10,7,FRandom(0,360),0,0,0,Random(1,3),0,Random(-3,3));
			A_SpawnParticle(green,SPF_FULLBRIGHT|SPF_RELATIVE,10,7,FRandom(0,360),0,0,0,Random(1,3),0,Random(-3,3));
			A_SpawnParticle(green,SPF_FULLBRIGHT|SPF_RELATIVE,10,7,FRandom(0,360),0,0,0,Random(1,3),0,Random(-3,3));
			A_SpawnParticle(green,SPF_FULLBRIGHT|SPF_RELATIVE,10,7,FRandom(0,360),0,0,0,Random(1,3),0,Random(-3,3));
			A_SpawnParticle(green,SPF_FULLBRIGHT|SPF_RELATIVE,10,7,FRandom(0,360),0,0,0,Random(1,3),0,Random(-3,3));
			A_SpawnParticle(green,SPF_FULLBRIGHT|SPF_RELATIVE,10,7,FRandom(0,360),0,0,0,Random(1,3),0,Random(-3,3));
			A_SpawnParticle(green,SPF_FULLBRIGHT|SPF_RELATIVE,10,7,FRandom(0,360),0,0,0,Random(1,3),0,Random(-3,3));
		}
		Loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(300,192,0) // 200
		TNT1 A 0 A_SpawnItem("PPE_GreenQuakeExplo")
		TNT1 A 0 A_Quake(8,9,0,640,"")
		TNT1 A 0 A_Quake(5,10,0,1280,"")
		TNT1 A 0 A_Quake(3,12,0,9064,"")
		BFGX ABCDE 3 Bright 
		BFGX F 2 Bright
		TNT1 A 0 A_CheckSight("SkipSpray")
		TNT1 A 0 A_BFGSpray("PPE_BFG10kFlash",160,0,360,1024,32,19,BFGF_MISSILEORIGIN) // 12.5
	SkipSpray:
		BFGX F 1 Bright
		BFGX GHIJK 3 Bright
		Stop
  }
}

ACTOR PPE_BFGBeamItem : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	Pickup:
		TNT1 A 0 A_CheckProximity("Spawn","PPE_Q2BFGBall",760,1,CPXF_CHECKSIGHT|CPXF_SETMASTER|CPXF_CLOSEST|CPXF_ANCESTOR)
		Stop
	Spawn:
		TNT1 A 0 A_SpawnItemEx("PPE_BFGBeamTarget",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_SETTRACER)
		//TNT1 A 0 A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION )
		Stop
	}
}

ACTOR PPE_BFGBeamTarget : PPE_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Warp(AAPTR_MASTER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION)
		TNT1 A 0 A_SpawnItemEx("PPE_BFGBeamShooter",0,0,0,0,0,0,0,SXF_SETTARGET)
		TNT1 AA 1 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,0,0.5)
		Stop
	}
}

ACTOR PPE_BFGBeamShooter : PPE_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_CustomRailgun((0),0,"none","none",RGF_SILENT|RGF_EXPLICITANGLE,1,0,"BulletPuff",0,0,760,0,5.0,0,"PPE_BFGLaser",0)
		Stop
	}
}

Actor PPE_BFGLaser : PPE_TrailBase
{
	States
	{
  	Spawn:
    	LAZG AAABBC 1 bright
    	Stop
	}
}

ACTOR PPE_BFG10kFlash : BFGExtra
{
  +NOBLOCKMAP
  +NOGRAVITY
  -ALLOWPARTICLES
  +PUFFONACTORS
  +MTHRUSPECIES
  RenderStyle Normal
  Alpha 1.0
  DamageType "BFGSplash"
  States
  {
  Spawn:
  Melee:
    BFEX ABCDEFGHIJKLMNO 2 Bright
    Stop
  Crash:
    TNT1 A 1
	Stop
  }
}

actor PPE_Q3BfgBall : PPE_SlowMissileBase
{
	Speed 65
	ProjectileKickBack 100
	Damage (0)
	DamageType "Quake3"
	Decal BFGLightning
	SeeSound ""
	DeathSound "weapons/bfg/explode3"
	Obituary "$OB_MPROCKET"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/rocket/fly",6,1,1)
	Loopy:
		BFTK ABCD 1 bright A_SpawnItem("PPE_Q3BfgTrail")
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_SpawnItem("PPE_GreenQuakeExploSmall")
		TNT1 A 0 A_Explode(256,128,0)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BFE3 ABCDE 3 Bright
		Stop
	}
}

Actor PPE_Q3BfgTrail : PPE_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 2
		BFTT ABCDE 2 bright
		Stop
	}
}