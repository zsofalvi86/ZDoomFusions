Actor PPE_Q3Bfg : PPE_Quake3WeaponBase
{
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	-WEAPON.NOAUTOFIRE
	Inventory.Pickupmessage "$GOT_PPE_Q3BFG"
	Tag "$TAG_PPE_Q3BFG"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 0
	Weapon.AmmoType "PPE_Nukes"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 4
	States
	{
	Spawn:
		3BFG W -1
		Stop
	Ready:
		3BFG AABBCCDD 1 A_WeaponReady
		Loop
	Deselect:
		3BFG A 1 A_Lower(18)
		Loop
	Select:
		3BFG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/q3/bfg/fire",1)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3BfgBall",0,1,0,1,1,0)
		3BFF ABCD 1 bright
		3BFG EFG 1
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

actor PPE_Q3BfgBall : PPE_SlowMissileBase
{
	Speed 65
	ProjectileKickBack 100
	Damage (128)
	DamageType "Quake3SplashImpact"
	Decal BFGLightning
	SeeSound ""
	DeathSound "weapons/q3/bfg/explode"
	Obituary "$OB_MPROCKET"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/rocket/fly",6,1,1)
	Loopy:
		BFTK ABCD 1 bright A_SpawnItem("PPE_Q3BfgTrail")
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(40,128,1,0,128,0,0,"None","Normal")
		TNT1 A 0 A_Explode(80,128,1,0,0,0,0,"None","Normal")
		TNT1 A 0 A_SpawnItem("PPE_GreenQuakeExploSmall")
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BFE3 ABCDE 3 Bright
		Stop
	}
}

Actor PPE_Q3BfgTrail : PPE_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 2
		BFTT ABCDE 2 bright
		Stop
	}
}