Actor PPE_ChainNail : PPE_SlowMissileBase
{
	Decal BulletChip
	SeeSound ""
	DeathSound ""
	Speed 40
	Damage (12)
	Radius 5
	Height 4
	+BLOODSPLATTER
	States
	{
	Spawn:
	    PUFF A 4 bright
		Loop
	Death:
	Crash:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/nailgun/wallhit",4)
		TNT1 A 0 A_PlaySound("weapons/ric",3)
		TNT1 A 0 A_SpawnItem("PPE_YellowQuakeExploTiny")
		TNT1 A 0 A_Explode(24,96)
		PUFF AB 2 bright
		PUFF CD 2 bright
		stop
	XDeath:
		TNT1 A 0 A_PlaySound("weapons/ric",3)
		TNT1 A 0 A_SpawnItem("PPE_YellowQuakeExploTiny")
		TNT1 A 0 A_Explode(24,96)
		stop
	}
}

Actor PPE_Chaingun_Q2 : Chaingun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_CG"
	Tag "$TAG_PPE_CG"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 5
	Weapon.AmmoType1 "PPE_Bullets"
	Weapon.AmmoType2 "PPE_Bullets"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 40
	DamageType "Quake2BulletCg"
	Decal "BulletChip"
	States
	{
	Spawn:
		Q2CW A -1
		Stop
	Ready:
		Q2CG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		Q2CG A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		Q2CG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_SetInventory("PPE_ChaingunOvershoot",0)
		TNT1 A 0 A_PlaySound("weapons/chaingun/windup",5)
	FireLoop:
		TNT1 A 0 A_JumpIfInventory("PPE_ChaingunSpin",2,1)
		Goto SkipLoopSound
		TNT1 A 0 A_PlaySound("weapons/chaingun/loop",6,1,1)
	SkipLoopSound:
		TNT1 A 0 A_JumpIfInventory("PPE_ChaingunSpin",6,"FireSpeed3")
		TNT1 A 0 A_JumpIfInventory("PPE_ChaingunSpin",2,"FireSpeed2")
	FireSpeed1:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		Q2CF AAB 1 bright
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/fire1",1) // 8
		TNT1 A 0 A_FireBullets(3.3,3.3,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		Q2CF CCDD 1 bright
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/fire1",1)
		TNT1 A 0 A_FireBullets(3.3,3.3,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_ReFire("ReFire")
		Goto Released
	FireSpeed2:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		Q2CF AB 1 bright
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/fire1",1)
		TNT1 A 0 A_FireBullets(4.3,4.3,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		Q2CF CD 1 bright
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/fire1",1)
		TNT1 A 0 A_FireBullets(4.3,4.3,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_ReFire("ReFire")
		Goto Released
	FireSpeed3:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		Q2CF A 1 bright
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/fire1",1)
		TNT1 A 0 A_FireBullets(5.3,5.3,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		Q2CF B 1 bright
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/fire1",1)
		TNT1 A 0 A_FireBullets(5.3,5.3,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		Q2CF C 1 bright
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/fire1",1)
		TNT1 A 0 A_FireBullets(5.3,5.3,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		Q2CF D 1 bright
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/fire1",1)
		TNT1 A 0 A_FireBullets(5.3,5.3,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_ReFire("ReFire")
		Goto Released
	ReFire:
		TNT1 A 0 A_SetInventory("PPE_ChaingunOvershoot",0)
		TNT1 A 0 A_GiveInventory("PPE_ChaingunSpin",1)
		Goto FireLoop
	Released:
		TNT1 A 0 A_JumpIfInventory("PPE_ChaingunSpin",2,1)
		Goto SkipWindDownSound
		TNT1 A 0 A_PlaySound("weapons/chaingun/winddown",6)
	SkipWindDownSound:
		TNT1 A 0 A_JumpIfInventory("PPE_ChaingunOvershoot",0,"WindDown")
		TNT1 A 0 A_GiveInventory("PPE_ChaingunOvershoot",1)
		Goto FireLoop
	WindDown:
		TNT1 A 0 A_SetInventory("PPE_ChaingunSpin",0)
		TNT1 A 0 A_JumpIfInventory("PPE_ChaingunSpin",2,1)
		Goto WindDownSpeed1
		Q2CX ABCD 1 bright
		Q2CG ABCD 1
		Q2CG ABCD 2
		Q2CG AA 1
		Q2CG BBB 1
		Q2CG CCCC 1
		Goto Ready
	WindDownSpeed1:
		Q2CX ABCD 2 bright
		Q2CG AA 1
		Q2CG BBB 1
		Q2CG CCCC 1
		Goto Ready
	Flash:
		TNT1 A 0
		TNT1 A 1 A_Light(Random(0,3))
		Goto LightDone
	AltFire:
		Q2CG BBBB 1 
		TNT1 A 0 A_PlaySound("weapons/chaingun/xloop",6)
		Q2CG CCCDD 1
		TNT1 A 0 A_ReFire("AltHold")
		Goto AltSpindown
	AltHold:
		TNT1 A 0 A_PlaySound("weapons/chaingun/xloop",6,1,1)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/xfire",1)
		TNT1 AA 0 A_FireBullets(4.1,4.1,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		//TNT1 A 0 A_FireCustomMissile("PPE_ChainNail",0,1,0,3,1,0)
		Q2CF AB 2 bright
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/chaingun/xfire",1)
		TNT1 AA 0 A_FireBullets(4.1,4.1,-1,(12),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		//TNT1 A 0 A_FireCustomMissile("PPE_ChainNail",0,1,0,3,1,0)
		Q2CF CD 2 bright
		TNT1 A 0 A_ReFire("AltHold")
		Q2CX ABCD 2 bright
	AltSpindown:
		Q2CG A 2
		Q2CG B 3
		TNT1 A 0 A_StopSound(6)
		Q2CG C 4
		Goto Ready
	}
}

Actor PPE_ChaingunOvershoot : Inventory
{
	Inventory.MaxAmount 3
}

Actor PPE_ChaingunSpin : Inventory
{
	Inventory.MaxAmount 14
}