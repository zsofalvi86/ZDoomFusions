Actor PPE_Q2Shotgun : PPE_Quake2WeaponBase
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_SG"
	Tag "$TAG_PPE_SG"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 2
	Weapon.AmmoType "PPE_Q2Shells"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Decal "BulletChip"
	DamageType "Quake2Shell"
	States
	{
	Ready:
    	Q2SG A 1 A_WeaponReady
    	Loop
  	Deselect:
		Q2SG A 1 A_Lower(18)
    	Loop
  	Select:
		Q2SG A 1 A_Raise(18)
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash //4 - should one shot imps
		TNT1 A 0 A_FireBullets(4.0,4.0,12,(6),"PPE_BulletPuffQ2",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/q2/shotgun/fire",1,1-(CountInv("PowerSilencer")*0.7))
    	Q2SF ABC 2 bright
		TNT1 A 0 A_PlaySound("weapons/q2/shotgun/reload",4,1-(CountInv("PowerSilencer")*0.7))
		Q2SG EFGHIJ 2
		Q2SG JIHGFE 2
		//Q2SG A 8
		Q2SG BCDA 2
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.45)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.9)
		Goto LightDone
  	Spawn:
    	Q2SW A -1
    	Stop
	}
}

