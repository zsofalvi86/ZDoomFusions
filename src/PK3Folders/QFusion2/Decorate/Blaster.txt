ACTOR PPE_Blaster_Q2 : Fist
{
  Weapon.SelectionOrder 2000
  Weapon.AmmoUse1 0
  Weapon.AmmoUse2 2
  Weapon.AmmoType2 "PPE_Cells"
  Weapon.AmmoGive 0
  Weapon.SlotNumber 1
  Obituary "$OB_MPBLASTER"
  +WEAPON.NOALERT
  +WEAPON.NOAUTOAIM
  +WEAPON.AMMO_OPTIONAL
  -WEAPON.WIMPY_WEAPON
  Inventory.Pickupmessage "$GOT_PPE_BLS"
  Tag "$TAG_PPE_BLS"
  Inventory.PickupSound "items/weapon2"
  Weapon.UpSound "weapons/switch"
  States
  {
  Spawn:
	BSTW A -1
    Stop
  Ready:
    BSTG A 1 A_WeaponReady
    Loop
  Deselect:
	TNT1 A 0 A_Lower
	TNT1 A 0 A_Lower
    BSTG A 1 A_Lower
    Loop
  Select:
	TNT1 A 0 A_Raise
	TNT1 A 0 A_Raise
    BSTG A 1 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_FireCustomMissile("PPE_BlasterBolt",0,0,0,5,1,0)
	TNT1 A 0 A_PlaySound("weapons/blaster/fire1",1)
	TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
	Goto FireAnim
  AltFire:
	TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_FireCustomMissile("PPE_BlasterBolt",0,1,0,5,1,0)
	TNT1 A 0 A_PlaySound("weapons/blaster/fire3",1)
	TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
	Goto FireAnim
  FireAnim:
	TNT1 A 0 A_GunFlash
    BSTF ABC 2 bright
    BSTG CBBAA 2
    TNT1 A 0 A_ReFire
    Goto Ready
  Flash:
    TNT1 A 1 A_Light1
	TNT1 AAA 1 A_SetPitch(pitch-0.16)
	TNT1 AA 1 A_SetPitch(pitch+0.24)
    Goto LightDone
  }
}

Actor PPE_BlasterBolt : PPE_SlowMissileBase
{
	SeeSound ""
	DeathSound "weapons/blaster/hit"
	Speed 35
	Damage (15)//10 - should 2 shot both zombieman and shotgunners
	DamageType "Quake2Blaster"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/blaster/fly",6,1.0,1)
	Loopy:
		GLAP A 1 bright
		TNT1 AA 0 A_SpawnParticle(orange,SPF_FULLBRIGHT|SPF_RELATIVE,20,7,180,7,0,0,FRandom(0,1),FRandom(-0.2,0.2),FRandom(-0.2,0.2))
		loop
	Crash:
	XDeath:
	Death:
		TNT1 A 0 A_StopSound(6)
		GPSX ABCDE 3 bright
		stop
	}
}