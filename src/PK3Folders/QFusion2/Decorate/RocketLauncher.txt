actor PPE_QRocket : Rocket
{
	+FORCERADIUSDMG
	Speed 35
	Damage (0)
	SeeSound ""
	DeathSound "weapons/rocket/explode"
	Obituary "$OB_MPROCKET"
	DamageType "Quake1Rocket"
	States
	{
	Death: // 40, 80
		TNT1 A 0 A_Explode(78,128,1,0,128)
		TNT1 A 0 A_Explode(156,128)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor PPE_QMiniRocket : PPE_QRocket
{
	+FORCERADIUSDMG
	Speed 30
	Damage (0)
	DeathSound "weapons/rocket/explode"
	Obituary "$OB_MPROCKET"
	DamageType "Quake1Rocket"
	States
	{
	Death:
		TNT1 A 0 A_Explode(39,128,1,0,128)
		TNT1 A 0 A_Explode(78,128)
		TNT1 A 0 A_Quake(3,4,0,224,"")
		TNT1 A 0 A_Quake(2,5,0,512,"")
		TNT1 A 0 A_Quake(1,5,0,3072,"")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExploSmall")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor PPE_QMiniRocketTarget : PPE_QMiniRocket
{
	+SEEKERMISSILE
	+SCREENSEEKER
	DamageType "Quake1Rocket"
	States
	{
	Spawn:
		MISL AAAA 1 bright
		TNT1 A 0 A_Tracer2
		MISL A 1 A_SeekerMissile(10,25,SMF_LOOK|SMF_CURSPEED,256,4)
		Goto Super::Spawn
	}
}

actor PPE_RocketLauncher : RocketLauncher
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 2
	Weapon.AmmoType1 "PPE_Rockets"
	Weapon.AmmoType2 "PPE_Rockets"
	Weapon.SlotNumber 7
	Weapon.UpSound "weapons/switch"
	Weapon.SisterWeapon "PPE_RocketLauncher_P"
	Inventory.PickupSound "items/weapon1"
	Inventory.Pickupmessage "$GOT_PPE_RL"
	Tag "$TAG_PPE_RL"
	+NOAUTOFIRE
	+NOALERT
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		QRLG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		QRLG A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		QRLG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_QRocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/rocket/fire1",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QRLF ABCD 2 bright
		QRLG A 20
		TNT1 A 0 A_ReFire
    	Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_JumpIfTargetInLos("FireTargeted",0,JLOSF_DEADNOJUMP|JLOSF_ALLYNOJUMP|JLOSF_COMBATANTONLY|JLOSF_NOAUTOAIM,256)
		TNT1 A 0 A_FireCustomMissile("PPE_QMiniRocket",-6,1,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_QMiniRocket",-2,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_QMiniRocket",2,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_QMiniRocket",6,0,0,1,1,0)
		Goto Anim
	FireTargeted:
		TNT1 A 0 A_FireCustomMissile("PPE_QMiniRocketTarget",-1,1,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_QMiniRocketTarget",0,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_QMiniRocketTarget",0,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_QMiniRocketTarget",1,0,0,1,1,0)
	Anim:
		TNT1 A 0 A_PlaySound("weapons/rocket/fire1",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QMRF ABCD 2 bright
		QMRG A 20
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	Spawn:
		QRLP A -1
		Stop
   }
}

Actor PPE_RocketLauncher_P : PPE_RocketLauncher
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "PPE_MultiRockets"
	States
	{
	Ready:
		QMRG A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfTargetInLos("FireTargeted",0,JLOSF_DEADNOJUMP|JLOSF_ALLYNOJUMP|JLOSF_COMBATANTONLY|JLOSF_NOAUTOAIM,256)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocket",-6,1,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocket",-2,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocket",2,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocket",6,0,0,1,1,0)
		Goto Anim
	FireTargeted:
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocketTarget",-1,1,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocketTarget",0,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocketTarget",0,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocketTarget",1,0,0,1,1,0)
	Anim:
		TNT1 A 0 A_PlaySound("weapons/rocketf",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QMRF ABCD 2 bright
		QMRG A 20
		TNT1 A 0 A_ReFire
    	Goto Ready
	}
}