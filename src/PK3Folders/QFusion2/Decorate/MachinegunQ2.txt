Actor PPE_Q2MachineGun : PPE_Quake2WeaponBase
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_MG"
	Tag "$TAG_PPE_MG"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 4
	Weapon.AmmoType "PPE_Bullets"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Decal "BulletChip"
	DamageType "Quake2Bullet"
	States
	{
	Spawn:
		2MGW A -1
		Stop
	Ready:
		2MGG A 1 A_WeaponReady
		Loop
	Deselect:
		2MGG A 1 A_Lower(18)
		Loop
	Select:
		2MGG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_SetInventory("PPE_PitchToReset",90+pitch)
	Fire1:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters // 8
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/q2/machinegun/fire",1,1-(CountInv("PowerSilencer")*0.7))
	    TNT1 A 0 A_FireBullets(4.2,4.2,-1,(11),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		2MGF ABC 1 bright A_SetPitch(pitch-0.35)
		TNT1 A 0 A_Refire("Hold")
		Goto ResetPitch
	Hold:
		2MGG A 1 bright
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/q2/machinegun/fire",1,1-(CountInv("PowerSilencer")*0.7))
	    TNT1 A 0 A_FireBullets(4.2,4.2,-1,(11),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		2MGF DEF 1 bright A_SetPitch(pitch-0.35)
		TNT1 A 0 A_ReFire("Fire1")
		Goto ResetPitch
	ResetPitch:
		TNT1 A 0 A_SetPitch(CountInv(PPE_PitchToReset)-90)
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		//TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		//TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor PPE_PitchToReset : Inventory
{
	Inventory.MaxAmount 180
}