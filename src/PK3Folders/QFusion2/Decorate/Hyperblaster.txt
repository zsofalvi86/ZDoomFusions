Actor PPE_Hyperblaster : PlasmaRifle
{
	+DONTGIB
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_HYB"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Tag "$TAG_PPE_HYB"
	Weapon.SelectionOrder 105
	Weapon.SlotNumber 8
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoType1 "PPE_Cells"
	Weapon.AmmoType2 "PPE_Cells"
	Weapon.AmmoGive 40
	Weapon.SisterWeapon "PPE_Hyperblaster_P"
	States
	{
	Ready:
    	HHYB A 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_StopSound(7)
	DeseLoop:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
    	HHYB A 1 A_Lower
    	Loop
  	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
    	HHYB A 1 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/windup",5)
		TNT1 A 0 A_SetInventory("PPE_XPos",3)
		TNT1 A 0 A_SetInventory("PPE_YPos",6)
	FireHyperblaster:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/fire",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireCustomMissile("PPE_HyperBlasterBolt",0,1,CountInv("PPE_XPos")-3,CountInv("PPE_YPos")+1,1,0)
		Goto FireAnimation
	FireAnimation:
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",3,"FireCont3")
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",2,"FireCont2")
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",1,"FireCont1")
		TNT1 A 0 A_Jump(256,"FireCont")
	// Remember, each section sets the next section's x and y
	FireCont: // HybState: 0, frames: 4
		TNT1 A 0 A_GiveInventory("PPE_HybState",1)
		HHYF A 1 bright
		HHYF B 1 bright
		HHYF C 1 bright
		HHYQ A 1 bright
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
		TNT1 A 0 A_SetInventory("PPE_YPos",3)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/loop",6,1,1)
		TNT1 A 0 A_ReFire("FireHyperblaster")
		Goto Spindown
	FireCont1: // HybState: 1, frames: 3
		TNT1 A 0 A_GiveInventory("PPE_HybState",1)
		HHYF D 1 bright
		HHYF E 1 bright
		HHYF F 1 bright
		TNT1 A 0 A_SetInventory("PPE_XPos",3)
		TNT1 A 0 A_SetInventory("PPE_YPos",0)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/loop",6,1,1)
		TNT1 A 0 A_ReFire("FireHyperblaster")
		goto Spindown
	FireCont2: // HybState: 2, frames: 4
		TNT1 A 0 A_GiveInventory("PPE_HybState",1)
		HHYF G 1 bright
		HHYF H 1 bright
		HHYF I 1 bright
		HHYQ B 1 bright
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
		TNT1 A 0 A_SetInventory("PPE_YPos",3)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/loop",6,1,1)
		TNT1 A 0 A_ReFire("FireHyperblaster")
		Goto Spindown
	FireCont3: // HybState: 3, frames: 3
		TNT1 A 0 A_TakeInventory("PPE_HybState",255)
		HHYF J 1 bright
		HHYF K 1 bright
		HHYF L 1 bright
		TNT1 A 0 A_SetInventory("PPE_XPos",3)
		TNT1 A 0 A_SetInventory("PPE_YPos",6)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/loop",6,1,1)
		TNT1 A 0 A_ReFire("FireHyperblaster")
		goto Spindown
	Spindown:
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",3,"SpindownStartSV2")
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",2,"SpindownStartNV1")
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",1,"SpindownStartNV2")
		HHYB BC 1
		HHYQ A 1
		Goto SpindownStartNV1
	SpindownStartSV2:
		HHYB DEABC 1
		HHYQ A 1
	SpindownStartNV1:
		HHYQ BCA 1
	SpindownStartNV2:
		HHYQ BCAB 1
	SpindownEnd:
		//TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/winddown",6)
		HHYB DEABC 1
		HHYQ ABC 1
		HHYQ ABC 2
		HHYB D 3
		HHYB E 4
		HHYB A 1
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
		TNT1 A 0 A_SetInventory("PPE_YPos",0)
		TNT1 A 0 A_SetInventory("PPE_HybState",0)
		Goto Ready
  	Flash:
    	TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.3)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.6)
		Goto LightDone
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("weapons/plasmabeam/fire",6,1,1)
		TNT1 A 0 A_PlaySound("weapons/plasmabeam/run",7,1,1)
		HHYB A 1 bright A_GunFlash("FireAnimFunc") // 15 - upscaled to more or less match other cell users
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((20),0,1,"none","none",RGF_SILENT,0,"none",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		HHYB BC 1 bright A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((20),0,1,"none","none",RGF_SILENT,0,"none",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		TNT1 A 0 A_ReFire("AltFire2")
		Goto WinddownBeam
	AltFire2:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("weapons/plasmabeam/fire",6,1,1)
		TNT1 A 0 A_PlaySound("weapons/plasmabeam/run",7,1,1)
		HHYQ A 1 bright A_GunFlash("FireAnimFunc") // 15 - upscaled to more or less match other cell users
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((20),0,1,"none","none",RGF_SILENT,0,"none",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		HHYQ B 1 bright A_GunFlash("FireAnimFunc")
		HHYB D 1 bright A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((20),0,1,"none","none",RGF_SILENT,0,"none",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		HHYB E 1 bright A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_ReFire("AltFire")
		HHYB ABC 1
	WinddownBeam:
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_StopSound(7)
		TNT1 A 0 A_PlaySound("weapons/plasmabeam/winddown",7)
		HHYB ABCDE 1
		HHYQ ABC 1
		HHYB DDEEE 1
		Goto Ready
	FireAnimFunc:
		TNT1 A 0 A_JumpIfInventory("PPE_XPos",10,1)
		Goto SkipReset
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
	SkipReset:
		TNT1 A 0 A_GiveInventory("PPE_XPos",1)
		TNT1 A 0 A_RailAttack((0),0,0,"none","none",RGF_SILENT,0,"PPE_PainlessPuff",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		TNT1 A 0 A_RailAttack((0),0,0,"none","none",RGF_SILENT,0,"PPE_PainlessPuff",0,0,3500,0,32+(CountInv("PPE_XPos")*12),0,"PPE_PlasmaRing",3)
		TNT1 A 1 A_Light(Random(1,3))
		Goto LightDone
  	Spawn:
		HYBG AAAAAABBCCDDCCBB 1
		Loop
	}
}

Actor PPE_Hyperblaster_P : Plasmarifle
{
	+NOAUTOAIM
	+WEAPON.POWERED_UP
	Inventory.Pickupmessage "$GOT_PPE_PB"
	Tag "$TAG_PPE_PB"
	Decal PlasmaScorch
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.AmmoUse 1
	Weapon.AmmoType "PPE_Cells"
	Weapon.AmmoGive 40
	Weapon.SlotNumber 8
	Weapon.KickBack 125
	Weapon.SisterWeapon "PPE_Hyperblaster"
	DamageType "Quake2Plasma"
	States
	{
	Ready:
		PBMG ABB 1
	ReadyLoop:
		PBMG CCDDEEFFEEDDCC 1 A_WeaponReady
		Loop
  	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
    	PBMG A 1 A_Lower
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
    	PBMG A 1 A_Raise
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
    	Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("weapons/plasmabeam/fire",6,1,1)
		PBMF A 1 bright A_GunFlash("FireAnimFunc") // 15 - upscaled to more or less match other cell users
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((20),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"none",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		PBMF BC 1 bright A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((20),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"none",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		TNT1 A 0 A_ReFire("Fire2")
		Goto Winddown
	Fire2:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		PBMF CB 1 bright A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((20),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"none",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		PBMF AB 1 bright A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((20),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"none",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		PBMF C 1 bright A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_ReFire("Fire")
	Winddown:
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
		TNT1 A 0 A_StopSound(6)
		PBMF DEF 2 bright
		PBMG CC 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready
	FireAnimFunc:
		TNT1 A 0 A_JumpIfInventory("PPE_XPos",10,1)
		Goto SkipReset
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
	SkipReset:
		TNT1 A 0 A_GiveInventory("PPE_XPos",1)
		TNT1 A 0 A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_PainlessPuff",0,0,3500,0,9,0,"PPE_PlasmaBeamTrail",3)
		TNT1 A 0 A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_PainlessPuff",0,0,3500,0,32+(CountInv("PPE_XPos")*12),0,"PPE_PlasmaRing",3)
		TNT1 A 1 A_Light(Random(1,3))
		Goto LightDone
	Spawn:
    	PBMW AAAAAABBCCDDCCBB 1
		Loop
	}
}

Actor PPE_HyperBlasterBolt : PPE_SlowMissileBase
{
	var int user_missiletime;
	SeeSound ""
	DeathSound "weapons/hyperblaster/hit"
	//BounceSound "weapons/hyperblaster/bounce"
	Speed 35
	Damage (23) // 20 - upscaled to match plasma
	DamageType "Quake2Cell"
	//BounceType Doom
	//BounceCount 4
	//BounceFactor 1.0
	//WallBounceFactor 1.0
	//+USEBOUNCESTATE
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_missiletime",200)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/fly",6,1.0,1)
	MissileLoop:
		TNT1 A 0 A_JumpIf(user_missiletime==0,"StopDeath")
		TNT1 A 0 A_SetUserVar("user_missiletime",user_missiletime-1)
		GLAP A 1 bright
		loop
	//Bounce:
	//	TNT1 A 0 A_ChangeFlag("HITOWNER",1)
	//	Goto MissileLoop
	StopDeath:
		TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
	Crash:
	XDeath:
	Death:
		TNT1 A 0 A_StopSound(6)
		GPSX ABCDE 3 bright
		stop
	}
}

Actor PPE_PlasmaBeamTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(64,"Despawn")
    	PBMB BC 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_PlasmaRing : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(64,"Despawn")
    	PBMU CDE 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_PainlessPuff : BulletPuff
{
	-ALLOWPARTICLES
	+PAINLESS
	+BLOODLESSIMPACT
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		Stop
	}
}

Actor PPE_HybState : Inventory
{
	Inventory.MaxAmount 3
}

Actor PPE_XPos : Inventory
{
	Inventory.MaxAmount 100
}

Actor PPE_YPos : Inventory
{
	Inventory.MaxAmount 100
}