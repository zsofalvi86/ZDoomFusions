Actor PPE_Q3SuperShotgun : PPE_Quake3WeaponBase
{
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	Inventory.Pickupmessage "$GOT_PPE_SSG"
	Tag "$TAG_PPE_SSG"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 3
	Weapon.AmmoUse 2
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoGive 16
	Decal "BulletChip"
	States
	{
	Ready:
    	Q3SG A 1 A_WeaponReady
    	Loop
  	Deselect:
		Q3SG A 1 A_Lower(18)
    	Loop
  	Select:
		Q3SG A 1 A_Raise(18)
    	Loop
  	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		// 1st row, 1 pellet
		TNT1 A 0 A_FireBullets(0,-4.0,-1,(8),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 2nd row, 2 pellets
		TNT1 A 0 A_FireBullets(3.7,-2.6,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-3.7,-2.6,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 3rd row, 2 pellets
		TNT1 A 0 A_FireBullets(1.5,-1.6,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-1.5,-1.6,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 4th row, 4 pellets
		TNT1 A 0 A_FireBullets(6.5,0,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(3.2,0,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-3.2,0,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-6.5,0,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 5th row, 2 pellets
		TNT1 A 0 A_FireBullets(1.5,1.6,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-1.5,1.6,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 6th row, 2 pellets
		TNT1 A 0 A_FireBullets(3.7,2.6,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-3.7,2.6,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 7st row, 1 pellet
		TNT1 A 0 A_FireBullets(0,4.0,-1,(8),"PPE_RicBulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_PlayWeaponSound("weapons/q3/shotgun/fire")
		Q3SF ABC 2 bright
		Q3SG A 6 Offset(0,37)
		Q3SG B 1 Offset(0,32)
		Q3SG BCC 1
		TNT1 A 0 A_FireCustomMissile("PPE_Q3SGShellCasingShooterL",0,0,0,6)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3SGShellCasingShooterR",0,0,0,6)
		Q3SG DDEEFFGGHH 1
		Q3SG AAAAAAAAA 1
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.5)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.0)
		Goto LightDone
  	Spawn:
    	Q3SP A -1
    	Stop
	}
}

Actor PPE_Q3SGShellCasingShooterL : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_ShellCasing",0,0,FRandom(62,45),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-16,-22))
		Stop
	}
}

Actor PPE_Q3SGShellCasingShooterR : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_ShellCasing",0,0,FRandom(-62,-45),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-16,-22))
		Stop
	}
}