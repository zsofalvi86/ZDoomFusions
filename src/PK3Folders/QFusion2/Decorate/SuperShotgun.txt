Actor PPE_SuperShotgun : SuperShotgun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_SSG"
	Tag "$TAG_PPE_SSG"
	Inventory.PickupSound "items/weapon1"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 3
	Weapon.AmmoUse 2
	Weapon.AmmoType "PPE_Shells"
	Weapon.SisterWeapon "PPE_SuperShotgun_P"
	Weapon.AmmoGive 16
	Decal "BulletChip"
	DamageType "Quake1Shell"
	States
	{
	Ready:
    	QDGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
  	Deselect:
		QDGG A 1 A_Lower(18)
    	Loop
	Select:
		TNT1 A 0 A_JumpIfInventory("PPE_SuperShotgunAlt",1,1)
		Goto SeLoop
		TNT1 A 0 A_GiveInventory("PPE_AltAmmo")
  	SeLoop:
		QDGG A 1 A_Raise(18)
    	Loop
  	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters // 4
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireBullets(7.5,4.0,14,(6),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlayWeaponSound("weapons/dbshotgun/fire")
		QDGF ABC 2 bright
		QDGG BCD 2
		QDGG EFGH 1
		QDGG IJKL 2
		QDGG A 1
		TNT1 A 0 A_ReFire
		Goto Ready
	Reload:
		TNT1 A 0 A_SetInventory("PPE_SuperShotgunAlt",1)
		TNT1 A 0 A_GiveInventory("PPE_AltAmmo")
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.2)
		Goto LightDone
  	Spawn:
    	QDGP A -1
    	Stop
	}
}

Actor PPE_SuperShotgun_P : PPE_SuperShotgun
{
	+WEAPON.POWERED_UP
	Weapon.AmmoType "PPE_HeavyShells"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.SisterWeapon "PPE_SuperShotgun"
	Decal "BulletChip"
	DamageType "Quake2Shell"
	States
	{
	Ready:
    	Q2SS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("PowerAlt",1)
  	DeseLoop:
		Q2SS A 1 A_Lower(18)
    	Loop
  	Select:
		Q2SS A 1 A_Raise(18)
    	Loop
  	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters // 6 - should two shot cacodemons
		TNT1 A 0 A_FireBullets(7.5,4.0,20,(11),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlaySound("weapons/supershotgun/fire1",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		Q2SS BCD 2 bright
		Q2SS A 4
		Q2SS EEEFFFGG 1
		Q2SS HHIIJJKKKJJII 1
		Q2SS HHGGFFEEA 1
		Goto Ready
	Reload:
		TNT1 A 0 A_SetInventory("PPE_SuperShotgunAlt",0)
		TNT1 A 0 A_TakeInventory("PowerAlt",1)
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAA 1 A_SetPitch(pitch-0.61)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch-0.61)
		TNT1 A 0 A_Light1
		TNT1 AAA 1 A_SetPitch(pitch+1.22)
		Goto LightDone
  	Spawn:
    	Q2SS W -1
    	Stop
	}
}