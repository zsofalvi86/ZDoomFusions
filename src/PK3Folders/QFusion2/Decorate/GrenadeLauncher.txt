actor PPE_QGrenade : Rocket
{
	+FORCERADIUSDMG
	+USEBOUNCESTATE
	-ROCKETTRAIL
	+GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
	SeeSound ""
	BounceSound "weapons/grenade/bounce1"
	DeathSound "weapons/grenade/explode"
	BounceFactor 0.6
	WallBounceFactor 0.6
	Gravity 0.4
	BounceCount 4
	Speed 25
	Damage (0)
	DamageType "Quake1Rocket"
	States
	{
	Spawn:
		DGRD A 1
		Loop
	Bounce:
		DGRR ABCDEFGHIJKL 2
		Loop
	Death:
		TNT1 A 0 A_NoGravity // 40, 80
		TNT1 A 0 A_Explode(78,128,1,0,128)
		TNT1 A 0 A_Explode(156,128)
		TNT1 A 0 A_Quake(4,5,0,256,"")
		TNT1 A 0 A_Quake(2,6,0,720,"")
		TNT1 A 0 A_Quake(1,6,0,4096,"")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor PPE_QGrenadeMulti : PPE_QGrenade
{
  var int user_angle;
  SeeSound ""
  BounceSound "weapons/grenade/bounce2"
  DeathSound ""
  BounceFactor 0.6
  WallBounceFactor 0.6
  Gravity 0.4
  BounceCount 4
  Speed 25
  Damage (0)
  DamageType "Quake1Rocket"
  States
  {
	Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_SetUserVar("user_angle",5)
	SpawnLoop:
		TNT1 A 0 A_CustomMissile("PPE_QGrenadeMultiFrag",0,0,72*user_angle,CMF_TRACKOWNER|CMF_AIMDIRECTION,40)
		TNT1 A 0 A_JumpIf(user_angle==0,"SpawnEnd")
		TNT1 A 0 A_SetUserVar("user_angle",user_angle-1)
		Goto SpawnLoop
	SpawnEnd:
		TNT1 A 0
		Stop
  }
}

actor PPE_QGrenadeMultiFrag : PPE_QGrenade
{
  SeeSound ""
  BounceSound "weapons/grenade/bounce2"
  DeathSound "weapons/grenade/explode"
  BounceFactor 0.6
  WallBounceFactor 0.6
  Gravity 0.4
  BounceCount 4
  Speed 8
  Damage (0)
  BounceType "None"
  Gravity 0.85
  DamageType "Quake1Rocket"
  States
  {
  Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_Explode(39,128,1,0,128)
		TNT1 A 0 A_Explode(78,128)
		TNT1 A 0 A_Quake(3,3,0,208,"")
		TNT1 A 0 A_Quake(2,4,0,480,"")
		TNT1 A 0 A_Quake(1,4,0,2048,"")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExploSmall")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
  }
}

actor PPE_Q1Prox : PPE_QGrenade
{
  var int user_countdown;
  var int user_x;
  var int user_y;
  var int user_z;
  SeeSound ""
  BounceSound ""
  DeathSound ""
  -USEBOUNCESTATE
  +HITTRACER
  BounceType None
  BounceFactor 0.0
  WallBounceFactor 0.0
  BounceCount 0
  Damage (5)
  States
  {
  Death:
	TNT1 A 0 A_NoGravity
	TNT1 A 0 A_PlaySound("weapons/proximity/impact",1)
	DGRD A 35
	TNT1 A 0 A_PlaySound("weapons/proximity/open",1)
	DGRD A 35
	TNT1 A 0 A_SpawnItem("PPE_Q1ProxStuck")
	stop
  Crash:
	TNT1 A 0 A_PlaySound("weapons/proximity/impact",1)
	Goto Stick
  XDeath:
	TNT1 A 0 A_PlaySound("weapons/proximity/hit",1)
  Stick:
    TNT1 A 0 A_SetUserVar("user_countdown",70)
	//TNT1 A 0 A_SetUserVar("user_x",Random(6,32))
	TNT1 A 0 A_SetUserVar("user_x",Random(-50,50))
	TNT1 A 0 A_SetUserVar("user_y",Random(-15,15))
	TNT1 A 0 A_SetUserVar("user_z",Random(25,90))
	Goto StickLoop
  StickLoop:
	TNT1 A 0 A_JumpIf(user_countdown==0,"StickEnd")
	DGRD A 1 A_Warp(AAPTR_TRACER,0,user_y,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,0,user_z*0.01,user_x*0.01)
	TNT1 A 0 A_SetUserVar("user_countdown",user_countdown-1)
	Goto StickLoop
  StickEnd:
	TNT1 A 0 A_PlaySound("weapons/proximity/warn",2)
	TNT1 A 0 A_SetUserVar("user_countdown",18)
  DetLoop:
	TNT1 A 0 A_JumpIf(user_countdown==0,"DetEnd")
	DGRD A 1 A_Warp(AAPTR_TRACER,user_x,user_y,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,0,user_z*0.01,user_x*0.01)
	TNT1 A 0 A_SetUserVar("user_countdown",user_countdown-1)
	Goto DetLoop
  DetEnd:
	TNT1 A 0 A_NoGravity
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
	TNT1 A 0 A_PlaySound("weapons/grenade/explode",1) // 28, 67
	TNT1 A 0 A_Explode(54,128,1,0,128)
	TNT1 A 0 A_Explode(131,128)
	TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
	TNT1 A 0 A_Quake(4,5,0,320,"")
	TNT1 A 0 A_Quake(2,6,0,720,"")
	TNT1 A 0 A_Quake(1,6,0,3256,"")
	BMXP A 4 bright
	BMXP BCDEF 3 bright
	Stop
  }
}

Actor PPE_Q1ProxStuck : PPE_Q1Prox
{
	+NOGRAVITY
	+DONTTHRUST
	-GRENADETRAIL
	-HITTRACER
	DeathSound "weapons/grenade/explode"
	DamageType "Quake1Rocket"
	Speed 0
	States
	{
	Spawn:
		DGRD A 45 A_SpawnItem("PPE_ProxBeep",0,6)
		loop
	Crash:
	XDeath:
	Death:
		TNT1 A 0 A_Jump(256,"StickEnd")
		stop
	}
}

actor PPE_GrenadeLauncher : RocketLauncher
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 2
	Weapon.AmmoType1 "PPE_Rockets"
	Weapon.AmmoType2 "PPE_Rockets"
	Weapon.SlotNumber 6
	Inventory.PickupSound "items/weapon1"
	Weapon.UpSound "weapons/switch"
	Inventory.Pickupmessage "$GOT_PPE_GL"
	Tag "$TAG_PPE_GL"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		QGLG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		QGLG A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		QGLG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_QGrenade",0,1,0,0,1,8)
		TNT1 A 0 A_PlaySound("weapons/grenade/fire1",1)    	
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QGLF ABCD 2 bright
		QGLG A 13
		TNT1 A 0 A_ReFire
    	Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_Q1Prox",0,1,0,0,1,8)
		TNT1 A 0 A_PlaySound("weapons/proximity/fire1",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QGLF ABCD 2 bright
		QGLG A 13
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.5)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.0)
		Goto LightDone
	Spawn:
		QGLW A -1
		Stop
   }
}

Actor PPE_ProxBeep : PPE_TrailBase
{
+FORCEXYBILLBOARD
RenderStyle Normal
Alpha 1.0
Vspeed 0
States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_PlaySound("weapons/proximity/tick",1)
    LAZP CBABC 1 Bright
	stop
  }
}