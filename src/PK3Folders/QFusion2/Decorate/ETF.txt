Actor PPE_ETF : Chaingun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_ETF"
	Tag "$TAG_PPE_ETF"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 4
	Weapon.AmmoType "PPE_Nails"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Decal "BulletChip"
	DamageType "Quake3"
	States
	{
	Drop:
	Spawn:
		3NGN W -1
		Stop
	Ready:
		3NGN A 1 A_WeaponReady
		Loop
	Deselect:
		3NGN A 1 A_Lower
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		3NGN A 1 A_Raise
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/etf/fire",1,1)
		TNT1 A 0 A_PlaySound("weapons/nailgun/shoot",3)
	    TNT1 A 0 A_FireCustomMissile("PPE_ETFlechette",0,1,-3,1,1,0)
		3NGN BC 1 bright
		3NGN A 1
		TNT1 A 0 A_Refire("Hold")
		Goto Ready
	Hold:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/etf/fire",1,1)
		TNT1 A 0 A_PlaySound("weapons/nailgun/shoot",3)
	    TNT1 A 0 A_FireCustomMissile("PPE_ETFlechette",0,1,3,1,1,0)
		3NGN DE 1 bright
		3NGN A 2
		TNT1 A 0 A_Refire("Fire")
		Goto Ready
	Flash:
		TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.4)
		Goto LightDone
	}
}

Actor PPE_ETFlechette : PPE_Nail1Base
{
	+PIERCEARMOR
	//Decal BulletChip
	DeathSound "weapons/etf/hit"
	Damage (14) // 14 - upscaled to match shells
	DamageType "Quake2Flech"
	Speed 35
}
