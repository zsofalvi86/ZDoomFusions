Actor PPE_SSGrenadeBase : Rocket
{
	+USEBOUNCESTATE
	-ROCKETTRAIL
	+GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
	SeeSound ""
	BounceSound "weapons/grenade/bounce2"
	DeathSound "weapons/qexp"
	BounceFactor 0.6
	WallBounceFactor 0.6
	Gravity 0.4
	BounceCount 4
	Speed 15
	Damage (98)
	States
	{
	Spawn:
		DGRD A 1
		Loop
	Bounce:
		DGRR ABCDEFGHIJKL 2
		Loop
	Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_Explode(130,192)
		TNT1 A 0 A_Quake(4,5,0,256,"")
		TNT1 A 0 A_Quake(2,6,0,720,"")
		TNT1 A 0 A_Quake(1,6,0,4096,"")
		MISL B 8 Bright
		MISL C 6 Bright
		MISL D 4 Bright
		Stop
	}
}

Actor PPE_SSGrenade1 : PPE_SSGrenadeBase
{
	Speed 15
}

Actor PPE_SSGrenade2 : PPE_SSGrenadeBase
{
	Damage (101)
	Speed 30
}

Actor PPE_SSGrenade3 : PPE_SSGrenadeBase
{
	Damage (104)
	Speed 45
}

actor PPE_UtilityLauncher : RocketLauncher
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 2
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 6
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Inventory.Pickupmessage "$GOT_PPE_UGL"
	Tag "$TAG_PPE_UGL"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		2GLG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		2GLG A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		2GLG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("weapons/grenade/fire2",1)
		TNT1 A 0 A_FireCustomMissile("PPE_QGrenadeMulti",0,1,0,0,1,12)
		2GLF ABCD 2
		TNT1 A 0 A_PlaySound("weapons/grenade/reload",6)
		2GLF DD 2 bright
		2GLG A 2
		2GLG AAAAA 3
		2GLG A 6
		2GLG A 7
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.5)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.0)
		Goto LightDone
	Spawn:
		2GLW A -1
		Stop
   }
}

Actor PPE_LauncherGrenade : PPE_SlowMissileBase
{
	+USEBOUNCESTATE
	-ROCKETTRAIL
	+GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
	SeeSound ""
	BounceSound "weapons/grenade/bounce2"
	DeathSound "weapons/grenade/explode"
	BounceFactor 0.7
	WallBounceFactor 0.7
	Gravity 0.4
	BounceCount 4
	Speed 22
	Damage (0)
	DamageType "Quake2Rocket"
	States
	{
	Spawn:
		DGRD A 1
		Loop
	Bounce:
		DGRR ABCDEFGHIJKL 2
		Loop
	Death:
		TNT1 A 0 A_NoGravity //40, 72 - upscaled to match rocket
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		TNT1 A 0 A_Explode(78,128,1,0,128)
		TNT1 A 0 A_Explode(139,128)
		TNT1 A 0 A_Quake(4,5,0,256,"")
		TNT1 A 0 A_Quake(2,6,0,720,"")
		TNT1 A 0 A_Quake(1,6,0,4096,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}