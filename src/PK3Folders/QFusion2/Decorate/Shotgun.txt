Actor PPE_Shotgun : Shotgun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_SG"
	Tag "$TAG_PPE_SG"
	Inventory.PickupSound "items/weapon1"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 1300
	Weapon.SlotNumber 2
	Weapon.AmmoUse 1
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoGive 16
	Weapon.SisterWeapon "PPE_Shotgun_P"
	Decal "BulletChip"
	DamageType "Quake1Shell"
	States
	{
	Ready:
    	QSGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
  	Deselect:
		QSGG A 1 A_Lower(18)
    	Loop
  	Select:
		TNT1 A 0 A_JumpIfInventory("PPE_ShotgunAlt",1,1)
		Goto SeLoop
		TNT1 A 0 A_GiveInventory("PPE_AltAmmo")
	SeLoop:
		QSGG A 1 A_Raise(18)
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash // 4
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireBullets(2.0,2.0,6,(6),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlayWeaponSound("weapons/lightshotgun/fire")
    	QSGF ABC 2 bright
		QSGG ABCBA 2
		QSGG A 2
		TNT1 A 0 A_ReFire
		Goto Ready
	Reload:
		TNT1 A 0 A_SetInventory("PPE_ShotgunAlt",1)
		TNT1 A 0 A_GiveInventory("PPE_AltAmmo")
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
  	Spawn:
    	QSGP A -1
    	Stop
	}
}

Actor PPE_Shotgun_P : PPE_Shotgun
{
	+WEAPON.POWERED_UP
	Weapon.AmmoType "PPE_HeavyShells"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.SisterWeapon "PPE_Shotgun"
	Decal "BulletChip"
	DamageType "Quake2Shell"
	States
	{
	Ready:
    	Q2SG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
  	Deselect:
		TNT1 A 0 A_TakeInventory("PowerAlt",1)
	DeseLoop:
		Q2SG A 1 A_Lower(18)
    	Loop
  	Select:
		TNT1 A 0 A_JumpIfInventory("PPE_ShotgunAlt",1,"SeLoop")
		TNT1 A 0 A_TakeInventory("PowerAlt",1)
	SeLoop:
		Q2SG A 1 A_Raise(18)
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash //4 - should one shot imps
		TNT1 A 0 A_FireBullets(4.0,4.0,12,(6),"PPE_BulletPuffQ2",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlaySound("weapons/shotgun/fire",1,1-(CountInv("PowerSilencer")*0.7))
    	Q2SF ABC 2 bright
		TNT1 A 0 A_PlaySound("weapons/shotgun/reload",4,1-(CountInv("PowerSilencer")*0.7))
		Q2SG DEFGHI 2
		Q2SG J 3
		Q2SG IHGFED 2
		Q2SG A 5
		TNT1 A 0 A_ReFire
		Goto Ready
	Reload:
		TNT1 A 0 A_SetInventory("PPE_ShotgunAlt",0)
		TNT1 A 0 A_TakeInventory("PowerAlt",1)
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.45)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.9)
		Goto LightDone
	}
}