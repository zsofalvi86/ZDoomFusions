actor PPE_RocketLauncher_Q3 : RocketLauncher
{
	Weapon.SelectionOrder 2400
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 5
	Weapon.AmmoType1 "PPE_Rockets"
	Weapon.SlotNumber 7
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Inventory.Pickupmessage "$GOT_PPE_QRL"
	Tag "$TAG_PPE_QRL"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		Q3RG A 1 A_WeaponReady
		Loop
	Deselect:
		Q3RG A 1 A_Lower
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		Q3RG A 1 A_Raise
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_Q3Rocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/rocket/fire3",1)
		Q3RF ABCDE 2 bright
		Q3RG A 18
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	Spawn:
		Q3RP A -1
		Stop
   }
}

actor PPE_Q3Rocket : PPE_SlowMissileBase
{
	Speed 30
	ProjectileKickBack 100
	Damage (0)
	DamageType "Quake3"
	Decal Scorch
	SeeSound ""
	DeathSound "weapons/rocket/explode3"
	Obituary "$OB_MPROCKET"
	-ROCKETTRAIL
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/rocket/fly",6,1,1)
	Loopy:
		MISL A 1 bright A_SpawnItem("PPE_Q3RocketTrail",0,6)
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(200,128,1,0,32)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

Actor PPE_Q3RocketTrail : PPE_TrailBase
{
	RenderStyle "Translucent"
	Alpha 0.6
	States
	{
  	Spawn:
		TNT1 A 2
		PUFF B 2
    	PUFF CD 6
    	Stop
	}
}