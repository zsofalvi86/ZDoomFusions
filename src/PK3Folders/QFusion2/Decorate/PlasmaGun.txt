actor PPE_PlasmaGun : PlasmaRifle
{
	Weapon.SelectionOrder 400
	Weapon.AmmoGive 40
	Weapon.AmmoType "PPE_Cells"
	Weapon.AmmoUse 1
	Weapon.SlotNumber 8
	Weapon.UpSound "weapons/switch"
	Inventory.PickupSound "items/weapon2"
	Inventory.Pickupmessage "$GOT_PPE_PG"
	Tag "$TAG_PPE_PG"
	Weapon.BobRangeX 0.05
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 2.0
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		Q3PG IIIIJJKKKJJ 1 A_WeaponReady
		Loop
	Deselect:
		Q3PG I 1 A_Lower(18)
		Loop
	Select:
		Q3PG I 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_Q3Plasma",0,1,0,1,1,0)
		//TNT1 A 0 A_FireBullets(0,0,1,(0),"PPE_Q3PlasmaSpawnPuff",FBF_NORANDOMPUFFZ,256)
		TNT1 A 0 A_PlaySound("weapons/plasma/fire1",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		Q3PG ABC 1 bright
		TNT1 A 0 A_ReFire("Hold")
		Q3PG D 1 bright
    	Goto Ready
	Hold:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_Q3Plasma",0,1,0,1,1,0)
		//TNT1 A 0 A_FireBullets(0,0,1,(0),"PPE_Q3PlasmaSpawnPuff",FBF_NORANDOMPUFFZ,256)
		TNT1 A 0 A_PlaySound("weapons/plasma/fire1",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		Q3PG EFGH 1 bright
		TNT1 A 0 A_ReFire("Fire")
    	Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 A 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.2)
		Goto LightDone
	Spawn:
		Q3PG W -1
		Stop
   }
}

Actor PPE_Q3PlasmaPuff : BulletPuff
{
	+PUFFONACTORS
	+FORCERADIUSDMG
	-ALLOWPARTICLES
	RenderStyle Normal
	Vspeed 0
	DamageType "Quake3"
	Decal QPlasmaScorch
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/hit",3)
		TNT1 A 0 A_Explode(25,64,1,0,16)
		PLCR ABCD 3 bright
		Stop
  }
}

Actor PPE_Q3PlasmaSpawnPuff : BulletPuff
{
	+ALWAYSPUFF
	+PUFFONACTORS
	+PUFFGETSOWNER
	-ALLOWPARTICLES
	+SEEINVISIBLE
	RenderStyle Normal
	Vspeed 0
	DamageType "Quake3"
	Decal QPlasmaScorch
	States
	{
	Crash:
		TNT1 A 0
		TNT1 A 0 A_CheckBlock("Death")
		TNT1 A 0 A_SpawnProjectile("PPE_Q3Plasma",0,0,0,CMF_TRACKOWNER)//,180,CMF_TRACKOWNER|CMF_AIMOFFSET,-pitch)
		Stop
	Death:
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/hit",3)
		TNT1 A 0 A_Explode(25,64,1,0,16)
		PLCR ABCD 3 bright
		Stop
  }
}

actor PPE_Q3Plasma : PPE_SlowMissileBase
{
	Speed 65
	ProjectileKickBack 120
	Damage (5)
	DamageType "Quake3"
	Decal QPlasmaScorch
	SeeSound ""
	DeathSound "weapons/plasma/hit"
	Obituary "$OB_MPROCKET"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/fly1",6,1,1)
		TNT1 A 0 A_Jump(256,1,2,3,4,5)
	Loopy:
		PLSB ABCDEF 1 bright
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(20,64,1,0,32)
		PLCR ABCD 3 bright
		Stop
	}
}
