actor PPE_PlasmaGun : PlasmaRifle
{
	Weapon.SelectionOrder 400
	Weapon.AmmoGive 40
	Weapon.AmmoType1 "PPE_Cells"
	Weapon.AmmoType2 "PPE_Cells"
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 15
	Weapon.SlotNumber 8
	Weapon.UpSound "weapons/switch"
	Inventory.PickupSound "items/weapon2"
	Inventory.Pickupmessage "$GOT_PPE_PG"
	Tag "$TAG_PPE_PG"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		Q3PG IIIIJJKKKJJ 1 A_WeaponReady
		Loop
	Deselect:
		Q3PG I 1 A_Lower
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		Q3PG I 1 A_Raise
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireBullets(0,0,1,(15),"PPE_Q3PlasmaSpawnPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_NORANDOMPUFFZ,256)
		//TNT1 A 0 A_FireBullets(0,0,1,(0),"PPE_Q3PlasmaSpawnPuff",FBF_NORANDOMPUFFZ,256)
		TNT1 A 0 A_PlaySound("weapons/plasma/fire1",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		Q3PG ABC 1 bright
		TNT1 A 0 A_ReFire("Hold")
		Q3PG D 1 bright
    	Goto Ready
	Hold:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireBullets(0,0,1,(15),"PPE_Q3PlasmaSpawnPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_NORANDOMPUFFZ,256)
		//TNT1 A 0 A_FireBullets(0,0,1,(0),"PPE_Q3PlasmaSpawnPuff",FBF_NORANDOMPUFFZ,256)
		TNT1 A 0 A_PlaySound("weapons/plasma/fire1",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		Q3PG EFGH 1 bright
		TNT1 A 0 A_ReFire("Fire")
    	Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/plasma/fire2")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		Q3PG ABCBA 2 bright
		TNT1 A 0 A_PlaySound("items/quad/use",3,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireCustomMissile("PPE_LightningBall",0,1,0,4,1,0)
		TNT1 A 0 A_GunFlash
		Q3PG DEFG 2 bright
		Q3PG IIIJJKK 1
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 A 1 A_SetPitch(pitch-0.25)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.25)
		Goto LightDone
	AltFlash:
		TNT1 A 1
		TNT1 AAAA 1 A_SetPitch(pitch-0.9)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+1.8)
		Goto LightDone
	Spawn:
		Q3PG W -1
		Stop
   }
}

Actor PPE_Q3PlasmaPuff : BulletPuff
{
	+PUFFONACTORS
	+FORCERADIUSDMG
	-ALLOWPARTICLES
	RenderStyle Normal
	Vspeed 0
	DamageType "Quake3"
	Decal PlasmaScorch
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/hit",3)
		TNT1 A 0 A_Explode(25,64,1,0,16)
		PLCR ABCD 3 bright
		Stop
  }
}

Actor PPE_Q3PlasmaSpawnPuff : BulletPuff
{
	+ALWAYSPUFF
	+PUFFONACTORS
	+PUFFGETSOWNER
	-ALLOWPARTICLES
	+SEEINVISIBLE
	RenderStyle Normal
	Vspeed 0
	DamageType "Quake3"
	Decal PlasmaScorch
	States
	{
	Crash:
		TNT1 A 0
		TNT1 A 0 A_CheckBlock("Death")
		TNT1 A 0 A_SpawnProjectile("PPE_Q3Plasma",0,0,0,CMF_TRACKOWNER)//,180,CMF_TRACKOWNER|CMF_AIMOFFSET,-pitch)
		Stop
	Death:
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/hit",3)
		TNT1 A 0 A_Explode(25,64,1,0,16)
		PLCR ABCD 3 bright
		Stop
  }
}

actor PPE_Q3Plasma : PPE_SlowMissileBase
{
	Speed -65
	ProjectileKickBack 120
	Damage (15)
	DamageType "Quake3"
	Decal PlasmaScorch
	SeeSound ""
	DeathSound "weapons/plasma/hit"
	Obituary "$OB_MPROCKET"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/fly1",6,1,1)
		TNT1 A 0 A_Jump(256,1,2,3,4,5)
	Loopy:
		PLSB ABCDEF 1 bright
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(25,64,1,0,16)
		PLCR ABCD 3 bright
		Stop
	}
}

Actor PPE_LightningBall : BFGBall
{
	var int user_radius;
	var int user_count;
	Speed 25
	Damage (50)
	RenderStyle Normal
	DeathSound "weapons/plasma/explode"
	+FORCERADIUSDMG
	DamageType "Quake1Cell"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/fly2",1,0.9,1)
		LBAL ABCD 3 Bright
		Loop
	Death:
		TNT1 A 0 // 40, 80
		TNT1 A 0 A_Quake(7,6,0,320,"")
		TNT1 A 0 A_Quake(4,8,0,640,"")
		TNT1 A 0 A_Quake(2,9,0,4096,"")
		TNT1 A 0 A_SpawnItem("PPE_LightBlueQuakeExplo")
		TNT1 A 0 A_RadiusGive("PPE_LGExpItem",192,RGF_MONSTERS|RGF_PLAYERS|RGF_NOTARGET)
		PLSE ABCDE 2 Bright A_Explode(45,192,0,1,192)
		Stop
  }
}

ACTOR PPE_LGExpItem : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	Pickup:
		TNT1 A 0 A_CheckProximity("Spawn","PPE_LightningBall",192,1,CPXF_CHECKSIGHT|CPXF_SETMASTER|CPXF_CLOSEST|CPXF_ANCESTOR)
		Stop
	Spawn:
		TNT1 A 0 A_SpawnItemEx("PPE_LGExpTarget",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_SETTRACER)
		Stop
	}
}

ACTOR PPE_LGExpTarget : PPE_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Warp(AAPTR_MASTER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION)
		TNT1 A 0 A_SpawnItemEx("PPE_LGExpShooter",0,0,0,0,0,0,0,SXF_SETTARGET)
		TNT1 AAAAAAAAAAAA 1 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,0,0.5)
		Stop
	}
}

ACTOR PPE_LGExpShooter : PPE_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/lightning/loop",5)
		TNT1 AAAAA 2 A_CustomRailgun((0),0,"none","none",RGF_SILENT|RGF_EXPLICITANGLE,1,0,"BulletPuff",0,0,192,0,5,0,"PPE_PlasmaLightningTrail",0)
		Stop
	}
}

Actor PPE_PlasmaLightningTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"A","B","C","D","E","F")
		A:
    	LSHF AB 1 bright
		Stop
		B:
    	LSHF BC 1 bright
    	Stop
		C:
    	LSHF CD 1 bright
    	Stop
		D:
    	LSHF DE 1 bright
    	Stop
		E:
    	LSHF EF 1 bright
    	Stop
		F:
    	LSHF FG 1 bright
		Stop
	}
}
