Actor PPE_Phalanx_P : PlasmaRifle
{
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	+WEAPON.POWERED_UP
	Inventory.Pickupmessage "$GOT_PPE_PHX"
	Tag "$TAG_PPE_PHX"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 2000
	Weapon.SlotNumber 9
	//Weapon.AmmoUse 1
	//Weapon.AmmoType "PPE_MagSlugs"
	Weapon.AmmoUse 1
	Weapon.AmmoType "PPE_MagSlugs"
	Weapon.AmmoGive 2
	Weapon.KickBack 200
	States
	{
	Ready:
		PHAL AABBCCDDDCCBBAAAAA 1 A_WeaponReady
		Loop
  	Deselect:
		PHAL A 1 A_Lower
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
    	PHAL A 1 A_Raise
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("weapons/phalanx/fire",1,1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireCustomMissile("PPE_PhalanxMissile",0,1,-4,4,1,0)
		PHAF ABC 2 bright
		TNT1 A 0 A_FireCustomMissile("PPE_PhalanxMissile",0,0,4,4,1,0)
		PHAF DEF 2 bright
		PHAL BDCBABCDDCBAA 1
		PHAL EFGHHGFE 2
		PHAL ABCDDCBA 1
		PHAL ABCDDCBA 1
    	TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
    	TNT1 A 0 A_Light1
		TNT1 A 1
		TNT1 AA 1 A_SetPitch(pitch-1.25)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+1.25)
		TNT1 A 1
		TNT1 A 0 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-1.05)
		TNT1 AAA 1 A_SetPitch(pitch+1.4)
		Goto LightDone
  	Spawn:
		PHAW AAAAAABBCCDDCCBB 1
		Loop
	}
}

Actor PPE_PhalanxMissile : PPE_SlowMissileBase
{
	+BLOODSPLATTER
	+FORCERADIUSDMG
	SeeSound ""
	DeathSound "weapons/q2exp"
	Obituary "$OB_MPPHALANX"
	Speed 35
	DamageType "Quake2Mag"
	Damage (110) // 55 - upscaled so it exceeds cell
	DamageType QuakeExplosive
	ProjectileKickBack 125
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/phalanx/fly",6,1.0,1)
	Loopy:
		PHML AABBCCDDEEFF 1 bright
		{
			A_SpawnParticle(orange,SPF_FULLBRIGHT|SPF_RELATIVE,20,7,180,7,FRandom(-1.8,1.8),FRandom(-1.8,1.8),FRandom(0,1.2),FRandom(-0.25,0.25),FRandom(-0.25,0.25));
			A_SpawnParticle(orange,SPF_FULLBRIGHT|SPF_RELATIVE,20,7,180,7,FRandom(-1.8,1.8),FRandom(-1.8,1.8),FRandom(0,1.2),FRandom(-0.25,0.25),FRandom(-0.25,0.25));
		}
		Loop
	Death:
		TNT1 A 0 A_Explode(40,128,1,0,48) // 20
		TNT1 A 0 A_Quake(4,4,0,208,"")
		TNT1 A 0 A_Quake(2,5,0,480,"")
		TNT1 A 0 A_Quake(1,5,0,3008,"")
		TNT1 A 0 A_SpawnItem("PPE_OrangeQuakeExplo")
		PMXP A 4 bright
		PMXP BCDEF 3 bright
		Stop
	}
}