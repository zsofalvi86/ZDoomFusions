Actor PPE_MachineGun : Chaingun
{
	Inventory.Pickupmessage "$GOT_PPE_MG"
	Tag "$TAG_PPE_MG"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 2
	Weapon.AmmoType "PPE_Bullets"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.BobRangeX 0.05
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 2.0
	Decal "BulletChip"
	States
	{
	Spawn:
		MACW A -1
		Stop
	Ready:
		MACC A 1 A_WeaponReady
		Loop
	Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_MachineGunZoom",1,"ReadyZoom")
		MACC A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	ReadyZoom:
		TNT1 A 0 A_StopSound(6)
	ReadyZoomLoop:
		TNT1 A 1 bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_MachineGunZoom",0)
	DeseLoop:
		MACC A 1 A_Lower(18)
		Loop
	Select:
		MACC A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_SetInventory("PPE_PitchToReset",90+pitch)
	Fire0:
		TNT1 A 0 A_JumpIfinventory("PPE_MachineGunZoom",1,"FireZoom")
		TNT1 A 0 A_CheckForReload(2,"Fire2")
		Goto Fire1
	Fire2:
		MACF D 1 bright
	Fire1:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/machinegun/fire")
	    TNT1 A 0 A_FireBullets(2.1,2.1,-1,(10),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_MGBulletCasingShooter",0,0,0,4)
		MACF ABC 1 bright A_SetPitch(pitch-0.35)
		TNT1 A 0 A_Refire("Fire0")
		Goto Spindown
	Spindown:
		MACF E 1 bright A_WeaponReady(WRF_NOSWITCH)
		TNT1 A 0 A_SetPitch(CountInv(PPE_PitchToReset)-90)
		MACF FF 1 bright A_WeaponReady(WRF_NOSWITCH)
		MACF GGG 1 bright A_WeaponReady(WRF_NOSWITCH)
		Goto Ready
	FireZoom: //tap fire
		TNT1 A 0 A_GunFlash("ZoomFlash")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/machinegun/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_MGBulletCasingShooter",0,0,0,4)
	    TNT1 A 0 A_FireBullets(0,0,1,(15),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 AAAA 1
		TNT1 AAA 1 A_Refire("FireZoomAuto")
		Goto SpindownZoom
	FireZoomAuto:
		TNT1 A 0 A_GunFlash("ZoomFlash")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/machinegun/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_MGBulletCasingShooter",0,0,0,4)
		TNT1 A 0 A_FireBullets(1.2,1.2,-1,(10),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 AAA 1
		TNT1 A 0 A_CheckForReload(2,"FireZoom2")
		TNT1 A 1
	FireZoom2:
		TNT1 A 0 A_Refire("FireZoomAuto")
		Goto SpindownZoom
	SpindownZoom:
		TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
		TNT1 AA 1 A_WeaponReady(WRF_NOSWITCH)
		TNT1 AAAA 1 A_WeaponReady(WRF_NOSWITCH)
		Goto Ready
	Zoom:
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_MachineGunZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_MachineGunZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyZoom
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyZoom
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyZoom
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyZoom
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_MachineGunZoom",0)
		SSSS Z 5 bright
		MACC A 5
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		Goto LightDone
	ZoomFlash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_SetPitch(pitch-0.35)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.35)
		Goto LightDone
	}
}

Actor PPE_MGBulletCasingShooter : PPE_CasingShooterBase
{
	Speed 8
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BulletCasing",0,0,FRandom(-60,-75),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-13,-22))
		Stop
	}
}

Actor PPE_MachineGunZoom : Inventory
{
	Inventory.MaxAmount 1
}

Actor PPE_PitchToReset : Inventory
{
	Inventory.MaxAmount 180
}