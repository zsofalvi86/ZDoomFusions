Actor PPE_RailGun_Q2 : Chaingun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_RG"
	Tag "$TAG_PPE_RG"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 7
	Weapon.AmmoType "PPE_Slugs"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Decal "PlasmaScorch"
	DamageType "Quake2Slug"
	States
	{
	Drop:
	Spawn:
		2RGW A -1
		Stop
	Ready:
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"ReadyScope")
		2RGI AABBCCDD 1 A_WeaponReady
		Loop
	ReadyScope:
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",0)
	DeseLoop:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		2RGG A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		2RGG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("weapons/railgun/fire1",1) // 150
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_RailAttack((280),0,1,6666FF,888888,RGF_FULLBRIGHT|RGF_SILENT,0,"PPE_Q2RailPuff")
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"FireScope")
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
	FireNormal:
		2RGC ABCDE 2 bright
		2RGG A 29
		TNT1 A 0 A_PlaySound("weapons/railgun/reload",2)
		2RGG A 7
		2RGG DCB 2
		//2RGI AABBCCDD 1
		//2RGI AABBCCDD 1
		TNT1 A 0 A_Refire
		Goto Ready
	FireScope:
	    //TNT1 A 0 A_FireBullets(0,0,1,(390),"DSS_SniperPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		SSSS ZZZ 3 bright
		SSSS Z 10 bright
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",0)
		SSSS Z 5 bright
		SSSS A 10
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor DSS_SniperZoom : Inventory
{
	Inventory.MaxAmount 1
}

Actor PPE_Q2RailPuff : BulletPuff
{
	-ALLOWPARTICLES
	+ALWAYSPUFF
	+PUFFONACTORS
	+HITTARGET
	+PAINLESS
	RenderStyle None
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		//TNT1 A 0 A_TransferPointer(AAPTR_TRACER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_MASTER)
		TNT1 A 0 A_PlaySound("weapons/railgun/hit")
		TNT1 A 0 A_JumpIfInTargetLos(1,180)
		Goto UnawareDamage
		TNT1 A 0 // landing state
		//TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_MASTER,AAPTR_NULL) == True, "UnawareDamage")
		Stop
	UnawareDamage:
		TNT1 A 0 A_DamageTarget(280)
		Stop
	}
}