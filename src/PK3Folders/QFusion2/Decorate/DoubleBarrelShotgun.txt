Actor PPE_DoubleBarrelShotgun : SuperShotgun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_DSG"
	Tag "$TAG_PPE_DSG"
	Inventory.PickupSound "items/weapon1"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 3
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 2
	Weapon.AmmoType1 "PPE_Shells"
	Weapon.AmmoType2 "PPE_Shells"
	Weapon.AmmoGive 16
	Decal "BulletChip"
	DamageType "Quake1Shell"
	States
	{
	Ready:
    	QDGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
  	Deselect:
		TNT1 A 0 A_SetInventory("PPE_DBSGBurst",0)
	DeseLoop:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		QDGG A 1 A_Lower
    	Loop
  	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		QDGG A 1 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters // 4
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireBullets(7.5,4.0,14,(6),"PPE_RicBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlayWeaponSound("weapons/dbshotgun/fire")
		QDGF ABC 2 bright
		QDGG BCD 2
		QDGG EFGH 1
		QDGG IJKL 2
		QDGG A 1
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.2)
		Goto LightDone
  	Spawn:
    	QDGP A -1
    	Stop
	}
}