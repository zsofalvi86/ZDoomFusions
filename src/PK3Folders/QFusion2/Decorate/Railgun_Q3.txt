Actor PPE_RailGun_Q3 : Chaingun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_DSS_TG"
	Tag "$TAG_DSS_TG"
	Inventory.PickupSound "items/weapon2"
	Weapon.UpSound "weapons/switch"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 7
	Weapon.AmmoType "PPE_Slugs"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Decal "BulletChip"
	States
	{
	Drop:
	Spawn:
		3RGW A -1
		Stop
	Ready:
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"ReadyScope")
		3RGI AABBCCDD 1 A_WeaponReady
		Loop
	ReadyScope:
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",0)
	DeseLoop:
		3RGA A 1 A_Lower
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		3RGA A 1 A_Raise
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/railgun/fire2")
		TNT1 A 0 A_RailAttack((200),0,1,"none","none",RGF_SILENT,0,"PPE_Q3RailPuff",0,0,3500,0,96,0,"PPE_Q3RailRing",0)
		TNT1 A 0 A_RailAttack((0),0,0,"none","none",RGF_SILENT,0,"PPE_InvisiPuff",0,0,3500,0,8,0,"PPE_Q3RailTrail",0)
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"FireScope")
	FireNormal:
		3RGC ABCDE 2 bright
		3RGG A 20
		3RGG DCB 2
		3RGI AABBCCDD 1
		3RGI AABBCCDD 1
		TNT1 A 0 A_Refire
		Goto Ready
	FireScope:
	    //TNT1 A 0 A_FireBullets(0,0,1,(390),"DSS_SniperPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		SSSS ZZZ 3 bright
		SSSS Z 10 bright
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",0)
		SSSS Z 5 bright
		SSSS A 10
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		//TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		//TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor PPE_Q3RailTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(64,"Despawn")
    	Q3RT AABBCCCBBBBAAAAA 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_Q3RailRing : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(64,"Despawn")
    	Q3RR AABBBCCCDDDEEE 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_Q3RailPuff : BulletPuff
{
	+ALWAYSPUFF
	RenderStyle Normal
	VSpeed 0
	States
	{
	Crash:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/railgun/hit")
		PLCR ABCD 4 bright
		Stop
	Spawn:
	Melee:
		TNT1 A 0
		Stop
  }
}

Actor PPE_InvisiPuff : BulletPuff
{
	-ALLOWPARTICLES
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		Stop
	}
}

Actor DSS_SniperZoom : Inventory
{
	Inventory.MaxAmount 1
}