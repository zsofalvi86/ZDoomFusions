Actor PPE_Thunderbolt : Plasmarifle
{
	Inventory.Pickupmessage "$GOT_PPE_TB"
	Tag "$TAG_PPE_TB"
	Decal PlasmaScorch
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 20
	Weapon.AmmoType1 "PPE_Cells"
	Weapon.AmmoType2 "PPE_Cells"
	Weapon.AmmoGive 40
	Weapon.SlotNumber 8
	Weapon.KickBack 0
	Inventory.PickupSound "items/weapon1"
	Weapon.UpSound "weapons/switch"
	DamageType "Quake1Cell"
	States
	{
	Ready:
		TBLG AAAAAAAAAAABBCCDDCCBBAAAAAAA 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
		TNT1 A 0 A_Lower
		TNT1 A 0 A_Lower
		TBLG A 1 A_Lower
    	Loop
  	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_Raise
		TBLG A 1 A_Raise
    	Loop
	Fire:
		TNT1 A 0 A_PlaySound("weapons/lightning/start",5)
	Anim1:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters // 30
		TNT1 A 0 A_RailAttack((45),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffActual",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_Jump(256,"AnimA1","AnimB1","AnimC1")
	Anim2:
		TNT1 A 0 A_PlaySound("weapons/lightning/loop",6,1.0,1)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_RailAttack((45),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffActual",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_PlaySound("items/quad/use",3,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_Jump(256,"AnimA2","AnimB2","AnimC2")
	AnimA1:
		TBLF ABC 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffFake",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimB1:
		TBLF BCA 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffFake",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimC1:
		TBLF CAB 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffFake",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimA2:
		TBLF AABB 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffFake",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	AnimB2:
		TBLF BBCC 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffFake",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	AnimC2:
		TBLF CCAA 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffFake",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	FireEnd:
		TNT1 A 0 A_StopSound(6)
		Goto Ready
	Flash:
		TNT1 A 0
		//TNT1 A 0 A_SetPitch(pitch-0.5)
		TNT1 A 2 A_Light(Random(1,3))
		//TNT1 A 0 A_SetPitch(pitch+0.5)
		TNT1 A 2 A_Light(Random(1,3))
		Goto LightDone
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("weapons/lightning/tesla",4)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
    	TBLF ABCBA 2 bright
		TNT1 A 0 A_GunFlash
		//TNT1 A 0 A_FireBullets(0,0,1,(120),"PPE_HammerPuff",FBF_NORANDOM,72)
		//TNT1 A 0 A_JumpIfNoAmmo("SkipLightning")
		TNT1 A 0 A_PlaySound("weapons/lightning/mjolnir",5)
		TNT1 A 0 A_PlaySound("items/quad/use",3,CountInv("PowerQuadDamage"))
		TBLF B 5 bright A_GunFlash("Lightning1")
		TBLF ABC 5 bright A_GunFlash("Lightning2")
		TBLG A 10
		TNT1 A 0 A_ReFire
		Goto Ready
  	AltFlash:
    	TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	Lightning1:
		TNT1 A 0 A_RailAttack((60),0,1,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffActual",-6,0,640,0,12,0,"PPE_MjolnirTrail",1)
		TNT1 A 0 A_RailAttack((60),0,0,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffActual",6,0,640,0,12,0,"PPE_MjolnirTrail",1)
	Lightning2:
		TNT1 AAAA 0 A_RailAttack((45),0,0,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"PPE_LightningPuffActual",(FRandom(-24,24)),0,640,0,12,0,"PPE_MjolnirTrail",1)
		Goto LightDone
	Spawn:
    	TBLW AAAAAABBCCDDCCBB 1
		Loop
	}
}

Actor PPE_LightningTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(36,"Despawn")
		TNT1 A 0 A_SpawnItemEx("PPE_LightningTrailParticle",0,FRandom(-4,4),FRandom(-4,4))
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_LightningTrailParticle : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"A","B","C","D","E","F")
		A:
    	LSHF A 1 bright
		Stop
		B:
    	LSHF B 1 bright
    	Stop
		C:
    	LSHF C 1 bright
    	Stop
		D:
    	LSHF D 1 bright
    	Stop
		E:
    	LSHF E 1 bright
    	Stop
		F:
    	LSHF F 1 bright
		Stop
	}
}

Actor PPE_MjolnirTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(32,"Despawn")
		TNT1 A 0 A_Jump(256,"A","B","C","D","E","F")
		A:
    	LSHF ABCD 1 bright
		Stop
		B:
    	LSHF BCDE 1 bright
    	Stop
		C:
    	LSHF CDEF 1 bright
    	Stop
		D:
    	LSHF DEFA 1 bright
    	Stop
		E:
    	LSHF EFAB 1 bright
    	Stop
		F:
    	LSHF FABC 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_LightningPuffActual : BulletPuff
{
	-ALLOWPARTICLES
	+PUFFONACTORS
	+ALWAYSPUFF
	+BLOODLESSIMPACT
	RenderStyle Normal
	Vspeed 0
	States
	{
	Crash:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/lightning/hit")
		Goto Anim
	Spawn:
	Melee:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/lightning/impact")
	Anim:
		PLSE BCD 2 bright
		Stop
	}
}

Actor PPE_LightningPuffFake : BulletPuff
{
	+PAINLESS
	-ALLOWPARTICLES
	+BLOODLESSIMPACT
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		Stop
	}
}
