Actor QD_Mjolnir : Fist
{
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	Inventory.Pickupmessage "$GOT_QD_MJ"
	Inventory.Icon "HMMPB0"
	Tag "$TAG_QD_MJ"
	Inventory.PickupSound "items/qweapon"
	Weapon.SlotNumber 1
	Weapon.BobRangeX 0.05
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 2.0
	Weapon.AmmoUse 15
	Weapon.AmmoType "QD_Cells"
	Weapon.AmmoGive 45
	DamageType "Quake1Cell"
	States
	{
	Ready:
    	QMJL A 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_Lower
    	Loop
  	Select:
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/axeswing")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QMJL C 1 Offset(2,24)
		QMJL C 1 Offset(4,28)
    	QMJL B 1 Offset(0,30)
		QMJL B 1 Offset(0,32)
		QMJL C 1 Offset(0,30)
		QMJL C 1 Offset(0,32)
		QMJL D 1 Offset(0,32)
		QMJL D 1 Offset(0,30)
		QMJL E 1 Offset(2,38)
		QMJL E 1 Offset(0,35)
		TNT1 A 0 A_FireBullets(0,0,1,(67),"QD_HammerPuff",FBF_NORANDOM,72) //50
		TNT1 A 0 A_JumpIfNoAmmo("SkipLightning")
		TNT1 A 0 A_PlaySound("weapons/mjollight",5)
		QMJL E 1 Offset(0,34) A_GunFlash("Lightning1")
		QMJL E 1 Offset(0,33)
		QMJL E 1 Offset(0,32)
		QMJL E 1 Offset(0,31)
		QMJL E 1 Offset(-1,30)
		QMJL E 1 Offset(-1,29) A_GunFlash("Lightning2")
		QMJL E 1 Offset(-1,28)
		QMJL E 1 Offset(-2,26)
		QMJL E 1 Offset(-2,23)
		QMJL E 1 Offset(-2,20)
		QMJL A 1 Offset(6,85) A_GunFlash("Lightning2")
		QMJL A 1 Offset(5,75)
		QMJL A 1 Offset(5,68)
		QMJL A 1 Offset(4,60)
		QMJL A 1 Offset(4,52)
		QMJL A 1 Offset(3,45) A_GunFlash("Lightning2")
		QMJL A 1 Offset(3,40)
		QMJL A 1 Offset(2,37)
		QMJL A 1 Offset(2,35)
		QMJL A 1 Offset(1,33)
		Goto AnimEnd
	SkipLightning:
		QMJL E 1 Offset(0,34)
		QMJL E 1 Offset(0,33)
		QMJL E 1 Offset(0,32)
		QMJL E 1 Offset(0,31)
		QMJL E 1 Offset(-1,30)
		QMJL E 1 Offset(-1,29)
		QMJL E 1 Offset(-1,28)
		QMJL E 1 Offset(-2,26)
		QMJL E 1 Offset(-2,23)
		QMJL E 1 Offset(-2,20)
		QMJL A 1 Offset(6,85)
		QMJL A 1 Offset(5,75)
		QMJL A 1 Offset(5,68)
		QMJL A 1 Offset(4,60)
		QMJL A 1 Offset(4,52)
		QMJL A 1 Offset(3,45)
		QMJL A 1 Offset(3,40)
		QMJL A 1 Offset(2,37)
		QMJL A 1 Offset(2,35)
		QMJL A 1 Offset(1,33)
	AnimEnd:
		QMJL A 0 Offset(0,32)
		QMJL A 10
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	Lightning1: // 40
		TNT1 A 0 A_RailAttack((53),0,1,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"none",-6,0,640,0,4,0,"QD_MjolnirTrail",-11)
		TNT1 A 0 A_RailAttack((53),0,0,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"none",6,0,640,0,4,0,"QD_MjolnirTrail",-11)
	Lightning2: // 30
		TNT1 AAAA 0 A_RailAttack((40),0,0,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"none",(FRandom(-32,32)),0,640,0,4,0,"QD_MjolnirTrail",-11)
		Goto LightDone
  	Spawn:
    	HMMP A -1
    	Stop
	}
}

Actor QD_HammerPuff : BulletPuff
{
	+PUFFONACTORS
	+ALWAYSPUFF
	SeeSound "weapons/mjolhit"
	ActiveSound "weapons/mjolhit"
	AttackSound "weapons/mjolhit"
	States
	{
	Spawn:
	Crash:
	Melee:
		TNT1 A 0
		PUFF AB 2 Bright
		PUFF CD 2
		Stop
	XDeath:
		TNT1 A 10
		Stop
	}
}

Actor QD_MjolnirTrail : QD_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("QD_MjolnirTrailParticle",0,FRandom(-4,4),FRandom(-4,4))
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor QD_MjolnirTrailParticle : QD_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"A","B","C","D","E","F")
		A:
    	LSHF ABCD 1 bright
		Stop
		B:
    	LSHF BCDE 1 bright
    	Stop
		C:
    	LSHF CDEF 1 bright
    	Stop
		D:
    	LSHF DEFA 1 bright
    	Stop
		E:
    	LSHF EFAB 1 bright
    	Stop
		F:
    	LSHF FABC 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}