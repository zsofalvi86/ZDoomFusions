actor QD_QRocket : Rocket
{
	Speed 35
	Damage (96)
	SeeSound ""
	DeathSound "weapons/qexp"
	Obituary "$OB_MPROCKET"
	DamageType "Quake1Sp"
	States
	{
	Death: // 40, 80
		TNT1 A 0 A_Explode(40,128,1,0,128,0,0,"None","Normal")
		TNT1 A 0 A_Explode(80,128,1,0,0,0,0,"None","Normal")
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		TNT1 A 0 A_SpawnItem("QD_RedQuakeExplo")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor QD_QMiniRocket : QD_QRocket
{
	+FORCERADIUSDMG
	Speed 30
	Damage (48)
	DeathSound "weapons/qexp"
	Obituary "$OB_MPROCKET"
	DamageType "Quake1Rocket"
	States
	{
	Death:
		TNT1 A 0 A_Explode(20,128,1,0,128,0,0,"None","Normal")
		TNT1 A 0 A_Explode(40,128,1,0,0,0,0,"None","Normal")
		TNT1 A 0 A_Quake(3,4,0,224,"")
		TNT1 A 0 A_Quake(2,5,0,512,"")
		TNT1 A 0 A_Quake(1,5,0,3072,"")
		TNT1 A 0 A_SpawnItem("QD_RedQuakeExploSmall")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor QD_QMiniRocketTarget : QD_QMiniRocket
{
	+SEEKERMISSILE
	+SCREENSEEKER
	DamageType "Quake1Rocket"
	States
	{
	Spawn:
		MISL AAAA 1 bright
		TNT1 A 0 A_Tracer2
		MISL A 1 A_SeekerMissile(10,25,SMF_LOOK|SMF_CURSPEED,256,4)
		Goto Super::Spawn
	}
}

actor QD_RocketLauncher : RocketLauncher
{
	Weapon.SelectionOrder 2500
	Weapon.BobRangeX 0.1
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 2.0
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "QD_Rockets"
	Weapon.SlotNumber 7
	Inventory.PickupSound "items/qweapon"
	Inventory.Pickupmessage "$GOT_QD_RL"
	Weapon.SisterWeapon "QD_MultiRocketLauncher"
	Tag "$TAG_QD_RL"
	+NOAUTOFIRE
	+NOALERT
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		QRLG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("QD_QRocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/rocketf",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QRLF ABCD 2 bright
		QRLG A 20
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	Spawn:
		QRLP A -1
		Stop
   }
}

actor QD_MultiRocketLauncher : QD_RocketLauncher
{
	Tag "$TAG_QD_RL_M"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "QD_MultiRockets"
	States
	{
	Ready:
		QMRG A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfTargetInLos("FireTargeted",0,JLOSF_DEADNOJUMP|JLOSF_ALLYNOJUMP|JLOSF_COMBATANTONLY|JLOSF_NOAUTOAIM,256)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocket",-6,1,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocket",-2,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocket",2,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocket",6,0,0,1,1,0)
		Goto Anim
	FireTargeted:
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocketTarget",-1,1,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocketTarget",0,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocketTarget",0,0,0,1,1,0)
		TNT1 A 0 A_FireCustomMissile("QD_QMiniRocketTarget",1,0,0,1,1,0)
	Anim:
		TNT1 A 0 A_PlaySound("weapons/rocketf",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QMRF ABCD 2 bright
		QMRG A 20
		TNT1 A 0 A_ReFire
    	Goto Ready
	}
}

actor QD_MultiRocketLauncher_P : WeaponGiver
{
  DropItem "QD_MultiRocketLauncher"
  Inventory.Pickupmessage "$GOT_QD_RL"
  Weapon.AmmoGive 5
  States
  {
  Spawn:
		QRLP A -1
		Stop
  }
}