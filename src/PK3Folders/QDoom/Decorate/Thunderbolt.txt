Actor QD_LightningBall : BFGBall
{
	var int user_radius;
	var int user_count;
	Speed 25
	Damage (0)
	RenderStyle Normal
	DeathSound "weapons/lgphit"
	+FORCERADIUSDMG
	DamageType "Quake1Cell"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/lgpfly",1,0.9,1)
		LBAL ABCD 3 Bright
		Loop
	Death:
		TNT1 A 0 // 40, 80
		TNT1 A 0 A_Explode(40,128,1,0,128)
		TNT1 A 0 A_Explode(80,128)
		TNT1 A 0 A_Quake(7,6,0,320,"")
		TNT1 A 0 A_Quake(4,8,0,640,"")
		TNT1 A 0 A_Quake(2,9,0,4096,"")
		TNT1 A 0 A_SpawnItem("QD_LightBlueQuakeExplo")
		TNT1 A 0 A_RadiusGive("QD_LGExpItem",192,RGF_MONSTERS|RGF_PLAYERS|RGF_NOTARGET)
		PLSE ABCDE 2 Bright A_Explode(30,192,0,1,192)
		Stop
  }
}

ACTOR QD_LGExpItem : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	Pickup:
		TNT1 A 0 A_CheckProximity("Spawn","QD_LightningBall",192,1,CPXF_CHECKSIGHT|CPXF_SETMASTER|CPXF_CLOSEST|CPXF_ANCESTOR)
		Stop
	Spawn:
		TNT1 A 0 A_SpawnItemEx("QD_LGExpTarget",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_SETTRACER)
		Stop
	}
}

ACTOR QD_LGExpTarget : QD_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Warp(AAPTR_MASTER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION)
		TNT1 A 0 A_SpawnItemEx("QD_LGExpShooter",0,0,0,0,0,0,0,SXF_SETTARGET)
		TNT1 AAAAAAAAAAAA 1 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,0,0.5)
		Stop
	}
}

ACTOR QD_LGExpShooter : QD_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/lgloop",5)
		TNT1 AAAAA 2 A_CustomRailgun((0),0,"none","none",RGF_SILENT|RGF_EXPLICITANGLE,1,0,"BulletPuff",0,0,192,0,5,0,"QD_PlasmaLightningTrail",0)
		Stop
	}
}

Actor QD_Thunderbolt : Plasmarifle
{
	Inventory.Pickupmessage "$GOT_QD_TB"
	Tag "$TAG_QD_TB"
	Weapon.BobRangeX 0.1
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 2.0
	Decal PlasmaScorch
	Weapon.AmmoUse 1
	Weapon.AmmoType "QD_Cells"
	Weapon.AmmoGive 40
	Weapon.SlotNumber 8
	Weapon.KickBack 0
	Inventory.PickupSound "items/qweapon"
	Weapon.SisterWeapon "QD_PlasmaThunderbolt"
	DamageType "Quake1Cell"
	States
	{
	Ready:
		TBLG AAAAAAAAAAABBCCDDCCBBAAAAAAA 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
		TBLG A 0 A_Lower
    	Loop
  	Select:
		TBLG A 0 A_Raise
    	Loop
	Fire:
		TNT1 A 0 A_PlaySound("weapons/lgstart",5)
	Anim1:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters // 30
		TNT1 A 0 A_RailAttack((30),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"none",0,0,968,0,12,0,"QD_LightningTrail",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_Jump(256,"AnimA1","AnimB1","AnimC1")
	Anim2:
		TNT1 A 0 A_PlaySound("weapons/lgloop",6,1.0,1)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_RailAttack((30),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"none",0,0,968,0,12,0,"QD_LightningTrail",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_Jump(256,"AnimA2","AnimB2","AnimC2")
	AnimA1:
		TBLF ABC 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"QD_PainlessPuff",0,0,968,0,12,0,"QD_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimB1:
		TBLF BCA 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"QD_PainlessPuff",0,0,968,0,12,0,"QD_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimC1:
		TBLF CAB 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"QD_PainlessPuff",0,0,968,0,12,0,"QD_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimA2:
		TBLF AABB 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"QD_PainlessPuff",0,0,968,0,12,0,"QD_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	AnimB2:
		TBLF BBCC 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"QD_PainlessPuff",0,0,968,0,12,0,"QD_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	AnimC2:
		TBLF CCAA 1 bright A_RailAttack((0),0,0,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"QD_PainlessPuff",0,0,968,0,12,0,"QD_LightningTrail",1)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	FireEnd:
		TNT1 A 0 A_StopSound(6)
		Goto Ready
	Flash:
		TNT1 A 0
		//TNT1 A 0 A_SetPitch(pitch-0.5)
		TNT1 A 2 A_Light(Random(1,3))
		//TNT1 A 0 A_SetPitch(pitch+0.5)
		TNT1 A 2 A_Light(Random(1,3))
		Goto LightDone
	Spawn:
    	TBLW AAAAAABBCCDDCCBB 1
		Loop
	}
}

Actor QD_PlasmaThunderbolt : QD_Thunderbolt
{
	Tag "$TAG_QD_TB_P"
	Weapon.AmmoUse 1
	Weapon.AmmoType "QD_PlasmaCells"
	Weapon.AmmoGive 0
	States
	{
	Ready:
		TBLP A 1 A_WeaponReady
    	Loop
	Fire:
		TNT1 A 0 A_PlayWeaponSound("weapons/lgpfire")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TBLQ ABCBA 2 bright
		TNT1 A 0 A_FireCustomMissile("QD_LightningBall",0,1,0,4,1,0)
		TNT1 A 0 A_GunFlash
		TBLQ DEFG 2 bright
		TBLP A 7
		TNT1 A 0 A_ReFire("Fire")
		Goto Ready
	Flash:
		TNT1 A 1
		TNT1 AAAA 1 A_SetPitch(pitch-0.9)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+1.8)
		Goto LightDone
	}
}

actor QD_PlasmaThunderbolt_P : WeaponGiver
{
  DropItem "QD_PlasmaThunderbolt"
  Inventory.Pickupmessage "$GOT_QD_TB"
  Weapon.AmmoGive 5
  States
  {
  Spawn:
		TBLW AAAAAABBCCDDCCBB 1
		Loop
  }
}

Actor QD_LightningTrail : QD_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(36,"Despawn")
		TNT1 A 0 A_SpawnItemEx("QD_LightningTrailParticle",0,FRandom(-4,4),FRandom(-4,4))
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor QD_LightningTrailParticle : QD_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"A","B","C","D","E","F")
		A:
    	LSHF A 1 bright
		Stop
		B:
    	LSHF B 1 bright
    	Stop
		C:
    	LSHF C 1 bright
    	Stop
		D:
    	LSHF D 1 bright
    	Stop
		E:
    	LSHF E 1 bright
    	Stop
		F:
    	LSHF F 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor QD_PlasmaLightningTrail : QD_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("QD_PlasmaLightningTrailParticle",0,FRandom(-4,4),FRandom(-4,4))
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor QD_PlasmaLightningTrailParticle : QD_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"A","B","C","D","E","F")
		A:
    	LSHF AB 1 bright
		Stop
		B:
    	LSHF BC 1 bright
    	Stop
		C:
    	LSHF CD 1 bright
    	Stop
		D:
    	LSHF DE 1 bright
    	Stop
		E:
    	LSHF EF 1 bright
    	Stop
		F:
    	LSHF FG 1 bright
		Stop
	}
}

Actor QD_PainlessPuff : BulletPuff
{
	-ALLOWPARTICLES
	+PAINLESS
	+BLOODLESSIMPACT
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		Stop
	}
}