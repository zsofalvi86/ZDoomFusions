Actor QD_LaserCannonBullet
{
	var int user_missiletime;
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Projectile
	Damage (12) // 12.5
	Speed 35
	SeeSound ""
	DeathSound "weapons/laserx"
	BounceSound "weapons/laserric"
	BounceType Doom
	BounceCount 4
	BounceFactor 1.0
	WallBounceFactor 1.0
	DamageType "Quake1Cell"
	+USEBOUNCESTATE
	+BLOODSPLATTER
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_missiletime",200)
	MissileLoop:
		TNT1 A 0 A_JumpIf(user_missiletime==0,"StopDeath")
		TNT1 A 0 A_SetUserVar("user_missiletime",user_missiletime-1)
		RLAP A 1 bright
		Loop
	Bounce:
		TNT1 A 0 A_ChangeFlag("HITOWNER",1)
		Goto MissileLoop
	StopDeath:
		TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
	Crash:
	Death:
		RPSX ABCDE 3 bright
		stop
	XDeath:
		TNT1 A 10
		stop
	}
}

Actor QD_LaserCannon : PlasmaRifle
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_QD_LC"
	Tag "$TAG_QD_LC"
	Inventory.PickupSound "items/qweapon"
	Weapon.SelectionOrder 110
	Weapon.SlotNumber 9
	Weapon.BobRangeX 0.05
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 2.0
	Weapon.AmmoUse 1
	Weapon.AmmoType "QD_Cells"
	Weapon.AmmoGive 40
	States
	{
	Spawn:
		QLSW AAAAAABBCCDDCCBB 1
		Loop
	Ready:
		QLSG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/laserf",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireCustomMissile("QD_LaserCannonBullet",0,1,-5,-4)
		TNT1 A 0 A_FireCustomMissile("QD_LaserCannonBullet",0,0,5,-4)
	    QLSF D 1 bright
		QLSF E 1 bright
		QLSF F 1 bright
		TNT1 A 0 A_Refire
		Goto Ready
	Hold:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/laserf",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireCustomMissile("QD_LaserCannonBullet",0,1,0,3)
		TNT1 A 0 A_FireCustomMissile("QD_LaserCannonBullet",0,0,0,3)
		QLSF A 1 bright
		QLSF B 1 bright
		QLSF C 1 bright
		QLSG A 1
		TNT1 A 0 A_ClearRefire
		goto Ready
	Flash:
		TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.3)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.6)
		Goto LightDone
	}
}

Actor QD_RedPlasmaBulletTrail2 : QD_TrailBase
{
	States
	{
  	Spawn:
    	RPLT ABCDD 1 bright
		RPLT E 2 bright
    	Stop
	}
}