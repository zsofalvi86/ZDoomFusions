Actor QD_NailgunNail : QD_Nail2Base
{
	DamageType "Quake1Nail"
	Speed 40
	Damage (11) // 9
}

Actor QD_NailgunFireNail : QD_FireNail2Base
{
	+PIERCEARMOR
	DamageType "Quake1Nail"
	Speed 40
	DamageType LesserCloseSplatter
	Damage (16) // 15
}

Actor QD_Nailgun : Chaingun
{
	+WEAPON.NOALERT
	+WEAPON.ALT_USES_BOTH
	Inventory.Pickupmessage "$GOT_QD_NG"
	Tag "$TAG_QD_NG"
	Inventory.PickupSound "items/qweapon"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 4
	Weapon.BobRangeX 0.1
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 2.0
	Weapon.AmmoType "QD_Nails"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.SisterWeapon "QD_LavaNailGun"
	States
	{
	Drop:
	Spawn:
		NGNW A -1
		Stop
	Ready:
		NG1G A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/NgFire",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
	    NG1F A 1 bright A_FireCustomMissile("QD_NailgunNail",0,1,-4,0)
		NG1F B 1 bright
		NG1F C 1 bright
		TNT1 A 0 A_Refire
		Goto Ready
	Hold:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/NgFire",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		NG1F D 1 bright A_FireCustomMissile("QD_NailgunNail",0,1,4,0)
		NG1F E 1 bright
		NG1F F 1 bright
		NG1G A 1
		TNT1 A 0 A_ClearRefire
		goto Ready
	Flash:
	AltFlash:
		TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.4)
		Goto LightDone
	}
}

Actor QD_LavaNailgun : QD_Nailgun
{
	Tag "$TAG_QD_NG_L"
	Weapon.AmmoType "QD_LavaNails"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	States
	{
	Ready:
		NG1L AABBCCCBBAAA 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/NgFire",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
	    NG1M A 1 bright A_FireCustomMissile("QD_NailgunFireNail",0,1,-4,0)
		NG1M B 1 bright
		NG1M C 1 bright
		TNT1 A 0 A_Refire
		TNT1 A 0 A_PlaySound("Weapons/NgCool",6)
		Goto Ready
	Hold:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/NgFire",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		NG1M D 1 bright A_FireCustomMissile("QD_NailgunFireNail",0,1,4,0)
		NG1M E 1 bright
		NG1M F 1 bright
		NG1L A 1
		TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_Refire("Fire")
		TNT1 A 0 A_PlaySound("Weapons/NgCool",6)
		goto Ready
	}
}

actor QD_LavaNailgun_P : WeaponGiver
{
  DropItem "QD_LavaNailgun"
  Inventory.Pickupmessage "$GOT_QD_NG"
  Weapon.AmmoGive 20
  States
  {
  Spawn:
		QGLW A -1
		Stop
  }
}