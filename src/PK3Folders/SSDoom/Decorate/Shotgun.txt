Actor DSS_Shotgun : Shotgun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_DSS_SG"
	Tag "$TAG_DSS_SG"
	Inventory.PickupSound "items/ssweapon"
	Weapon.SelectionOrder 1300
	Weapon.SlotNumber 3
	Weapon.AmmoUse 1
	Weapon.AmmoType "DSS_Shells"
	Weapon.AmmoGive 8
	Decal "BulletChip"
	DamageType "SeriousShell"
	States
	{
	Ready:
    	SSSG A 1 A_WeaponReady
    	Loop
  	Deselect:
		SSSG A 1 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
		SSSG A 1 A_Raise
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		// Top row, 3 pellets
		TNT1 A 0 A_FireBullets(0,-0.35,-1,(11),"DSS_BulletPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(2,-0.35,-1,(11),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-2,-0.35,-1,(11),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// Bottom row, 4 pellets
		TNT1 A 0 A_FireBullets(0.6,0.35,-1,(11),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-0.6,0.35,-1,(11),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(3,0.35,-1,(11),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-3,0.35,-1,(11),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_PlayWeaponSound("weapons/ssshotgun")
    	SSSF ABC 2 bright
		SSSG B 2 Offset(25,52)
		SSSG B 2 Offset(0,32)
		SSSG C 2 Offset(0,32)
		SSSG C 2 Offset(-4,37)
		SSSG C 2 Offset(-8,42)
		SSSG D 2 Offset(0,32)
		SSSG D 2 Offset(-4,39)
		SSSG D 2 Offset(0,32)
		SSSG C 2 Offset(-8,42)
		SSSG C 2 Offset(-4,37)
		SSSG C 2 Offset(0,32)
		SSSG B 2 Offset(0,32)
		SSSG B 2 Offset(25,52)
		SSSG A 2 Offset(-3,34)
		SSSG A 2 Offset(-1,32)
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAA 1 //A_SetPitch(pitch-0.45)
		TNT1 A 0 A_Light2
		TNT1 AA 1 //A_SetPitch(pitch+0.9)
		Goto LightDone
  	Spawn:
    	SSSW A -1
    	Stop
	}
}