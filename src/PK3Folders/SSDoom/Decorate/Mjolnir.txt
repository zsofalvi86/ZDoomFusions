Actor QD_Mjolnir : Fist
{
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	Inventory.Pickupmessage "$GOT_QD_MJ"
	Tag "$TAG_QD_MJ"
	Inventory.PickupSound "items/qweapon"
	Weapon.SlotNumber 1
	Weapon.BobRangeX 0.1
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 2.0
	Weapon.AmmoUse 15
	Weapon.AmmoType "QD_Cells"
	Weapon.AmmoGive 45
	States
	{
	Ready:
    	AXEG A 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_Lower
    	Loop
  	Select:
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/axeswing")
    	AXEG CDEFG 2
		TNT1 A 0 A_FireBullets(0,0,1,(120),"QD_HammerPuff",FBF_NORANDOM,72)
		TNT1 A 0 A_JumpIfNoAmmo("SkipLightning")
		TNT1 A 0 A_PlaySound("weapons/mjollight",5)
		AXEG H 5 A_GunFlash("Lightning1")
		AXEG IJK 5 A_GunFlash("Lightning2")
		Goto AnimEnd
	SkipLightning:
		AXEG HIJK 5
	AnimEnd:
		AXEG A 10
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	Lightning1:
		TNT1 A 0 A_RailAttack((40),0,1,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"none",-6,0,640,0,4,0,"QD_MjolnirTrail",-11)
		TNT1 A 0 A_RailAttack((40),0,0,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"none",6,0,640,0,4,0,"QD_MjolnirTrail",-11)
	Lightning2:
		TNT1 AAAA 0 A_RailAttack((30),0,0,"none","none",RGF_EXPLICITANGLE|RGF_SILENT|RGF_NOPIERCING,0,"none",(FRandom(-32,32)),0,640,0,4,0,"QD_MjolnirTrail",-11)
		Goto LightDone
  	Spawn:
    	AXEP A -1
    	Stop
	}
}

Actor QD_HammerPuff : BulletPuff
{
	+PUFFONACTORS
	+ALWAYSPUFF
	SeeSound "weapons/mjolhit"
	ActiveSound "weapons/mjolhit"
	AttackSound "weapons/mjolhit"
	States
	{
	Spawn:
	Crash:
	Melee:
		TNT1 A 0
		PUFF AB 2 Bright
		PUFF CD 2
		Stop
	}
}

Actor QD_MjolnirTrail : QD_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"A","B","C","D","E","F")
		A:
    	LSHF ABCD 1 bright
		Stop
		B:
    	LSHF BCDE 1 bright
    	Stop
		C:
    	LSHF CDEF 1 bright
    	Stop
		D:
    	LSHF DEFA 1 bright
    	Stop
		E:
    	LSHF EFAB 1 bright
    	Stop
		F:
    	LSHF FABC 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}