Actor DSS_SniperRifle : Chaingun
{
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	Inventory.Pickupmessage "$GOT_DSS_SR"
	Tag "$TAG_DSS_SR"
	Inventory.PickupSound "items/ssweapon"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 7
	Weapon.AmmoType "DSS_SniperBullets"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.Kickback 10
	Decal "BulletChip"
	DamageType "SeriousSniper"
	States
	{
	Drop:
	Spawn:
		SSSS W -1
		Stop
	Ready:
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"ReadyScope")
		SSSS A 1 A_WeaponReady
		Loop
	ReadyScope:
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",0)
	DeseLoop:
		SSSS A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		SSSS A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("Weapons/sssniperf")
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"FireScope")
	FireNormal:
	    TNT1 A 0 A_FireBullets(0,0,1,(70),"DSS_SniperPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		SSSS BCD 2 bright
		SSSS A 2 Offset(0,38)
		SSSS A 2 Offset(0,36)
		SSSS A 2 Offset(0,34)
		SSSS A 2 Offset(0,32)
		SSSS A 35
		TNT1 A 0 A_Refire
		Goto Ready
	FireScope:
	    TNT1 A 0 A_FireBullets(0,0,1,(300),"DSS_SniperPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		SSSS ZZZ 2 bright
		SSSS Z 13 bright
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",0)
		SSSS Z 5 bright
		SSSS A 10
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		//TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		//TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor DSS_SniperPuff : DSS_BulletPuff
{
	+NOEXTREMEDEATH
}

Actor DSS_SniperZoom : Inventory
{
	Inventory.MaxAmount 1
}