Actor DSS_Flamethrower : PlasmaRifle
{
	+DONTGIB
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_DSS_FT"
	Tag "$TAG_DSS_FT"
	Inventory.PickupSound "items/ssweapon"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 6
	Weapon.AmmoUse 1
	Weapon.AmmoType "DSS_Napalm"
	Weapon.AmmoGive 20
	States
	{
	Ready:
    	SSFT BBCCDD 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
    	SSFT A 1 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
    	SSFT A 1 A_Raise
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_PlayWeaponSound("weapons/flame/start")
	Hold:
		TNT1 A 0 A_AlertMonsters
		SSFT E 1 bright A_FireCustomMissile("DSS_FlameMissile",FRandom(-2,2),1,0,4,1,FRandom(-0.75,0.75))
    	SSFT F 1 bright A_FireCustomMissile("DSS_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
        SSFT G 1 bright A_FireCustomMissile("DSS_FakeFlameMissile",FRandom(-2,2),1,0,4,1,FRandom(-0.75,0.75))
        SSFT F 1 bright A_FireCustomMissile("DSS_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_PlaySound("weapons/flame/fire",6,1,1)
		SSFT E 1 bright A_FireCustomMissile("DSS_FlameMissile",FRandom(-2,2),1,0,4,1,FRandom(-0.75,0.75))
    	SSFT F 1 bright A_FireCustomMissile("DSS_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
    	SSFT G 1 bright A_FireCustomMissile("DSS_FakeFlameMissile",FRandom(-2,2),1,0,4,1,FRandom(-0.75,0.75))
    	SSFT F 1 bright A_FireCustomMissile("DSS_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
    	TNT1 A 0 A_ReFire
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlayWeaponSound("weapons/flame/stop")
    	SSFT EFGH 1 bright A_FireCustomMissile("DSS_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
		Goto Ready
  	Flash:
    	TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.45)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.6)
		Goto LightDone
  	Spawn:
		SSFT W -1
		Stop
	}
}

Actor DSS_FlameMissile : DSS_SlowMissileBase
{
	+RIPPER
	+BLOODLESSIMPACT
	//BounceType Doom
	//BounceFactor 0.15
	//WallBounceFactor 0.12
	SeeSound ""
	DeathSound ""
	RenderStyle Normal
	Alpha 1.0
	Speed 26
	Damage (2)
	DamageType "SeriousFlame"
	ProjectileKickBack 0
	States
	{
	Spawn:
		TNT1 A 0
		FLPR ABCDE 1 bright A_CustomMissile("DSS_IgniteMissile",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_SAVEPITCH)
		FLSS ABCDEFGHIJK 1 bright A_CustomMissile("DSS_IgniteMissile",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_SAVEPITCH)
	Death:
		TNT1 A 0
		stop
	}
}

Actor DSS_IgniteMissile : DSS_SlowMissileBase
{
	var int user_countdown;
	+HITTRACER
	Radius 18
	Height 10
	SeeSound ""
	DeathSound ""
	RenderStyle None
	Speed 0
	Damage (0)
	DamageType "SeriousFlame"
	ProjectileKickBack 0
	States
	{
	Spawn:
		TNT1 A 2
		stop
	Death:
		//TNT1 A 0
		//stop
	XDeath:
		TNT1 A 0 A_SetUserVar("user_countdown",25)
	BurnLoop:
		TNT1 A 0 A_JumpIf(user_countdown==0,"BurnEnd")
		TNT1 AA 1 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION )
		TNT1 A 0 A_CheckProximity("KillOther","DSS_IgniteMissile",10,1,CPXF_SETMASTER)
		Goto Resume
	KillOther:
		TNT1 A 0 A_RemoveMaster(RMVF_EVERYTHING)
	Resume:
		TNT1 A 0 A_SpawnItemEx("DSS_FT_FireChunk",Random(-16,16),Random(-16,16),12,0,0,3,0,0,64)
		TNT1 A 0 A_Explode(5,20,0,0,20)
		TNT1 A 0 A_SetUserVar("user_countdown",user_countdown-1)
		Goto BurnLoop
	BurnEnd:
		TNT1 A 0
		stop
	}
}

Actor DSS_FT_FireChunk : ArchvileFire
{
	RenderStyle Normal
	Alpha 1.0
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/flame/burn")
		TNT1 A 0 A_Jump(255,"A","B")
		A:
		SFIR ABCDEFGH 2 bright
		Stop
		B:
		SFIR IJKLMNOP 2 bright
		Stop
	}
}

Actor DSS_FakeFlameMissile : DSS_FlameMissile
{
	Damage (0)
	+NOPAIN
	States
	{
	Spawn:
		TNT1 A 0
		FLPR ABCDE 1 bright
		FLSS ABCDEFGHIJK 1 bright
		stop
	}
}
