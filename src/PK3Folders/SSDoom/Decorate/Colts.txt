Actor DSS_Revolver : Pistol
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_DSS_C1"
	Tag "$TAG_DSS_C1"
	Inventory.PickupSound "items/ssweapon"
	Weapon.SelectionOrder 1500
	Weapon.SlotNumber 2
	Weapon.AmmoUse 0
	Decal "BulletChip"
	DamageType "SeriousColt"
	States
	{
	Ready:
    	SSRS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
  	Deselect:
		TNT1 A 0 A_SetInventory("DSS_RevolverMagazine",0)
	DeseLoop:
		SSRS A 1 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
		SSRS A 1 A_Raise
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("DSS_RevolverMagazine",0,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/coltfire") //10
		TNT1 A 0 A_FireBullets(0,0,1,(15),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_GiveInventory("DSS_RevolverMagazine",2)
    	SSRT ABC 2 bright
		SSRS CDEC 1
		SSRS BA 1
		SSRS A 6
		TNT1 A 0 A_ReFire
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("DSS_RevolverMagazine",1,1)
		Goto Ready
		TNT1 A 0 A_SetInventory("DSS_RevolverMagazine",0)
		TNT1 A 0 A_PlaySound("weapons/coltload",6)
		SSRS C 25
		Goto Ready
  	Flash:
    	TNT1 A 2 bright A_Light1
		TNT1 A 2 A_Light2
		Goto LightDone
  	Spawn:
    	REVW A -1
    	Stop
	}
}

Actor DSS_Revolvers : Pistol
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_DSS_C2"
	Tag "$TAG_DSS_C1"
	Inventory.PickupSound "items/ssweapon"
	Weapon.SelectionOrder 1400
	Weapon.SlotNumber 2
	Weapon.AmmoUse 0
	Decal "BulletChip"
	DamageType "SeriousColt"
	States
	{
	Ready:
    	SSRR A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
  	Deselect:
		TNT1 A 0 A_SetInventory("DSS_RevolverMagazine",0)
	DeseLoop:
		SSRR A 1 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
		SSRR A 1 A_Raise
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("DSS_RevolverMagazine",0,"Reload")
		TNT1 A 0 A_JumpIf(CountInv("DSS_RevolverMagazine") % 2 == 1,"Hold")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/coltfire") // 10
		TNT1 A 0 A_FireBullets(0,0,1,(15),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_GiveInventory("DSS_RevolverMagazine",1)
    	SSRQ ABC 2 bright
		SSRR CDEC 1
		SSRR B 1
		TNT1 A 0 A_ReFire("Hold")
		SSRR A 4
		Goto Ready
	Hold:
		TNT1 A 0 A_JumpIfInventory("DSS_RevolverMagazine",0,"Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/coltfire")
		TNT1 A 0 A_FireBullets(0,0,1,(13),"DSS_BulletPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_GiveInventory("DSS_RevolverMagazine",1)
		SSRQ DEF 2 bright
		SSRR GHIG 1
		SSRR F 1
		TNT1 A 0 A_ReFire("Fire")
		SSRR A 4
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("DSS_RevolverMagazine",1,1)
		Goto Ready
		TNT1 A 0 A_SetInventory("DSS_RevolverMagazine",0)
		TNT1 A 0 A_PlaySound("weapons/coltload",6)
		SSRR C 20
		TNT1 A 0 A_PlaySound("weapons/coltload",6)
		SSRR G 25
		TNT1 A 0 A_ReFire("Fire")
		Goto Ready
  	Flash:
    	TNT1 A 1 bright A_Light1
		TNT1 AAAA 1 bright //A_SetPitch(pitch-0.35)
		TNT1 A 0 A_Light2
		TNT1 AA 1 //A_SetPitch(pitch+0.7)
		Goto LightDone
  	Spawn:
    	REVW A -1
    	Stop
	}
}

Actor DSS_RevolverMagazine : Inventory
{
	Inventory.MaxAmount 12
}