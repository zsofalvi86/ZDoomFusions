const int DSS_C_CANNONBALL_MIN_DAMAGE = 20;
const int DSS_C_CANNONBALL_MAX_DAMAGE = 40;
const int DSS_C_CANNONBALL_GLANCE_DAMAGE = 2;
const int DSS_C_CANNONBALL_GLANCE_RADIUS = 112;
const int DSS_C_CANNONBALL_TRAMPLE_DAMAGE = 12;
const int DSS_C_CANNONBALL_TRAMPLE_RADIUS = 128;
const int DSS_C_CANNONBALL_EXPLOSION_DAMAGE = 256;
const int DSS_C_CANNONBALL_EXPLOSION_RADIUS = 256;

Actor DSS_Cannon : BFG9000
{
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	-WEAPON.NOAUTOFIRE
	Inventory.Pickupmessage "$GOT_DSS_CN"
	Tag "$TAG_DSS_CN"
	Inventory.PickupSound "items/ssweapon"
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 8
	Weapon.AmmoType "DSS_CannonBalls"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	States
	{
	Spawn:
		CNNP A -1
		Stop
	Ready:
		CNNG A 1 A_WeaponReady
		Loop
	Deselect:
		CNNG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		CNNG A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("weapons/cannonchrg",6)
		CNNG A 3
		TNT1 A 0 A_SetInventory("DSS_MC_Charge",0)
		TNT1 A 0 A_SetInventory("DSS_MC_UnloadTolerance",0)
		Goto CannonChargeInitial
	CannonChargeLoopy:
		CNNG A 2 A_WeaponOffset(Random(-1,1),32+Random(0,2)+CountInv("DSS_MC_Charge"))//A_Quake(1,3,0,1,"")
	CannonChargeInitial:
	    TNT1 A 0 A_JumpIfInventory("DSS_MC_Charge",0,"CannonChargeReleaseLoopy")
		TNT1 A 0 A_GiveInventory("DSS_MC_Charge",1)
		TNT1 A 0 A_ReFire("CannonChargeLoopy")
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_JumpIfInventory("DSS_MC_Charge",10,"CannonChargeRelease3")
		TNT1 A 0 A_JumpIfInventory("DSS_MC_Charge",5,"CannonChargeRelease2")
		Goto CannonChargeRelease
	CannonChargeReleaseLoopy:
		TNT1 A 0 A_JumpIfInventory("DSS_MC_UnloadTolerance",0,"CannonChargeRelease")
		TNT1 A 0 A_GiveInventory("DSS_MC_UnloadTolerance",1)
		CNNG A 1 A_WeaponOffset(Random(-2,2),32+Random(0,3)+CountInv("DSS_MC_Charge"))//A_Quake(2,2,0,1,"")
		TNT1 A 0 A_ReFire("CannonChargeReleaseLoopy")
		Goto CannonChargeRelease
	CannonChargeRelease:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_JumpIfInventory("DSS_MC_Charge",10,"CannonChargeRelease3")
		TNT1 A 0 A_JumpIfInventory("DSS_MC_Charge",5,"CannonChargeRelease2")
		Goto CannonChargeRelease1
	CannonChargeRelease1:
		//TNT1 AAAAAAAAAAA 0 A_FireCustomMissile("DSS_CannonBall1",0,0,0,-2,1,3)
		TNT1 A 0 A_FireCustomMissile("DSS_CannonBallL1",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonChargeRelease2:
		//TNT1 AAAAAAAAAAAAAAA 0 A_FireCustomMissile("DSS_CannonBall2",0,0,0,-2,1,3)
		TNT1 A 0 A_FireCustomMissile("DSS_CannonBallL2",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonChargeRelease3:
		//TNT1 AAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("DSS_CannonBall3",0,0,0,-2,1,3)
		TNT1 A 0 A_FireCustomMissile("DSS_CannonBallL3",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonReleaseAnim:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("weapons/cannonfire",CHAN_WEAPON)
		CNNF ABCDEFG 2 bright
		CNNG C 2 offset(0,40)
		CNNG C 2 offset(0,36)
		CNNG B 2 offset(0,34)
		CNNG A 2 offset(0,32)
		Goto Ready
	Flash:
		TNT1 A 0 A_Light1
		TNT1 AA 1 //A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light2
		TNT1 A 1 //A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor DSS_CannonBallBase : DSS_SlowMissileBase
{
  -NOGRAVITY
  +USEBOUNCESTATE
  Damage (Random(DSS_C_CANNONBALL_MIN_DAMAGE,DSS_C_CANNONBALL_MAX_DAMAGE))
  DamageType Explosive
  BounceType Doom
  BounceFactor 0.65
  WallBounceFactor 0.65
  BounceCount 4
  Gravity 0.2
  SeeSound ""
  DeathSound ""
  BounceSound "weapons/cannonbnc"
  Obituary "$OB_MPROCKET"
  DamageType "SeriousCannon"
  States
  {
  Spawn:
	TNT1 A 1 A_SpawnItemEx("DSS_CannonTrail",Random(-16,16),Random(-16,16),1,0,0,3,0,0,248)
	TNT1 A 0 A_Explode(DSS_C_CANNONBALL_GLANCE_DAMAGE,DSS_C_CANNONBALL_GLANCE_RADIUS,0,0,1)
	loop
  Crash:
  Death:
  XDeath:
	TNT1 A 1 A_Explode(DSS_C_CANNONBALL_TRAMPLE_DAMAGE,DSS_C_CANNONBALL_TRAMPLE_RADIUS,0,0,DSS_C_CANNONBALL_TRAMPLE_RADIUS)
	stop
  }
}

Actor DSS_CannonBallFinalBase : DSS_CannonBallBase
{
  DeathSound "weapons/ssexp"
  States
  {
  Crash:
  Death:
  XDeath:
	TNT1 A 0 A_Explode(DSS_C_CANNONBALL_EXPLOSION_DAMAGE,DSS_C_CANNONBALL_EXPLOSION_RADIUS,0)
	TNT1 A 0 A_Quake(4,4,0,375,"")
	TNT1 A 0 A_Quake(2,5,0,750,"")
	TNT1 A 0 A_Quake(1,5,0,3200,"")
	//TNT1 A 0 A_SpawnItemEx("DSS_GenericLinearShockwaveSpawnerLong",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	BMZP A 4 bright
	BMZP BCDEF 3 bright
	Stop
  }
}

Actor DSS_CannonBall1 : DSS_CannonBallBase
{
  Speed 30
}

Actor DSS_CannonBall2 : DSS_CannonBallBase
{
  Speed 40
}

Actor DSS_CannonBall3 : DSS_CannonBallBase
{
  Speed 50
}

Actor DSS_CannonBallL1 : DSS_CannonBall
{
  Speed 27
}

Actor DSS_CannonBallL2 : DSS_CannonBall
{
  Speed 39
}

Actor DSS_CannonBallL3 : DSS_CannonBall
{
  Speed 50
}

Actor DSS_CannonBallFinal1 : DSS_CannonBallFinalBase
{
  Speed 30
  States
  {
  Spawn:
	CBLP AABBCCBB 1 A_Explode(DSS_C_CANNONBALL_GLANCE_DAMAGE,DSS_C_CANNONBALL_GLANCE_RADIUS,0,0,1)
	Loop
  Bounce:
	//TNT1 A 0 A_SpawnItemEx("DSS_GenericLinearShockwaveSpawnerShort",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	Goto Spawn
  }
}

Actor DSS_CannonBallFinal2 : DSS_CannonBallFinalBase
{
  Speed 40
  States
  {
  Spawn:
	CBLP DDEEDDEEFFEEFFEE 1 bright A_Explode(DSS_C_CANNONBALL_GLANCE_DAMAGE,DSS_C_CANNONBALL_GLANCE_RADIUS,0,0,1)
	Loop
  Bounce:
	//TNT1 A 0 A_SpawnItemEx("DSS_GenericLinearShockwaveSpawnerShort",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
  BLoop:
	CBLP AABBCCBB 1 A_Explode(DSS_C_CANNONBALL_GLANCE_DAMAGE,DSS_C_CANNONBALL_GLANCE_RADIUS,0,0,1)
    Loop
  }
}

Actor DSS_CannonBallFinal3 : DSS_CannonBallFinalBase
{
  Speed 50
  States
  {
  Spawn:
	CBLP GGHHGGHHIIHHIIHH 1 bright A_Explode(DSS_C_CANNONBALL_GLANCE_DAMAGE,DSS_C_CANNONBALL_GLANCE_RADIUS,0,0,1)
	Loop
  Bounce:
	//TNT1 A 0 A_SpawnItemEx("DSS_GenericLinearShockwaveSpawnerShort",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
  BLoop:
	CBLP DDEEDDEEFFEEFFEE 1 bright A_Explode(DSS_C_CANNONBALL_GLANCE_DAMAGE,DSS_C_CANNONBALL_GLANCE_RADIUS,0,0,1)
	Loop
  }
}

Actor DSS_CannonTrail : DSS_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 3
		TNT1 A 0 A_Jump(256,"A","B","C")
	A:
		MFC1 ABCD 2 bright
		Stop
	B:
		MFC2 ABCD 2 bright
		Stop
	C:
    	MFC3 ABCD 2 bright
		Stop
	}
}

Actor DSS_MC_Charge : Inventory
{
	Inventory.MaxAmount 10
}

Actor DSS_MC_UnloadTolerance : Inventory
{
	Inventory.MaxAmount 100
}