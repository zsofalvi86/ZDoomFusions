Actor DSS_SSRocket : Rocket
{
	+FORCERADIUSDMG
	Speed 17
	Damage (140)
	SeeSound ""
	DeathSound "weapons/ssexp"
	Obituary "$OB_MPROCKET"
	DamageType "SeriousRocket"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/rocket/fly",6,1,1)
	Loopy:
		MISL A 1 bright
		Loop
	Death:
		TNT1 A 0 A_StopSound(6) // 100, 50
		TNT1 A 0 A_Explode(70,168)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor DSS_RocketLauncher : RocketLauncher
{
	Weapon.SelectionOrder 2400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "DSS_Rockets"
	Weapon.SlotNumber 5
	Inventory.PickupSound "items/ssweapon"
	Inventory.Pickupmessage "$GOT_DSS_RL"
	Tag "$TAG_DSS_RL"
	+NOAUTOFIRE
	+NOALERT
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		SSRL A 1 A_WeaponReady
		Loop
	Deselect:
		SSRL A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		SSRL A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("DSS_SSRocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/rocketf",1)
		SSRF ABCDE 2 bright
		SSRL ABCD 2
		SSRL A 6
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 //A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 //A_SetPitch(pitch+0.8)
		Goto LightDone
	Spawn:
		SSRW A -1
		Stop
   }
}