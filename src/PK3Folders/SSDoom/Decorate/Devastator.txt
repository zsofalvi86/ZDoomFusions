Actor DSS_Devastator : DSS_Scoped
{
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	Inventory.Pickupmessage "$GOT_DSS_DEV"
	Tag "$TAG_DSS_DEV"
	Inventory.PickupSound "items/ssweapon"
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 9
	Weapon.AmmoType "DSS_DevastatorShells"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 4
	Decal "BulletChip"
	DamageType "SeriousSniper"
	States
	{
	Drop:
	Spawn:
		SDEV W -1
		Stop
	Ready:
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"ReadyScope")
		SDVG A 1 A_WeaponReady
		Loop
	ReadyScope:
		TNT1 A 0 A_RollScope(false)
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_ClearOverlays(11,11)
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",0)
	DeseLoop:
		SDVG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		SDVG A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/devf")
		TNT1 A 0 A_FireCustomMissile("DSS_DevastatorShot",0,1,0,1,1,0)
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"FireScope")
	FireNormal:
		SDVF ABCDE 2 bright
		SDVG BCDA 2
		TNT1 A 0 A_Refire
		Goto Ready
	FireScope:
		SSSS ZZZZZ 2 bright
		SSSS ZZZZZZZZ 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("DSS_SniperZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		TNT1 A 0 A_RollScope(true)
		TNT1 A 0 A_Overlay(11,"ScopeStart",false)
		TNT1 A 0 A_OverlayFlags(11,PSPF_ALPHA|PSPF_RENDERSTYLE|PSPF_ADDWEAPON,true)
		TNT1 A 0 A_OverlayRenderstyle(11,STYLE_Translucent)
		TNT1 A 0 A_OverlayAlpha(11, 0.75)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 4 bright
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_RollScope(false)
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		SSSS Z 5 bright
		TNT1 A 0 A_ClearOverlays(11,11)
		TNT1 A 0 A_SetInventory("DSS_SniperZoom",0)
		SDVG A 10
		Goto Ready
	ScopeStart:
		TNT1 A 0 A_SetScopeSprite
	ScopeWall:
		SXHS "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeGreen:
		SXHT "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeYellow:
		SXHU "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeRed:
		SXHV "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	Flash:
		TNT1 A 1 A_Light1
		//TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		//TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor DSS_DevTrail : DSS_TrailBase
{
	RenderStyle "Translucent"
	Alpha 0.2
	States
	{
  	Spawn:
		TNT1 A 2
    	WTRS ABCDEF 3
    	Stop
	}
}