#library "SWDoom"
#include "zcommon.acs"

global int 1:level;
global int 2:SG;
global int 3:CG;
global int 4:SSG;
global int 5:RL;
global int 6:PG;
global int 7:BFG;
global int 8:CS;
global int 9:BER;
global int 10:POW;
global int 11:HP;
global int 12:DROP;
global int 13:XSHP;
global int 14:XSPOW[];

script "map exit" UNLOADING
{
	level += 1;
	if(RL>=2) {RL=0;}
	if(SSG>0 && (CG==0 || CG==3)) {CG=1;}
	DROP = RL;

	For(int i=0;i<=7;i++)
	{
		If (PlayerInGame(i) && GetActorProperty(6000+i, APROP_HEALTH) <= 0) // any player dead, reset the levels
		{
			SG=0; SSG=0; CG=0;
			break;
		}
	}
}

script "startup" ENTER
{
	Thing_ChangeTID(0, 6000 + PlayerNumber());
}

script "GetLevelNum" (void)
{
	SetResultValue(level);
	Print(i:level); // For debug purposes, it doesn't work on inventory anyway
}

script "SetSpawn" (int spawnTag)
{
	switch(spawnTag)
	{
		case 0:CS++;break;
		case 1:SG++;break;
		case 2:SSG++;break;
		case 3:CG++;break;
		case 4:RL++;break;
		case 5:PG++;break;
		case 6:BFG++;break;
		case 7:BER++;break;
		case 8:POW++;break;
		case 9:HP++;break;
		case 10:DROP++;break;
	}
	
	if(SG>2){SG=1;}
	if(SSG>2){SSG=1;}
	if(CG>3){CG=3;}
	if(RL>3){RL=1;}
	if(PG>2){PG=1;}
	if(BFG>2){BFG=1;}
	if(CS>2){CS=1;}
	if(BER>5){BER=1;}
	if(POW>5){POW=1;}
	if(HP>2){HP=1;}
	if(DROP>3){DROP=2;}
	
	if(spawnTag==1) //SG
	{
		if(SSG==1 && SG==2) {SG=1;}
		if(PG==0 && SG==2) {SG=1;}
		if(SSG==2 && SG==1) {SG=2;}
	}
	if(spawnTag==2) //SSG
	{
		if((SG==0 || PG==0) && SSG==1) {SSG=2;}
		if(SG==1 && SSG==2) {SSG=1;}
		if(SG==2 && SSG==1) {SSG=2;}
	}
	if(spawnTag==3) //CG
	{
		if((PG==0 && RL==0) && CG==2) {CG=1;}
	}
	if(spawnTag==4) //RL
	{
		if(CS==1 && RL==3) {RL=1;}
	}
	if(spawnTag==5) //PG
	{
		if(BFG==1 && PG==2) {PG=1;}
		if(BFG==2 && PG==1) {PG=2;}
	}
	if(spawnTag==6) //BFG
	{
		if(PG==0 && BFG==1) {BFG=2;}
		if(PG==1 && BFG==2) {BFG=1;}
		if(PG==2 && BFG==1) {BFG=2;}
	}
	
	if(spawnTag==7) //BER
	{
		if(CS==2 && BER==1) {BER=2;CS=3;}
		if(POW==1 && BER==1) {BER=2;}
		if(POW==2 && BER==2) {BER=3;}
		if(CS==3 && BER==3) {BER=4;CS=4;}
		if(POW==3 && BER==3) {BER=4;}
		if(CS==4 && BER==4) {BER=5;CS=0;}
		if(POW==4 && BER==4) {BER=5;}
	}
	if(spawnTag==8) //POW(erups)
	{
		int i,j; int amo=100;
		for (i=1; i<=5; i++) {if(XSPOW[i] > amo) {amo = XSPOW[i];j=i;}}
		if (XSPOW[j] > 100) {XSPOW[j]-=100; POW=j;}
		else {POW=6;}
	}
	if(spawnTag==10) //shotgunner DROP
	{
		if(RL==0 && DROP==2) {DROP=1;}
		if(RL==0 && DROP==3) {DROP=1;}
		if(RL==1 && DROP==3) {DROP=2;}
		if(RL==2 && DROP==0) {DROP=3;}
	}
}

script "GetSpawn" (int spawnTag) // call this with correct values, this checks nothing!
{
	int ret = 0;
	switch(spawnTag)
	{
		case 0:ret=CS;break;
		case 1:ret=SG;break;
		case 2:ret=SSG;break;
		case 3:ret=CG;break;
		case 4:ret=RL;break;
		case 5:ret=PG;break;
		case 6:ret=BFG;break;
		case 7:ret=BER;break;
		case 8:ret=POW;break;
		case 9:ret=HP;break;
		case 10:ret=DROP;break;
	}
	
	SetResultValue(ret);
}

script "RegisterHP" (int amount)
{
	XSHP += amount;
}

script "DebtHP" (int amount)
{
	XSHP -= amount;
}

script "SpendHP" (int amount)
{
	int ret = 0;
	if(XSHP > amount) {XSHP-=amount; ret=1;}
	SetResultValue(ret);
}

script "CheckHP" (int amount)
{
	int ret = 0;
	if(XSHP > amount) {ret=1;}
	SetResultValue(ret);
}

script "RegisterPOW" (int slot, int amount)
{
	XSPOW[slot]+=amount;
}
