Actor PPE_Uzi : Chaingun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_UZI"
	Tag "$TAG_PPE_UZI"
	Weapon.SelectionOrder 1400
	Weapon.SlotNumber 4
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "PPE_Bullets"
	Weapon.UpSound "weapons/uzi/up"
	Inventory.PickupSound "q/smallweapon"
	Weapon.KickBack 100
	States
	{
	Ready:
		SUZG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
  	Deselect:
    	SUZG A 1 A_Lower(12)
    	Loop
  	Select:
    	SUZG A 1 A_Raise(12)
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("PPE_UziMag", 0, "Reload")
		TNT1 A 0 A_GiveInventory("PPE_UziMag",1)
		TNT1 A 0 A_Jump(236,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/uzi/fire")
		TNT1 A 0 A_FireBullets(2.5,2.5,-1,(10),"PPE_BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_UziCasingShooter",0,0,0,2)
		SUZF ABC 1 bright
		TNT1 A 0 A_ReFire("Fire2")
		Goto Ready
	Fire2:
		TNT1 A 0 A_JumpIfInventory("PPE_UziMag", 0, "Reload")
		TNT1 A 0 A_GiveInventory("PPE_UziMag",1)
		TNT1 A 0 A_Jump(236,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/uzi/fire")
		TNT1 A 0 A_FireBullets(2.5,2.5,-1,(10),"PPE_BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_UziCasingShooter",0,0,0,2)
		TNT1 A 0 A_CheckForReload(2,"SkipTracer")
		TNT1 A 0 A_FireCustomMissile("PPE_Tracer",FRandom(-1,1),0,0,2,0,FRandom(-1,1))
	SkipTracer:
		SUZF DEF 1 bright
		//SUZG A 1
		TNT1 A 0 A_ReFire("Fire")
		Goto Ready
	Reload:
		SUZG AAAAAAAA 1 A_WeaponOffset(0,11,WOF_ADD)
		TNT1 A 0 A_JumpIfInventory("PPE_UziMag", 1, "ReloadReal")
		TNT1 A 0 A_Overlay(-2,"MagFake")
		SUZR AAAAAAAA 1 A_WeaponOffset(0,-11,WOF_ADD)
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		SUZR A 10
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		SUZR A 7
		Goto ReloadEnd
	ReloadReal:
		SUZR AAAAAAAA 1 A_WeaponOffset(0,-11,WOF_ADD)
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		TNT1 A 0 A_Overlay(-2,"MagRight")
		SUZR A 14
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		SUZR A 5
		TNT1 A 0 A_SetInventory("PPE_UziMag",0)
		Goto ReloadEnd
	ReloadEnd:
		SUZR AAAAAAAA 1 A_WeaponOffset(0,11,WOF_ADD)
		TNT1 A 0 A_PlaySound("weapons/uzi/reload2")
		SUZG AAAAAAAA 1 A_WeaponOffset(0,-11,WOF_ADD)
		SUZG A 20 A_WeaponReady
		Goto Ready
	MagRight:
		SUZM AAAAAAAA 1 A_OverlayOffset(-2,-12,12,WOF_ADD)
		TNT1 A 2
		SUZM BBBBBBBB 1 A_OverlayOffset(-2,12,-12,WOF_ADD)
		SUZM B 8
		Goto LightDone
	MagFake:
		SUZM B 8
		SUZM BBBBBB 1 A_OverlayOffset(-2,-8,8,WOF_ADD)
		SUZM B 5
		SUZM BBBBBB 1 A_OverlayOffset(-2,8,-8,WOF_ADD)
		SUZM B 8
		Goto LightDone
  	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_SetPitch(pitch-0.15)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.15)
		Goto LightDone
  	Spawn:
		SUZP A -1
		Stop
	}
}

Actor PPE_Uzis : PPE_Uzi
{
	Weapon.SelectionOrder 1500
	Weapon.AmmoUse 2
	Inventory.Pickupmessage "$GOT_PPE_UZI2"
	Inventory.PickupSound "q/uzi2"
	Tag "$TAG_PPE_UZI2"
	States
	{
	Ready:
		SUZH A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
  	Deselect:
    	SUZH A 1 A_Lower(12)
    	Loop
  	Select:
    	SUZH A 1 A_Raise(12)
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("PPE_UziMag", 0, "Reload")
		TNT1 A 0 A_GiveInventory("PPE_UziMag",1)
		TNT1 A 0 A_Jump(236,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/uzi/fire")
		TNT1 A 0 A_FireBullets(3.5,3.5,2,(10),"PPE_BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_UziCasingShooterR",0,0,0,2)
		TNT1 A 0 A_FireCustomMissile("PPE_UziCasingShooterL",0,0,0,2)
		TNT1 A 0 A_CheckForReload(3,"SkipTracer")
		TNT1 A 0 A_FireCustomMissile("PPE_Tracer",FRandom(-1.5,1.5),0,-5,2,0,FRandom(-1.5,1.5))
	SkipTracer:
		SUZI ABC 1 bright 
		TNT1 A 0 A_ReFire("Fire2")
		Goto Ready
	Fire2:
		TNT1 A 0 A_JumpIfInventory("PPE_UziMag", 0, "Reload")
		TNT1 A 0 A_GiveInventory("PPE_UziMag",1)
		TNT1 A 0 A_Jump(236,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/uzi/fire")
		TNT1 A 0 A_FireBullets(3.5,3.5,2,(10),"PPE_BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_UziCasingShooterR",0,0,0,2)
		TNT1 A 0 A_FireCustomMissile("PPE_UziCasingShooterL",0,0,0,2)
		TNT1 A 0 A_CheckForReload(3,"SkipTracer2")
		TNT1 A 0 A_FireCustomMissile("PPE_Tracer",FRandom(-1.5,1.5),0,5,2,0,FRandom(-1.5,1.5))
	SkipTracer2:
		SUZI ABC 1 bright 
		//SUZG A 1
		TNT1 A 0 A_ReFire("Fire")
		Goto Ready
	Reload:
		SUZH AAAAAAAA 1 A_WeaponOffset(0,11,WOF_ADD)
		TNT1 A 0 A_JumpIfInventory("PPE_UziMag", 1, "ReloadReal")
		TNT1 A 0 A_Overlay(-2,"MagFake")
		SUZR AAAAAAAA 1 A_WeaponOffset(0,-11,WOF_ADD)
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		SUZR A 10
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		SUZR A 7
		Goto ReloadEnd
	ReloadReal:
		SUZR AAAAAAAA 1 A_WeaponOffset(0,-11,WOF_ADD)
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		TNT1 A 0 A_Overlay(-2,"MagRight")
		SUZR A 14
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		SUZR A 5
		SUZR AAAAAAAA 1 A_WeaponOffset(0,11,WOF_ADD)
		SUZR BBBBBBBB 1 A_WeaponOffset(0,-11,WOF_ADD)
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		TNT1 A 0 A_Overlay(-2,"MagLeft")
		SUZR B 14
		TNT1 A 0 A_PlaySound("weapons/uzi/reload1")
		SUZR B 5
		TNT1 A 0 A_SetInventory("PPE_UziMag",0)
		SUZR BBBBBBBB 1 A_WeaponOffset(0,11,WOF_ADD)
		TNT1 A 0 A_PlaySound("weapons/uzi/reload2")
		SUZH AAAAAAAA 1 A_WeaponOffset(0,-11,WOF_ADD)
		Goto Ready
	ReloadEnd:
		SUZR AAAAAAAA 1 A_WeaponOffset(0,11,WOF_ADD)
		TNT1 A 0 A_PlaySound("weapons/uzi/reload2")
		SUZH AAAAAAAA 1 A_WeaponOffset(0,-11,WOF_ADD)
		SUZH A 20 A_WeaponReady
		Goto Ready
	MagLeft:
		SUZM CCCCCCCC 1 A_OverlayOffset(-2,12,12,WOF_ADD)
		TNT1 A 2
		SUZM DDDDDDDD 1 A_OverlayOffset(-2,-12,-12,WOF_ADD)
		SUZM D 8
		Goto LightDone
  	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.2)
		Goto LightDone
  	Spawn:
		SUZP B -1
		Stop
	}
}

Actor PPE_Tracer : FastProjectile
{
	+BLOODLESSIMPACT
	+PAINLESS
	SeeSound ""
	DeathSound ""
	Radius 3
	Height 3
	Speed 70
	RenderStyle "Normal"
	Damage (0)
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1
	Loopy:
		PUFF A 1 bright
		Loop
	Death:
		TNT1 A 0
		Stop
	}
}

Actor PPE_UziCasingShooter : PPE_CasingShooterBase
{
	Speed 11
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BulletCasing",0,0,FRandom(-80,-65),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-12,-19))
		Stop
	}
}

Actor PPE_UziCasingShooterR : PPE_UziCasingShooter
{
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BulletCasing",0,8,FRandom(-80,-65),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-12,-19))
		Stop
	}
}

Actor PPE_UziCasingShooterL : PPE_UziCasingShooter
{
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BulletCasing",0,-8,FRandom(65,80),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-12,-19))
		Stop
	}
}

Actor PPE_UziMag : Inventory
{
	Inventory.MaxAmount 50
}