ACTOR PPE_StimpackSpawner : PPE_Spawner replaces Stimpack
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(CallAcs("SpendHP",90) == 1,"Spawn3")
		TNT1 A 0 A_JumpIf(CallAcs("SpendHP",10) == 1,"Spawn2")
		TNT1 A 0 ACS_NamedExecuteWithResult("RegisterHP",10)
		TNT1 A 0 A_SpawnPermanent("PPE_PowerupSpawner")
		stop
	Spawn2:
		TNT1 A 0 A_SpawnPermanent("PPE_MediKit")
		stop
	Spawn3:
		TNT1 A 0 A_SpawnPermanent("PPE_SWPortableMedikit")
		stop
	}
}

Actor PPE_MediKitSpawner : PPE_Spawner replaces MediKit
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(CallAcs("SpendHP",75) == 1,"Spawn2")
		TNT1 A 0 ACS_NamedExecuteWithResult("RegisterHP",5)
		TNT1 A 0 A_SpawnPermanent("PPE_MediKit")
		stop
	Spawn2:
		TNT1 A 0 A_SpawnPermanent("PPE_SWPortableMedikit")
		stop
	}
}

ACTOR PPE_MediKit : MediKit //replaces MediKit
{
	Inventory.PickupSound "items/pickup"
	Inventory.PickupMessage "$GOTHTH20"
	Health.LowMessage 25, "$GOTHTH20"
	Inventory.Amount 20
	States
	{
	Spawn:
		MEDP A -1 bright
		Stop
	}
}

ACTOR PPE_SWHealthBonusSpawner : PPE_Spawner replaces HealthBonus
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteWithResult("SetSpawn",9)
		TNT1 A 0 A_JumpIf(CallAcs("GetSpawn",9) == 10,"Spawn2")
		TNT1 A 0 A_SpawnPermanent("PPE_SWHealthBonus")
		stop
	Spawn2:
		TNT1 A 0 ACS_NamedExecuteWithResult("RegisterHP",1)
		TNT1 A 0 A_SpawnPermanent("PPE_PowerupSpawner")
		stop
	}
}

ACTOR PPE_SWHealthBonus : HealthBonus
{
    Inventory.PickupSound "items/pickup"
    Inventory.Amount 1
    Inventory.MaxAmount 200
	States
    {
    Spawn:
		SBON A -1 bright
		Stop
    }
}

ACTOR PPE_SWPortableMedikit : CustomInventory
{
	+INVENTORY.INVBAR
	Inventory.Icon "MEDPB0"
	Inventory.Amount 100
	Inventory.MaxAmount 100
	Inventory.InterHubAmount 100
	Inventory.PickupSound "items/pickup"
	Inventory.PickupMessage "$GOTPORTMED"
	Tag "$TAG_PPE_PORTMED"
	States
	{
	Spawn:
		MEDP B -1 bright
		Stop
	Use:
		TNT1 A 0 A_JumpIfHealthLower(100+stamina, "GiveHealth") //If below 100 (+adr) health, give health
		Fail
	GiveHealth:
		TNT1 A 0 A_JumpIfInventory("PPE_SWPortableMedikit",0,"GiveHealthFirst")
		TNT1 A 0 A_PlaySound("items/meduse2")
		Goto GiveHealthNow
	GiveHealthFirst:
		TNT1 A 0 A_PlaySound("items/meduse1")
	GiveHealthNow:
		TNT1 A 0 A_SetInventory("PPE_PortableHealBuffer",min(CountInv("PPE_SWPortableMedikit"),(100+stamina)-health))
		TNT1 A 0 A_SetHealth(health+CountInv("PPE_PortableHealBuffer")) //Add health
		TNT1 A 0 A_JumpIfInventory("PPE_PortableHealBuffer",2,1)
		Stop
		TNT1 A 0 A_TakeInventory("PPE_SWPortableMedikit",max(1,CountInv("PPE_PortableHealBuffer")-1)) //Take rest of inv
		Stop
	}
}

ACTOR PPE_PortableHealBuffer : Inventory
{
  Inventory.MaxAmount 100
}

ACTOR PPE_SWArmorBonusSpawner : PPE_Spawner replaces ArmorBonus
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteWithResult("SetSpawn",9)
		TNT1 A 0 A_JumpIf(CallAcs("GetSpawn",9) == 10,"Spawn2")
		TNT1 A 0 A_SpawnPermanent("PPE_SWArmorBonus")
		stop
	Spawn2:
		TNT1 A 0 ACS_NamedExecuteWithResult("RegisterHP",1)
		TNT1 A 0 A_SpawnPermanent("PPE_PowerupSpawner")
		stop
	}
}

ACTOR PPE_SWArmorBonus : ArmorBonus
{
    Inventory.PickupSound "items/pickup"
    Inventory.Icon "WARMA0"
    Armor.SaveAmount 1
    Armor.MaxSaveAmount 200
    States
    {
    Spawn:
		SBON B -1 bright
		Stop
    }
}

ACTOR PPE_SWArmor : GreenArmor replaces GreenArmor
{
	Inventory.PickupSound "items/gong"
	Inventory.Icon "WARMA0"
	Inventory.PickupMessage "$GOTARMOR1"
	//Armor.SavePercent 66.66
	Armor.SaveAmount 100
	States
    {
    Spawn:
		WARM A -1 bright
		Stop
    }
}

ACTOR PPE_SWKevlarArmor : BlueArmor replaces BlueArmor
{
	Inventory.PickupSound "items/gong"
	Inventory.Icon "WARMB0"
	Inventory.PickupMessage "$GOTARMOR2"
	//Armor.SavePercent 66.66
	Armor.SaveAmount 200
	States
    {
    Spawn:
		WARM B -1 bright
		Stop
    }
}
