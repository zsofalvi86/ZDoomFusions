Actor PPE_Shuriken: Pistol
{
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 2100
	Weapon.AmmoUse 3
	Weapon.AmmoType "PPE_ShurikenAmmo"
	Weapon.AmmoGive 30
	Inventory.PickupSound "weapons/swpu"
	Weapon.UpSound "weapons/shuriken/up"
	Weapon.ReadySound ""
	Inventory.Pickupmessage "$GOT_PPE_SHUR"
	Inventory.Icon "SHRAA0"
	Tag "$TAG_PPE_SHUR"
	States
	{
	Ready:
		TNT1 A 0 A_Jump(144,"ReadyLoop")
		TNT1 A 0 A_PlaySound("q/shurikenup",5)
	ReadyLoop:
		SWSH A 1 A_WeaponReady
		Loop
	Deselect:
		SWSH A 1 A_Lower(12)
		Loop
	Select:
		SWSH A 1 A_Raise(12)
		Loop
	Fire:
		TNT1 A 0 A_PlayWeaponSound("weapons/shuriken/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_ShurikenMissile",0,1,0,3,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_ShurikenMissile",-4,0,0,3,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_ShurikenMissile",4,0,0,3,1,0)
		SWSH BBB 2
		SWSH CC 2
    	TNT1 A 0 A_ReFire
		Goto ReadyLoop
  }
}

Actor PPE_ShurikenMissile : PPE_SlowMissileBase
{
    +FLATSPRITE
	+SKYEXPLODE
	var int user_missiletime;
	Damage (10)
	Speed 45
	SeeSound ""
	DeathSound ""
	BounceSound "weapons/shuriken/impact"
	BounceType "Doom"
	BounceCount 2
	BounceFactor 0.9
	WallBounceFactor 0.9
    Radius 6
    Height 4
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_missiletime",300)
	MissileLoop:
		TNT1 A 0 A_JumpIf(user_missiletime==0,"StopDeath")
		TNT1 A 0 A_SetUserVar("user_missiletime",user_missiletime-1)
		SHRM A 1
		Loop
    StopDeath:
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
    XDeath:
	Death.Sky:
        TNT1 A 0
		stop
	Crash:
	Death:
        TNT1 A 0 A_SpawnItem("PPE_ShurikenRecoveryBladeStuck")
		stop
	}
}

ACTOR PPE_ShurikenRecoveryBladeStuck : SwitchingDecoration
{
  +DONTGIB +FLATSPRITE +NOGRAVITY +USESPECIAL
  Activation THINGSPEC_Activate | THINGSPEC_ThingTargets
  Radius 6
  Height 4
  states
  {
  Spawn:
    SHRM A 1 
	TNT1 A 0 A_CheckBlock("Spawn",CBF_NOACTORS,AAPTR_DEFAULT,radius+1)
    goto Disappear
  Active:
	TNT1 A 0 A_PlaySound("items/pickup")
	TNT1 A 0 A_GiveToTarget("PPE_ShurikenAmmo", 1)
	stop
  Disappear:
	TNT1 A 0
	stop
  }
}
