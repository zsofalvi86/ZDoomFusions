Actor PPE_StickyBomb : RocketLauncher
{
	+WEAPON.NOAUTOAIM
	Weapon.SlotNumber 7
	Weapon.SelectionOrder 2700
	Weapon.AmmoUse 1
	Weapon.AmmoType "PPE_StickyBombs"
	Weapon.AmmoGive 30
	Inventory.PickupSound "weapons/swpu"
	Weapon.UpSound "weapons/sticky/up"
	Weapon.ReadySound ""
	Inventory.Pickupmessage "$GOT_PPE_SB"
	Inventory.Icon "SSBMA0"
	Tag "$TAG_PPE_SB"
	States
	{
	Ready:
		SWSB AAA 1 A_WeaponReady
		TNT1 A 0 A_PlayWeaponSound("weapons/sticky/spikes")
		SWSB BC 1 A_WeaponReady
	ReadyLoop:
		SWSB C 1 A_WeaponReady
		Loop
	Deselect:
		SWSB C 1 A_Lower(12)
		Loop
	Select:
		SWSB A 1 A_Raise(12)
		Loop
	Fire:
		TNT1 A 0 A_Jump(224,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_PlayWeaponSound("weapons/sticky/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_SWSticky",0,1,0,3,1,3)
		SWSB DDD 2
		SWSB DDDDDDDDBC 1
    	TNT1 A 0 A_ReFire
		Goto ReadyLoop
	}
}

actor PPE_SWSticky : PPE_SlowMissileBase
{
  var int user_countdown;
  var int user_x;
  var int user_y;
  var int user_z;
  SeeSound ""
  BounceSound ""
  DeathSound ""
  -USEBOUNCESTATE
  +HITTRACER
  -NOGRAVITY
  +BLOODLESSIMPACT
  -NOBLOCKMAP
  +SHOOTABLE
  +NOBLOOD
  Gravity 0.3
  Damage (5)
  Health 10
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_SetInvulnerable
  Spawnloop:
	SSBM A 1
	loop
  Death:
	TNT1 A 0 A_NoGravity
	TNT1 A 0 A_UnSetInvulnerable
	TNT1 A 0 A_SpawnItemEx("PPE_SWStickyStuck",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
	stop
  Crash:
	TNT1 A 0 A_PlaySound("weapons/proximity/impact",1)
	Goto Stick
  XDeath:
	TNT1 A 0 A_PlaySound("weapons/proximity/hit",1)
  Stick:
	TNT1 A 0 A_UnSetInvulnerable
    TNT1 A 0 A_SetUserVar("user_countdown",120)
	//TNT1 A 0 A_SetUserVar("user_x",Random(6,32))
	TNT1 A 0 A_SetUserVar("user_x",Random(-50,50))
	TNT1 A 0 A_SetUserVar("user_y",Random(-15,15))
	TNT1 A 0 A_SetUserVar("user_z",Random(25,90))
	TNT1 A 0 A_GiveToTarget("PPE_StickyQuote",1)
	Goto StickLoop
  StickLoop:
	TNT1 A 0 A_JumpIf(user_countdown==0,"StickEnd")
	SSBM A 1 A_Warp(AAPTR_TRACER,0,user_y,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,0,user_z*0.01,user_x*0.01)
	TNT1 A 0 A_SetUserVar("user_countdown",user_countdown-1)
	Goto StickLoop
  StickEnd:
	TNT1 A 0 A_PlaySound("weapons/sticky/beep",2)
  StickEnd2:
	TNT1 A 0 A_SetUserVar("user_countdown",18)
  DetLoop:
	TNT1 A 0 A_JumpIf(user_countdown==0,"DetEnd")
	SSBM A 1 A_Warp(AAPTR_TRACER,user_x,user_y,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,0,user_z*0.01,user_x*0.01)
	TNT1 A 0 A_SetUserVar("user_countdown",user_countdown-1)
	Goto DetLoop
  DetEnd:
	TNT1 A 0 A_NoGravity
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
	TNT1 A 0 A_PlaySound("weapons/sticky/explode",1) // 28, 67
	TNT1 A 0 A_Explode(192,192)
	TNT1 A 0 A_Quake(4,5,0,320,"")
	TNT1 A 0 A_Quake(2,6,0,720,"")
	TNT1 A 0 A_Quake(1,6,0,3256,"")
	BMXP A 4 bright
	BMXP BCDEF 3 bright
	Stop
  }
}

Actor PPE_SWStickyStuck : PPE_SWSticky
{
	+NOGRAVITY
	+DONTTHRUST
	-GRENADETRAIL
	-HITTRACER
	+HITOWNER
	Speed 0
	States
	{
	Spawn:
		SSBM A 115 A_PlaySound("weapons/sticky/beep")
		loop
	Crash:
	XDeath:
	Death:
		TNT1 A 0 A_Jump(256,"DetEnd")
		stop
	}
}