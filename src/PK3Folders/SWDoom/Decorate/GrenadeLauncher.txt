const int PPE_C_GRL_DAMAGE = 48;
const int PPE_C_GRL_EXP_DAMAGE = 192;
const int PPE_C_GRL_EXP_RADIUS = 192;
const int PPE_C_GRL_GAS_DAMAGE = 6;
const int PPE_C_GRL_GAS_DAMAGE_RADIUS = 168;
const int PPE_C_GRL_GAS_AREA_RADIUS = 48;
const int PPE_C_GRL_GAS_AREA_HEIGHT = 4;

Actor PPE_GrenadeLauncher : RocketLauncher
{
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_GL"
	Tag "$TAG_PPE_GL"
	Weapon.SelectionOrder 2100
	Weapon.SlotNumber 6
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_GrenadeShells"
	Weapon.UpSound "weapons/boltup"
	Inventory.PickupSound "q/bigweapon"
	Weapon.KickBack 160
	States
	{
	Ready:
		SWGL A 1 A_WeaponReady
		Loop
  	Deselect:
    	SWGL A 1 A_Lower(12)
    	Loop
  	Select:
    	SWGL A 1 A_Raise(12)
    	Loop
  	Fire:
		TNT1 A 0 A_Jump(216,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/grenade/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_SWGrenade",0,1,0,4,1,3)
		Goto FireAnim
	FireAnim:
		SWGF ABCDE 2 bright
		SWGL B 3 Offset(0,42)
		SWGL C 3 Offset(0,36)
		SWGL A 9 Offset(0,32)
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
		TNT1 A 0 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.7)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.4)
		Goto LightDone
  	Spawn:
		SWGP A -1
		Stop
	}
}

Actor PPE_SWGrenade : PPE_BouncyGrenadeBase
{
  SeeSound ""
  BounceSound "weapons/grenade/bounce"
  DeathSound "weapons/grenade/explode"
  BounceFactor 0.75
  WallBounceFactor 0.75
  BounceCount 3
  Gravity 0.2
  Speed 30
  Damage (PPE_C_GRL_DAMAGE)
  +FORCERADIUSDMG +USEBOUNCESTATE
  States
  {
  Spawn:
		DGRD A 1 A_SpawnItem("PPE_SmokeTrail")
		Loop
  Bounce:
		DGRR ABCDEFGHIJKL 2 A_SpawnItem("PPE_SmokeTrail")
		Loop
  Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_Explode(PPE_C_GRL_EXP_DAMAGE,PPE_C_GRL_EXP_RADIUS)
		TNT1 A 0 A_Quake(7,6,0,292,"")
		TNT1 A 0 A_Quake(4,7,0,804,"")
		TNT1 A 0 A_Quake(2,8,0,4096,"")
		TNT1 AAAAAA 0 A_SpawnItemEx("PPE_GLFireChunk",Random(-12,12),Random(-12,12),Random(12,42),FRandom(-8,8),FRandom(-8,8),FRandom(5,9),0,SXF_NOCHECKPOSITION,64)
		BMZP A 4 bright
		BMZP BCDEF 3 bright
		Stop
  }
}

Actor PPE_GLFireChunk
{
	Health 1
	Radius 8
	Height 1
	Gravity 1.1
	+CORPSE
	+NOTELEPORT
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0
		BAL1 AB 2 bright A_SpawnItem("PPE_GLFireChunkTrailTrail",0,0,0,1)
		loop
	Crash:
		TNT1 A 0 A_ScaleVelocity(0)
		BAL1 CDE 3 bright
		stop
	}
}

Actor PPE_GLFireChunkTrailTrail : PPE_TrailBase
{
	RenderStyle Translucent
	Alpha 0.6
	States
	{
	Spawn:
		TNT1 A 2
		FLAR BCDEFGH 2 bright
		Stop
	}
}

Actor PPE_GasCloudOld
{
	Radius 32
	Height 12
	var int user_timedlife;
	RenderStyle "Translucent"
	Alpha 0.37
	+MISSILE
	+NOGRAVITY
	+RIPPER
	+DONTSPLASH
	+BLOODLESSIMPACT
	BounceType Doom
	BounceFactor 0.6
	BounceCount 999
	PoisonDamage 5,5,5
	Damage (0)
	Speed 7
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_timedlife",Random(4,5))
	DecideFrame:
		TNT1 A 0 A_Jump(256,"A","B","C")
	A:
		GGAS A 0
		Goto DecideFacing
	B:
		GGBS A 0
		Goto DecideFacing
	C:
		GGCS A 0
		Goto DecideFacing
	DecideFacing:
		"####" A 0 A_Jump(256,"NormalLoop","MirrorLoop")
	NormalLoop:
		"####" A 0 A_SetUserVar("user_timedlife",user_timedlife-1)
		"####" A 0 A_ChangeVelocity(FRandom(-0.15,0.15),FRandom(-0.15,0.15),FRandom(-0.1,0.1))
		"####" ABCDE 3 A_Explode(PPE_C_GRL_GAS_DAMAGE,PPE_C_GRL_GAS_DAMAGE_RADIUS,1,0,1)
		"####" "#" 0 A_JumpIf(user_timedlife==0,"Disappear")
		Loop
	MirrorLoop:
		"####" A 0 A_SetUserVar("user_timedlife",user_timedlife-1)
		"####" A 0 A_ChangeVelocity(FRandom(-0.15,0.15),FRandom(-0.15,0.15),FRandom(-0.1,0.1))
		"####" FGHIJ 3 A_Explode(PPE_C_GRL_GAS_DAMAGE,PPE_C_GRL_GAS_DAMAGE_RADIUS,1,0,1)
		"####" "#" 0 A_JumpIf(user_timedlife==0,"Disappear")
		Loop
	Death:
	Disappear:
		"####" "#" 1 A_FadeOut(0.04)
		Loop
	}
}