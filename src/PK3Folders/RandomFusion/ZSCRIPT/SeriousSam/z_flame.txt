Class PPE_SSFlameMissile : PlasmaBall
{
	Override int DoSpecialDamage(Actor target, int damage, name damagetype)
	{
		target.GiveInventory("PPE_SSOnFire",5);
		let inv = target.FindInventory("PPE_SSOnFire");
		if (inv) { inv.target = self.target; }
		return Super.DoSpecialDamage(target,damage,damagetype);
	}
	
	States
	{
	Spawn:
		GHFB ABCD 1 bright;
		Loop;
	Death:
		BMYP ABCDEF 2 bright;
		Stop;
	}
	
	Default
	{
		DamageFunction 10;
		SeeSound "";
		DeathSound "weapons/sw/head/impact";
		RenderStyle "Normal";
		+BLOODLESSIMPACT;
	}
}

Class PPE_SSOnFire : Inventory
{
	Actor fireActor; int tic;
	override void DoEffect()
	{
		if (!fireActor)
		{
			fireActor = Spawn("PPE_SSFireActor",Vec3Offset(owner.pos.x,owner.pos.y + owner.radius*0.2,owner.pos.z + owner.height*0.2));
			fireActor.tracer = owner;
			//console.printf("%s", "spawnedFire");
		}
		
		if (tic <= 0) {tic += 3;}
		else {tic--;}
		if (tic <= 0)
		{
			if (target && owner)
			{
				if (owner.CountInv("PPE_SSOnFire") > 35)
				{
					DoMissileDamage(owner);
					owner.TakeInventory("PPE_SSOnFire",1);
				}
				if (owner.CountInv("PPE_SSOnFire") > 25)
				{
					DoMissileDamage(owner);
					owner.TakeInventory("PPE_SSOnFire",1);
				}
				if (owner.CountInv("PPE_SSOnFire") > 15)
				{
					DoMissileDamage(owner);
					owner.TakeInventory("PPE_SSOnFire",1);
				}
				if (owner.CountInv("PPE_SSOnFire") > 10)
				{
					DoMissileDamage(owner);
					owner.TakeInventory("PPE_SSOnFire",1);
				}
				if (owner.CountInv("PPE_SSOnFire") > 0)
				{
					DoMissileDamage(owner);
					owner.TakeInventory("PPE_SSOnFire",1);
				}
				else
				{
					if (fireActor) {fireActor.bShootable=true; fireActor.A_Die("Normal");}
				}
				//console.printf("%s%i", "fires:", owner.CountInv("PPE_OnFire"));
			}
		}
		super.DoEffect();
	}
	
	override void DetachFromOwner()
	{
		//console.printf("%s", "detached");
		if (owner.CountInv("PPE_SSOnFire") == 0 && fireActor) {fireActor.bShootable=true; fireActor.A_Die("Normal");}
		super.DetachFromOwner();
	}
	
	Default
	{
		DamageFunction 8;
		Inventory.MaxAmount 100;
		+BLOODLESSIMPACT;
	}
}

Class PPE_SSFireActor : Actor
{
	States
	{
	Spawn:
		GHFR QPO 2 bright A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,null,0.2,0.2);
	Loopy:
		GHFR ABCDEFGHIJKLMN 2 bright A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,null,0.2,0.2);
		TNT1 A 0 { if(!tracer) {return ResolveState("Death");} return ResolveState(null);}
		Loop;
	Death:
		GHFR OPQ 2 bright A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,null,0.2,0.2);
		Stop;
	}
	
	Default
	{
		+NOBLOCKMAP;
		+NOGRAVITY;
		+NOTELEPORT;
		+CANNOTPUSH;
		+NODAMAGETHRUST;
		+NOINTERACTION;
		+CLIENTSIDEONLY;
	}
}