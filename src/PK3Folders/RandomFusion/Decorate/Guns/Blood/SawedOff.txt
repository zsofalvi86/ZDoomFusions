Actor PPE_BloodSawedOffShotgun : PPE_BloodWeaponBase
{
	Tag "$TAG_BL_SO"
	Weapon.UpSound "weapons/blood/sawedoff/cock"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 3
	Weapon.SlotPriority 8
	Weapon.AmmoUse1 9
	Weapon.AmmoUse2 18
	Weapon.AmmoType1 "PPE_Shells"
	Weapon.AmmoType2 "PPE_Shells"
	Weapon.AmmoGive 80
	Decal "BulletChip"
	PPE_WeaponBase.SpawnerId1 0,2
	PPE_WeaponBase.SpawnerId2 1,1
	PPE_WeaponBase.SpawnerId3 1,3
	States
	{
	Ready.Ready:
    	BSGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
  	Deselect:
		BSGG A 1 A_Lower
    	Loop
  	Select:
		BSGG A 1 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireBullets(5.4,2.8,9,(10),"PPE_BloodBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/blood/sawedoff/fire",1,1)
		TNT1 A 0 A_CheckForReload(2,"Anim1")
		Goto Anim2
	Anim1:
		BSGF ADG 2 bright
		BSGG A 3
		TNT1 A 0 A_ReFire
		Goto Ready.Ready
	Anim2:
		BSGF BEH 2 bright
		Goto Reload
	AltFire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_CheckForReload(1,"ContAltFire",true)
		Goto Fire
	ContAltFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireBullets(7.9,3.9,18,(10),"PPE_BloodBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/blood/sawedoff/altfire",1,1)
		BSGF CFI 2 bright
		TNT1 A 0 A_ForceReload
		Goto Reload
	Reload:
		BSGR A 1
		BSGR A 1 Offset(0,32)
		BSGR A 1 Offset(0,32)
		BSGR B 1 Offset(0,32)
		BSGR B 1 Offset(0,32)
		BSGR B 1 Offset(0,32)
		TNT1 A 0 A_PlaySound("weapons/blood/sawedoff/cock",6,1)
		TNT1 A 0 A_CheckIfNoReload("ReloadFull")
		TNT1 A 0 A_CheckForReload(1,"ReloadOne",true)
		Goto ReloadTwo
	ReloadOne:
		BSGR C 1 Offset(0,32)
		BSGR C 1 Offset(0,32)
		BSGR C 1 Offset(0,32)
		TNT1 A 0 A_FireCustomMissile("PPE_BLSSGShellCasingShooterL",0,0,0,3)
//		TNT1 A 0 A_FireCustomMissile("PPE_BLSSGShellCasingShooterR",0,0,0,3)
		BSGR C 1 Offset(0,32)
		BSGR C 6
		TNT1 A 0 A_PlaySound("weapons/blood/sawedoff/load",7,1)
		TNT1 A 0 A_ResetReloadCounter
		BSGR C 2
		Goto ReloadEnd
	ReloadTwo:
		BSGR C 1 Offset(0,32)
		BSGR C 1 Offset(0,32)
		BSGR C 1 Offset(0,32)
		TNT1 A 0 A_FireCustomMissile("PPE_BLSSGShellCasingShooterL",0,0,0,3)
		TNT1 A 0 A_FireCustomMissile("PPE_BLSSGShellCasingShooterR",0,0,0,3)
		BSGR C 1 Offset(0,32)
		BSGR C 6
		TNT1 A 0 A_PlaySound("weapons/blood/sawedoff/load",7,1)
		TNT1 A 0 A_ResetReloadCounter
		BSGR C 2
		Goto ReloadEnd
	ReloadFull:
		BSGR C 1 Offset(0,32)
		BSGR C 1 Offset(0,32)
		BSGR C 1 Offset(0,32)
		BSGR C 1 Offset(0,32)
		BSGR C 6
	ReloadEnd:
		BSGR D 1
		BSGR D 1 Offset(0,32)
		BSGR D 1 Offset(0,32)
		BSGG A 1 Offset(0,32)
		BSGG A 2
		Goto Ready.Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAA 1 A_SetPitch(pitch-0.35)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch-0.35)
		TNT1 A 0 A_Light1
		TNT1 AAA 1 A_SetPitch(pitch+0.7)
		Goto LightDone
  	AltFlash:
    	TNT1 A 1 A_Light1
		TNT1 AAA 1 A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light1
		TNT1 AAA 1 A_SetPitch(pitch+1.2)
		Goto LightDone
  	Spawn:
    	BSGW A -1
    	Stop
	}
}

Actor PPE_BLSSGShellCasingShooterL : PPE_CasingShooterBase
{
	Speed 12
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BloodShellCasing",0,0,FRandom(65,40),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-14,-20))
		Stop
	}
}

Actor PPE_BLSSGShellCasingShooterR : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BloodShellCasing",0,0,FRandom(-70,-40),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-16,-22))
		Stop
	}
}