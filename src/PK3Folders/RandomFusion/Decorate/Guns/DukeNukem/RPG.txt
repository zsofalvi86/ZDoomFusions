Actor PPE_DNRPG : PPE_DNWeaponBase
{
	Weapon.SelectionOrder 2400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 5
	Weapon.SlotPriority 9
	Inventory.PickupSound "weapons/pickup/quote"
	Tag "$TAG_DN_RPG"
	PPE_WeaponBase.SpawnerId1 3,1
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready.Ready:
		RPGG A 1 A_WeaponReady
		Loop
	Deselect:
		RPGG A 1 A_Lower(12)
		Loop
	Select:
		RPGG A 1 A_Raise(12)
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_DukeRocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/dn/rpg/fire",1)
		RPGF ABCDE 2 bright
		RPGG A 14
		TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.3)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.6)
		Goto LightDone
	Spawn:
		RPGW A -1
		Stop
   }
}

actor PPE_DukeRocket : PPE_SlowMissileBase
{
	Speed 27
	Damage (90)
	DamageType Explosive
	SeeSound ""
	DeathSound "weapons/dn/explosion"
	Obituary "$OB_MPROCKET"
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("PPE_DnRocketTrail")
		MISL A 1 bright 
		Loop
	Death:
		TNT1 A 0 A_Explode(128,128)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		TNT1 A 0 A_CheckFloor("Floor")
		BMWP AB 3 bright
		BMWP CDEFG 3 bright
		Stop
	Floor:
		BMFP AB 3 bright
		BMFP CDEFG 3 bright
		Stop
	}
}

Actor PPE_DnRocketTrail : PPE_TrailBase
{
	RenderStyle "Translucent"
	Alpha 0.45
	States
	{
  	Spawn:
		TNT1 A 2
    	WTRL ABCDEF 5
    	Stop
	}
}
