Actor PPE_DNLeadCannon : PPE_DNWeaponBase
{
	Tag "$TAG_DN_LC"
	Inventory.PickupSound "weapons/pickup/quote"
	Weapon.UpSound "weapons/dn/shotgun/cock"
	Weapon.SelectionOrder 600
	Weapon.SlotNumber 3
	Weapon.SlotPriority 5
	Weapon.AmmoUse 4
	Weapon.AmmoType "PPE_Shells"
	PPE_WeaponBase.AmmoTypeAlt "PPE_Rockets"
	Weapon.AmmoGive 8
	Decal "BulletChip"
	PPE_WeaponBase.SpawnerId1 0,2
	PPE_WeaponBase.SpawnerId2 1,3
	States
	{
	Ready.Ready:
		DLCG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
	Deselect:
		TNT1 A 0 A_SwapIfOnlyOtherAmmo
	DeseLoop:
    	DLCG A 1 A_Lower(12)
    	Loop
  	Select:
    	DLCG A 1 A_Raise(12)
    	Loop
  	Fire:
    	DLCG A 1
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/dn/shotgun/fire")
		//TNT1 A 0 A_FireCustomMissile("PPE_NukemShotgunPellet",0,1,0,4,1,0)
		//TNT1 AAAAAAAA 0 A_FireCustomMissile("PPE_NukemShotgunPellet",FRandom(-2.3,2.3),0,0,4,1,FRandom(-1.1,1.1))
		TNT1 A 0 A_JumpIfAltAmmo("FireAlt")
		TNT1 A 0 A_FireBullets(6.5,3.25,32,(10),"PPE_DNBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		Goto FireEnd
	FireAlt:
		TNT1 A 0 A_FireBullets(8.0,4.5,14,(20),"PPE_DNExplosiveLeadPelletPuff",FBF_USEAMMO|FBF_NORANDOM)
	FireEnd:
    	DLCF ABC 2 bright
	Pump:
		DLCG A 2 offset(0,38)
		DLCG A 2 offset(0,40)
		DLCG A 2 offset(0,38)
		DLCG A 2 offset(0,36)
		DLCG A 2 offset(0,34)
		DLCG A 4 offset(0,32)
		//DLCG A 2 offset(0,32)
		TNT1 A 0 A_PlaySound("weapons/dn/shotgun/cock",6)
		TNT1 A 0 A_FireCustomMissile("PPE_DNLCShellCasingShooter",0,0,0,6)
		TNT1 A 0 A_FireCustomMissile("PPE_DNLCShellCasingShooter",0,0,0,6)
		TNT1 A 0 A_FireCustomMissile("PPE_DNLCShellCasingShooter",0,0,0,6)
		TNT1 A 0 A_FireCustomMissile("PPE_DNLCShellCasingShooter",0,0,0,6)
		DLCG B 1 offset(0,32)
		DLCG B 1 offset(3,28)
		DLCG B 1 offset(6,24)
		DLCG C 1 offset(0,32)
		DLCG C 1 offset(2,29)
		DLCG C 1 offset(4,26)
		DLCG D 1 offset(0,32)
		DLCG D 1 offset(1,31)
		DLCG D 1 offset(2,30)
		DLCG E 1 offset(0,32)
	PumpEnd:
		DLCG E 1 offset(1,34)
		DLCG E 1 offset(0,32)
		DLCG D 1 offset(2,30)
		DLCG D 1 offset(1,31)
		DLCG D 1 offset(0,32)
		DLCG C 1 offset(4,26)
		DLCG C 1 offset(2,29)
		DLCG C 1 offset(0,32)
		DLCG B 1 offset(6,24)
		DLCG B 1 offset(3,28)
		DLCG B 1 offset(0,32)
		DLCG A 6
		TNT1 A 0 A_JumpIfOnlyOtherAmmo("Reload")
    	TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	AltFire:
	Reload:
		TNT1 A 0 A_PlaySound("weapons/dn/shotgun/cock",6)
		DLCG B 1 offset(0,32)
		DLCG B 1 offset(3,28)
		DLCG B 1 offset(6,24)
		DLCG C 1 offset(0,32)
		DLCG C 1 offset(2,29)
		DLCG C 1 offset(4,26)
		DLCG D 1 offset(0,32)
		DLCG D 1 offset(1,31)
		DLCG D 1 offset(2,30)
		DLCG E 1 offset(0,32)
		TNT1 A 0 A_SwapAmmo
		Goto PumpEnd
  	Flash:
    	TNT1 A 2 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.9)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.8)
		Goto LightDone
  	Spawn:
    	DLCW A -1
    	Stop
	}
}

Actor PPE_DNExplosiveLeadPelletPuff : BulletPuff
{
	+ALWAYSPUFF
	+PUFFONACTORS
	-ALLOWPARTICLES
	VSpeed 0
	RenderStyle Normal
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/dn/smexplosion")
		TNT1 A 0 A_Explode(5,136)
		TNT1 A 0 A_Explode(35,136,0)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BMYP A 4 bright
		BMYP BCDEF 3 bright
		Stop
  }
}

Actor PPE_DNLCShellCasingShooter : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_ShellCasing",0,0,FRandom(60,85),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-11,-20))
		Stop
	}
}