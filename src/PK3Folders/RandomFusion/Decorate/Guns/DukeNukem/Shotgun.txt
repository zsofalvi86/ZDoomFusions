Actor PPE_DNShotgun : PPE_DNWeaponBase
{
	Tag "$TAG_DN_SG"
	Weapon.UpSound "weapons/dn/shotgun/cock"
	Weapon.SelectionOrder 600
	Weapon.SlotNumber 3
	Weapon.SlotPriority 5
	Weapon.AmmoUse 8
	Weapon.AmmoType "PPE_Shells"
	PPE_WeaponBase.AmmoTypeAlt "PPE_Rockets"
	Weapon.AmmoGive 80
	Decal "BulletChip"
	PPE_WeaponBase.SlotAlt1 "PPE_DNShotgunS2"
	PPE_WeaponBase.SpawnerId1 0,1
	PPE_WeaponBase.SpawnerId2 15,1
	States
	{
	Ready.Ready:
		PGSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
	Deselect:
		TNT1 A 0 A_SwapIfOnlyOtherAmmo
	DeseLoop:
    	PGSG A 1 A_Lower(12)
    	Loop
  	Select:
    	PGSG A 1 A_Raise(12)
    	Loop
  	Fire:
    	PGSG A 1
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/dn/shotgun/fire")
		//TNT1 A 0 A_FireCustomMissile("PPE_NukemShotgunPellet",0,1,0,4,1,0)
		//TNT1 AAAAAAAA 0 A_FireCustomMissile("PPE_NukemShotgunPellet",FRandom(-2.3,2.3),0,0,4,1,FRandom(-1.1,1.1))
		TNT1 A 0 A_JumpIfAltAmmo("FireAlt")
		TNT1 A 0 A_FireBullets(3.0,2.0,7,(10),"PPE_DNBulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		Goto FireEnd
	FireAlt:
		TNT1 A 0 A_FireBullets(1.0,1.0,1,(70),"PPE_DNExplosivePelletPuff",FBF_USEAMMO|FBF_NORANDOM)
	FireEnd:
    	PGSF ABC 2 bright
	Pump:
		PGSG A 2 offset(0,38)
		PGSG A 2 offset(0,40)
		PGSG A 2 offset(0,38)
		PGSG A 2 offset(0,36)
		//PGSG A 2 offset(0,32)
		TNT1 A 0 A_PlaySound("weapons/dn/shotgun/cock",6)
		TNT1 A 0 A_FireCustomMissile("PPE_DNSGShellCasingShooter",0,0,0,6)
		PGSG B 1 offset(0,32)
		PGSG B 1 offset(3,28)
		PGSG B 1 offset(6,24)
		PGSG C 1 offset(0,32)
		PGSG C 1 offset(2,29)
		PGSG C 1 offset(4,26)
		PGSG D 1 offset(0,32)
		PGSG D 1 offset(1,31)
		PGSG D 1 offset(2,30)
		PGSG E 1 offset(0,32)
	PumpEnd:
		PGSG E 1 offset(1,34)
		PGSG E 1 offset(0,32)
		PGSG D 1 offset(2,30)
		PGSG D 1 offset(1,31)
		PGSG D 1 offset(0,32)
		PGSG C 1 offset(4,26)
		PGSG C 1 offset(2,29)
		PGSG C 1 offset(0,32)
		PGSG B 1 offset(6,24)
		PGSG B 1 offset(3,28)
		PGSG B 1 offset(0,32)
		PGSG A 1
		TNT1 A 0 A_JumpIfOnlyOtherAmmo("Reload")
    	TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	AltFire:
	Reload:
		TNT1 A 0 A_PlaySound("weapons/dn/shotgun/cock",6)
		PGSG B 1 offset(0,32)
		PGSG B 1 offset(3,28)
		PGSG B 1 offset(6,24)
		PGSG C 1 offset(0,32)
		PGSG C 1 offset(2,29)
		PGSG C 1 offset(4,26)
		PGSG D 1 offset(0,32)
		PGSG D 1 offset(1,31)
		PGSG D 1 offset(2,30)
		PGSG E 1 offset(0,32)
		TNT1 A 0 A_SwapAmmo
		Goto PumpEnd
  	Flash:
    	TNT1 A 2 A_Light1
		TNT1 AAAAA 1 A_SetPitch(pitch-0.3)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.6)
		Goto LightDone
  	Spawn:
    	PGSW A -1
    	Stop
	}
}

Actor PPE_DNShotgunS2 : PPE_DNShotgun { Weapon.SlotNumber 2 PPE_WeaponBase.SlotAlt1 "PPE_DNShotgun" }

Actor PPE_DNExplosivePelletPuff : BulletPuff
{
	+ALWAYSPUFF
	+PUFFONACTORS
	VSpeed 0
	RenderStyle Normal
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/dn/explosion")
		TNT1 A 0 A_Explode(128,192)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
  }
}

Actor PPE_DNSGShellCasingShooter : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_ShellCasing",0,0,FRandom(65,80),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-12,-19))
		Stop
	}
}