Actor PPE_Q1Thunderbolt : PPE_Quake1WeaponBase
{
	Tag "$TAG_Q1_TB"
	Decal LightningScorch
	Weapon.AmmoUse 2
	Weapon.AmmoType "PPE_Cells"
	Weapon.AmmoGive 40
	Weapon.SlotNumber 8
	Weapon.SlotPriority 9
	Weapon.KickBack 0
	DamageType "Quake1Cell"
	PPE_WeaponBase.SlotAlt1 "PPE_Q1ThunderboltS0"
	PPE_WeaponBase.SpawnerId1 4,2
	States
	{
	Ready.Ready:
		TNT1 A 0 A_JumpIfMode(1,"Ready.M2")
	Ready.M1:
		TBLG AAAAAAAAAAABBCCDDCCBBAAAAAAA 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
    	Loop
	Ready.M2:
		TBLP A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
    	Loop
  	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
		TBLG A 1 A_Lower(18)
    	Loop
  	Select:
		TBLG A 1 A_Raise(18)
    	Loop
	Fire:
		TNT1 A 0 A_JumpIfMode(1,"Fire.M2")
	Fire.M1:
		TNT1 A 0 A_PlaySound("weapons/quake1/lightning/start",5)
	Anim1:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters // 30
		TNT1 A 0 //A_RailAttack((25),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"none",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_FireBullets(0,0,1,(42),"PPE_ThunderboltPuff",FBF_USEAMMO|FBF_NORANDOM,968,"none",0)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_Jump(256,"AnimA1","AnimB1","AnimC1")
	Anim2:
		TNT1 A 0 A_PlaySound("weapons/quake1/lightning/loop",6,1.0,1)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 //A_RailAttack((25),0,1,"none","none",RGF_SILENT|RGF_NOPIERCING,0,"none",0,0,968,0,12,0,"PPE_LightningTrail",1)
		TNT1 A 0 A_FireBullets(0,0,1,(42),"PPE_ThunderboltPuff",FBF_USEAMMO|FBF_NORANDOM,968,"none",0)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_Jump(256,"AnimA2","AnimB2","AnimC2")
	AnimA1:
		TBLF ABC 1 bright A_ThunderboltBeam("PPE_Q1LightningBeam",968)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimB1:
		TBLF BCA 1 bright A_ThunderboltBeam("PPE_Q1LightningBeam",968)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimC1:
		TBLF CAB 1 bright A_ThunderboltBeam("PPE_Q1LightningBeam",968)
		TNT1 A 0 A_ReFire("Anim2")
		Goto FireEnd
	AnimA2:
		TBLF AABB 1 bright A_ThunderboltBeam("PPE_Q1LightningBeam",968)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	AnimB2:
		TBLF BBCC 1 bright A_ThunderboltBeam("PPE_Q1LightningBeam",968)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	AnimC2:
		TBLF CCAA 1 bright A_ThunderboltBeam("PPE_Q1LightningBeam",968)
		TNT1 A 0 A_ReFire("Anim1")
		Goto FireEnd
	FireEnd:
		TNT1 A 0 A_StopSound(6)
		Goto Ready.Ready
	Fire.M2:
		TNT1 A 0 A_PlayWeaponSound("weapons/quake1/plasma/fire")
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TBLQ ABCBA 2 bright
		TNT1 A 0 A_FireCustomMissile("PPE_Q1LightningBall",0,1,0,4,1,0)
		TNT1 A 0 A_GunFlash("FlashPlasma")
		TBLQ DEFG 2 bright
		TBLP A 7
		TNT1 A 0 A_ReFire("Fire")
		Goto Ready.Ready
	Reload:
		TNT1 A 0 A_JumpIfMode(1,"BackToThunder")
	User1:
		TNT1 A 0 A_PlaySound("weapons/quake1/plasma/on",5)
		TBLP AAAAAAA 1
		TNT1 A 0 A_SwapMode(1)
		TNT1 A 0 A_ChangeAmmoUse(10)
		Goto Ready.Ready
	User2:
	BackToThunder:
		TNT1 A 0 A_PlaySound("weapons/quake1/plasma/off",5)
		TBLG AAAAAAA 1
		TNT1 A 0 A_SwapMode(0)
		TNT1 A 0 A_ChangeAmmoUse(2)
		Goto Ready.Ready
	Flash:
		TNT1 A 0
		//TNT1 A 0 A_SetPitch(pitch-0.5)
		TNT1 A 2 A_Light(Random(1,3))
		//TNT1 A 0 A_SetPitch(pitch+0.5)
		TNT1 A 2 A_Light(Random(1,3))
		Goto LightDone
	FlashPlasma:
		TNT1 A 1
		TNT1 AAAA 1 A_SetPitch(pitch-0.9)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+1.8)
		Goto LightDone
	Spawn:
    	TBLW AAAAAABBCCDDCCBB 1
		Loop
	}
}

Actor PPE_Q1ThunderboltS0 : PPE_Q1Thunderbolt { Weapon.SlotNumber 0 Weapon.SlotPriority 4 PPE_WeaponBase.SlotAlt1 "PPE_Q1Thunderbolt" }

Actor PPE_Q1LightningBall : PPE_Q1Zapper
{
	Speed 25
	Damage (0)
	RenderStyle Normal
	DeathSound "weapons/quake1/plasma/hit"
	ActiveSound "weapons/quake1/lightning/loop"
	+FORCERADIUSDMG
	DamageType "Quake1Cell"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/quake1/plasma/fly",1,0.9,1)
		LBAL ABCD 3 Bright
		Loop
	Death:
		TNT1 A 0 // 40, 80
		TNT1 A 0 A_Explode(40,128,1,0,128)
		TNT1 A 0 A_Explode(80,128)
		TNT1 A 0 A_Quake(7,6,0,320,"")
		TNT1 A 0 A_Quake(4,8,0,640,"")
		TNT1 A 0 A_Quake(2,9,0,4096,"")
		TNT1 A 0 A_SpawnItem("PPE_LightBlueQuakeExplo")
		TNT1 A 0 A_ToggleZap
		PLSE ABCDE 2 Bright A_Explode(30,192,0,1,192)
		Stop
  }
}

Actor PPE_Q1LightningTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("PPE_LightningTrailParticle",0,FRandom(-4,4),FRandom(-4,4))
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_Q1LightningBeam : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"A","B","C","D")
		A:
    	TBLB A 1 bright
		Stop
		B:
    	TBLB B 1 bright
    	Stop
		C:
    	TBLB C 1 bright
    	Stop
		D:
    	TBLB D 1 bright
    	Stop
	}
}

Actor PPE_Q1PlasmaBeam : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"A","B","C","D")
		A:
    	TBLB A 2 bright
		Stop
		B:
    	TBLB B 2 bright
    	Stop
		C:
    	TBLB C 2 bright
    	Stop
		D:
    	TBLB D 2 bright
    	Stop
	}
}

Actor PPE_ThunderboltPuff : BulletPuff
{
	-ALLOWPARTICLES
	+PUFFONACTORS
	+ALWAYSPUFF
	+BLOODLESSIMPACT
	RenderStyle Normal
	Vspeed 0
	States
	{
	Crash:
		TNT1 A 0
		Stop
	Spawn:
	Melee:
		BLUD ABC 2
		Stop
	}
}
