actor PPE_Q1GrenadeLauncher : PPE_Quake1WeaponBase
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 5
	Weapon.SlotPriority 6
	Tag "$TAG_Q1_GL"
	+NOAUTOFIRE
	+NOAUTOAIM
	PPE_WeaponBase.SpawnerId1 3,1
	States
	{
	Ready.Ready:
		TNT1 A 0 A_JumpIfMode(1,"Ready.M2")
	Ready.M1:
		QGLG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
		Loop
	Ready.M2:
		QMGG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER2)
		Loop
	Deselect:
		QGLG A 1 A_Lower(18)
		Loop
	Select:
		QGLG A 1 A_Raise(18)
		Loop
	Fire:
	Fire.M1:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_QGrenade",0,1,0,0,1,8)
		TNT1 A 0 A_PlaySound("weapons/quake1/grenade/fire",1)    	
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QGLF ABCD 2 bright
		QGLG A 13
		TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	Fire.M2:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_QGrenadeMulti",0,1,0,0,1,8)
		TNT1 A 0 A_PlaySound("weapons/quake1/grenade/fire",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QMGF ABCD 2 bright
		QMGG A 13
		TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	Reload:
		TNT1 A 0 A_JumpIfMode(1,"BackToNormal")
	User1:
		TNT1 A 0 A_PlaySound("weapons/quake1/multirocket/on",5)
		QGLG AAAAAAA 1
		TNT1 A 0 A_SwapMode(1)
		TNT1 A 0 A_ChangeAmmoUse(2)
		Goto Ready.Ready
	User2:
	BackToNormal:
		TNT1 A 0 A_PlaySound("weapons/quake1/multirocket/off",5)
		QMGG AAAAAAA 1
		TNT1 A 0 A_SwapMode(0)
		TNT1 A 0 A_ChangeAmmoUse(1)
		Goto Ready.Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.5)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.0)
		Goto LightDone
	Spawn:
		QGLW A -1
		Stop
   }
}

actor PPE_QGrenade : Rocket
{
	+FORCERADIUSDMG
	+USEBOUNCESTATE
	-ROCKETTRAIL
	+GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
	SeeSound ""
	BounceSound "weapons/quake1/grenade/bounce"
	DeathSound "weapons/quake1/explode"
	BounceFactor 0.6
	WallBounceFactor 0.6
	Gravity 0.4
	BounceCount 4
	Speed 25
	Damage (96)
	DamageType "Quake1Rocket"
	States
	{
	Spawn:
		DGRD A 1
		Loop
	Bounce:
		DGRR ABCDEFGHIJKL 2
		Loop
	Death:
		TNT1 A 0 A_NoGravity // 40, 80
		TNT1 A 0 A_Explode(40,128,1,0,128,0,0,"None","Normal")
		TNT1 A 0 A_Explode(80,128,1,0,0,0,0,"None","Normal")
		TNT1 A 0 A_Quake(4,5,0,256,"")
		TNT1 A 0 A_Quake(2,6,0,720,"")
		TNT1 A 0 A_Quake(1,6,0,4096,"")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor PPE_QGrenadeMulti : PPE_QGrenade
{
  var int user_angle;
  SeeSound ""
  BounceSound "weapons/quake1/grenade/bounce"
  DeathSound ""
  BounceFactor 0.6
  WallBounceFactor 0.6
  Gravity 0.4
  BounceCount 4
  Speed 25
  Damage (96)
  DamageType "Quake1Sp"
  States
  {
  Crash:
  Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_SetUserVar("user_angle",5)
	SpawnLoop:
		TNT1 A 0 A_CustomMissile("PPE_QGrenadeMultiFrag",0,0,72*user_angle,CMF_TRACKOWNER|CMF_AIMDIRECTION,40)
		TNT1 A 0 A_JumpIf(user_angle==0,"SpawnEnd")
		TNT1 A 0 A_SetUserVar("user_angle",user_angle-1)
		Goto SpawnLoop
	SpawnEnd:
		TNT1 A 0
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("weapons/quake1/explode",4)
		Goto Super::Death
  }
}

actor PPE_QGrenadeMultiFrag : PPE_QGrenade
{
  SeeSound ""
  BounceSound "weapons/quake1/grenade/bounce"
  DeathSound "weapons/quake1/explode"
  BounceFactor 0.6
  WallBounceFactor 0.6
  Gravity 0.4
  BounceCount 4
  Speed 8
  Damage (0)
  BounceType "None"
  Gravity 0.85
  DamageType "Quake1Sp"
  States
  {
  Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_Explode(20,128,1,0,128,0,0,"None","Normal")
		TNT1 A 0 A_Explode(40,128,1,0,0,0,0,"None","Normal")
		TNT1 A 0 A_Quake(3,3,0,208,"")
		TNT1 A 0 A_Quake(2,4,0,480,"")
		TNT1 A 0 A_Quake(1,4,0,2048,"")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExploSmall")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
  }
}