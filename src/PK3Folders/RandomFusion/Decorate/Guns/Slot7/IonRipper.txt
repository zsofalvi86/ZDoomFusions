Actor PPE_Q2IonRipper : PlasmaRifle
{
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_RIP"
	Tag "$TAG_PPE_RIP"
	Weapon.SelectionOrder 105
	Weapon.SlotNumber 7
	Weapon.AmmoUse 2
	Weapon.AmmoType "PPE_Cells"
	Weapon.AmmoGive 40
	Inventory.PickupSound "items/q2weapon"
	States
	{
	Ready:
		2IRG A 1 A_WeaponReady
		Loop
	Deselect:
		2IRG A 1 A_Lower
		Loop
	Select:
		2IRG A 1 A_Raise
		Loop
  	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("weapons/ionripper/fire",1,1-(CountInv("PowerSilencer")*0.7))
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireCustomMissile("PPE_IonRipperBullet",0,1,0,3,1,0)
		2IRG ABC 2 bright
		2IRG A 7
    	TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAA 1 A_SetPitch(pitch-0.3)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.3)
		Goto LightDone
  	Spawn:
		2IRW A -1
		Stop
	}
}

Actor PPE_IonRipperBullet : PPE_SlowMissileBase
{
    +FLATSPRITE
	var int user_missiletime;
	Damage (62) //50
	Speed 36
	SeeSound ""
	DeathSound "weapons/laser/hit"
	BounceType "Doom"
	BounceCount 5
	BounceFactor 1.0
	WallBounceFactor 1.0
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/laser/fly",6,1.0,1)
		TNT1 A 0 A_SetUserVar("user_missiletime",320)
	MissileLoop:
		TNT1 A 0 A_JumpIf(user_missiletime==0,"StopDeath")
		TNT1 A 0 A_SetUserVar("user_missiletime",user_missiletime-1)
        TNT1 AA 0 A_SpawnParticle(orange,SPF_FULLBRIGHT|SPF_RELATIVE,20,7,180,7,-4,0,FRandom(0,1),FRandom(-0.1,0.1),FRandom(-0.02,0.02))
		TNT1 AA 0 A_SpawnParticle(orange,SPF_FULLBRIGHT|SPF_RELATIVE,20,7,180,7,4,0,FRandom(0,1),FRandom(-0.1,0.1),FRandom(-0.02,0.02))
		GPSX E 1 bright
		Loop
    StopDeath:
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
	Crash:
	XDeath:
	Death:
		TNT1 A 0 A_StopSound(6)
		GPSX ABCDE 3 bright
		stop
	}
}