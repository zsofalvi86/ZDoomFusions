Actor PPE_Q3MachineGun : PPE_Quake3WeaponBase
{
	+WEAPON.NOAUTOAIM
	Tag "$TAG_Q3_MG"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 4
	Weapon.SlotPriority 4
	Weapon.AmmoType "PPE_Bullets"
	Weapon.AmmoUse 8
	Weapon.AmmoGive 200
	Decal "BulletChip"
	PPE_WeaponBase.SlotAlt1 "PPE_Q3MachineGunS2"
	PPE_WeaponBase.SpawnerId1 2,1
	PPE_WeaponBase.SpawnerId2 2,3
	PPE_WeaponBase.SpawnerId3 14,1
	PPE_WeaponBase.SpawnerId4 16,2
	States
	{
	Spawn:
		3MGW A -1
		Stop
	Ready.Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_MachineGunZoom",1,"ReadyZoom")
		3MGC A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	ReadyZoom:
		TNT1 A 0 A_StopSound(6)
	ReadyZoomLoop:
		TNT1 A 1 bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_MachineGunZoom",0)
	DeseLoop:
		3MGC A 1 A_Lower(18)
		Loop
	Select:
		3MGC A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_JumpIfinventory("PPE_MachineGunZoom",1,"FireZoom")
		TNT1 A 0 A_CheckForReload(2,"Fire2")
		Goto Fire1
	Fire2:
		3MGF D 1 bright
	Fire1:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/machinegun/fire")
	    TNT1 A 0 A_FireBullets(2.1,2.1,-1,(8),"PPE_Quake3BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3MGBulletCasingShooter",0,0,0,4)
		3MGF ABC 1 bright
		TNT1 A 0 A_Refire("Fire")
		Goto Spindown
	Spindown:
		3MGF E 1 bright A_WeaponReady(WRF_NOSWITCH)
		3MGF FF 1 bright A_WeaponReady(WRF_NOSWITCH)
		3MGF GGG 1 bright A_WeaponReady(WRF_NOSWITCH)
		Goto Ready.Ready
	FireZoom: //tap fire
		TNT1 A 0 A_GunFlash("ZoomFlash")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/machinegun/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_Q3MGBulletCasingShooter",0,0,0,4)
	    TNT1 A 0 A_FireBullets(0,0,1,(12),"PPE_Quake3BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 AAAA 1
		TNT1 AAA 1 A_Refire("FireZoomAuto")
		Goto SpindownZoom
	FireZoomAuto:
		TNT1 A 0 A_GunFlash("ZoomFlash")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/machinegun/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_Q3MGBulletCasingShooter",0,0,0,4)
		TNT1 A 0 A_FireBullets(1.2,1.2,-1,(8),"PPE_Quake3BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 AAA 1
		TNT1 A 0 A_CheckForReload(2,"FireZoom2")
		TNT1 A 1
	FireZoom2:
		TNT1 A 0 A_Refire("FireZoomAuto")
		Goto SpindownZoom
	SpindownZoom:
		TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
		TNT1 AA 1 A_WeaponReady(WRF_NOSWITCH)
		TNT1 AAAA 1 A_WeaponReady(WRF_NOSWITCH)
		Goto Ready.Ready
	Zoom:
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_MachineGunZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_MachineGunZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyZoom
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyZoom
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyZoom
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyZoom
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_MachineGunZoom",0)
		SSSS Z 5 bright
		3MGC A 5
		Goto Ready.Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_SetPitch(pitch-0.25)
		TNT1 A 1 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.25)
		Goto LightDone
	ZoomFlash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.2)
		Goto LightDone
	}
}

Actor PPE_Q3MachineGunS2 : PPE_Q3MachineGun 
{ 
	Weapon.SlotNumber 2 Weapon.SlotPriority 7 
	PPE_WeaponBase.SlotAlt1 "PPE_Q3MachineGun" 
	Weapon.AmmoUse 5
	States
	{
	Fire:
		TNT1 A 0 A_JumpIfinventory("PPE_MachineGunZoom",1,"FireZoom")
		TNT1 A 0 A_CheckForReload(2,"Fire2")
		Goto Fire1
	Fire2:
		3MGF D 1 bright
	Fire1:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/machinegun/fire")
	    TNT1 A 0 A_FireBullets(2.1,2.1,-1,(5),"PPE_Quake3BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3MGBulletCasingShooter",0,0,0,4)
		3MGF ABC 1 bright
		TNT1 A 0 A_Refire("Fire")
		Goto Spindown
	FireZoom: //tap fire
		TNT1 A 0 A_GunFlash("ZoomFlash")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/machinegun/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_Q3MGBulletCasingShooter",0,0,0,4)
	    TNT1 A 0 A_FireBullets(0,0,1,(7),"PPE_Quake3BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 AAAA 1
		TNT1 AAA 1 A_Refire("FireZoomAuto")
		Goto SpindownZoom
	FireZoomAuto:
		TNT1 A 0 A_GunFlash("ZoomFlash")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/machinegun/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_Q3MGBulletCasingShooter",0,0,0,4)
		TNT1 A 0 A_FireBullets(1.2,1.2,-1,(5),"PPE_Quake3BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 AAA 1
		TNT1 A 0 A_CheckForReload(2,"FireZoom2")
		TNT1 A 1
	}
}

Actor PPE_Q3MGBulletCasingShooter : PPE_CasingShooterBase
{
	Speed 8
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BulletCasing",0,0,FRandom(-60,-75),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-13,-22))
		Stop
	}
}

Actor PPE_MachineGunZoom : Inventory
{
	Inventory.MaxAmount 1
}