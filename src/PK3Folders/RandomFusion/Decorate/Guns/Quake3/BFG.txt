Actor PPE_Q3Bfg : PPE_Quake3WeaponBase
{
	+WEAPON.NOAUTOAIM
	-WEAPON.NOAUTOFIRE
	Tag "$TAG_Q3_BFG"
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 0
	Weapon.SlotPriority 8
	Weapon.AmmoType "PPE_Cells"
	Weapon.AmmoUse 10
	Weapon.AmmoGive 40
	PPE_WeaponBase.SlotAlt1 "PPE_Q3BfgS9"
	PPE_WeaponBase.SpawnerId1 5,1
	States
	{
	Spawn:
		3BFG W -1
		Stop
	Ready.Ready:
		3BFG AABBCCDD 1 A_WeaponReady
		Loop
	Deselect:
		3BFG A 1 A_Lower(18)
		Loop
	Select:
		3BFG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/quake3/bfg/fire",1)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3BfgBall",0,1,0,1,1,0)
		3BFF ABCD 1 bright
		3BFG EFG 1
		TNT1 A 0 A_ReFire
		Goto Ready.Ready
	Flash:
		TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor PPE_Q3BfgS9 : PPE_Q3Bfg { Weapon.SlotNumber 9 Weapon.SlotPriority 9 PPE_WeaponBase.SlotAlt1 "PPE_Q3Bfg" }

actor PPE_Q3BfgBall : PPE_SlowMissileBase
{
	Speed 65
	ProjectileKickBack 100
	Damage (192)
	DamageType "Quake3SplashImpact"
	Decal BFGLightning
	SeeSound ""
	DeathSound "weapons/quake3/bfg/explode"
	Obituary "$OB_MPROCKET"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/quake3/rocket/fly",6,1,1)
	Loopy:
		BFTK ABCD 1 bright A_SpawnItem("PPE_Q3BfgTrail")
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(128,128,0,0,0,0,0,"None","Normal")
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BFE3 ABCDE 3 Bright
		Stop
	}
}

Actor PPE_Q3BfgTrail : PPE_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 2
		BFTT ABCDE 2 bright
		Stop
	}
}