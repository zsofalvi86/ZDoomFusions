Actor PPE_Q3RailGun : PPE_Quake3WeaponBase
{
	+WEAPON.NOAUTOAIM
	Tag "$TAG_Q3_RG"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 7
	Weapon.SlotPriority 7
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoUse 23
	Weapon.AmmoGive 80
	Decal "BulletChip"
	DamageType "Quake3Rail"
	PPE_WeaponBase.SlotAlt1 "PPE_Q3RailGunS9"
	PPE_WeaponBase.SpawnerId1 0,3
	PPE_WeaponBase.SpawnerId2 1,2
	States
	{
	Spawn:
		3RGW A -1
		Stop
	Ready.Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"ReadyZoom")
		3RGI AABBCCDD 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	ReadyZoom:
		TNT1 A 0 A_StopSound(6)
	ReadyZoomLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RailZoom",0)
	DeseLoop:
		3RGA A 1 A_Lower(18)
		Loop
	Select:
		3RGA A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/active",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/railgun/fire")
		TNT1 A 0 A_RailAttack((200),0,1,"none","none",RGF_SILENT,0,"none",0,0,3500,0,96)
		TNT1 A 0 A_FireBullets(0,0,1,(0),"PPE_Q3RailPuff",FBF_NORANDOM,3500,"none",0)
		TNT1 A 0 
		{
			A_FlatBeam("PPE_Q3RailTrail",3500,24,3,true,true);
			A_SpriteBeam("PPE_Q3RailRing",3500,96,1,true,true);
		}//A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"FireZoom")
	FireNormal:
		3RGC ABCDE 2 bright
		3RGG A 20
		3RGG DCB 2
		3RGI AABBCCDD 1
		3RGI AABBCCDD 1
		TNT1 A 0 A_Refire
		Goto Ready.Ready
	FireZoom:
		SSSS ZZZZZ 2 bright
		SSSS Z 20 bright
		SSSS ZZZ 2 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_ALLOWZOOM|WRF_NOSWITCH)
		SSSS ZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_ALLOWZOOM|WRF_NOSWITCH)
		SSSS ZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_ALLOWZOOM|WRF_NOSWITCH)
		TNT1 A 0 A_ReFire
		Goto Ready.Ready
	Zoom:
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_RailZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyZoom
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyZoom
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyZoom
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyZoom
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RailZoom",0)
		SSSS Z 5 bright
		3RGI AABBCCDD 1
		Goto Ready.Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.35)
		TNT1 A 1 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.7)
		Goto LightDone
	}
}

Actor PPE_Q3RailGunS9 : PPE_Q3RailGun 
{ 
	Weapon.SlotNumber 9 Weapon.SlotPriority 6 PPE_WeaponBase.SlotAlt1 "PPE_Q3RailGun" 
	Weapon.AmmoUse 30
	States
	{
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/active",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/railgun/fire")
		TNT1 A 0 A_RailAttack((250),0,1,"none","none",RGF_SILENT,0,"none",0,0,3500,0,96)
		TNT1 A 0 A_FireBullets(0,0,1,(0),"PPE_Q3RailPuff",FBF_NORANDOM,3500,"none",0)
		TNT1 A 0 
		{
			A_FlatBeam("PPE_Q3RailTrail",3500,24,3,true,true);
			A_SpriteBeam("PPE_Q3RailRing",3500,96,1,true,true);
		}//A_GunFlash("FireAnimFunc")
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"FireZoom")
		Goto FireNormal
	}
}

Actor PPE_Q3RailTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
    	Q3RT ABCCDDDCCCBBBAAA 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_Q3RailRing : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
    	Q3RR AABBBCCCDDDEEE 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_Q3RailPuff : BulletPuff
{
	+ALLOWTHRUFLAGS
	+THRUACTORS
	+ALWAYSPUFF
	RenderStyle Normal
	VSpeed 0
	States
	{
	Crash:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/quake3/railgun/hit")
		TNT1 A 0 A_SpawnItem("PPE_Q3RailShockwave")
		PLCR ABCD 4 bright
		Stop
	Spawn:
	Melee:
		TNT1 A 0
		Stop
  }
}

Actor PPE_Q3RailShockwave : PPE_TrailBase
{
	RenderStyle Translucent
	Alpha 0.6
	States
	{
  	Spawn:
		TNT1 A 0
    	XSWG ABBCCDDEE 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_InvisiPuff : BulletPuff
{
	-ALLOWPARTICLES
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		Stop
	}
}

Actor PPE_RailZoom : Inventory
{
	Inventory.MaxAmount 1
}