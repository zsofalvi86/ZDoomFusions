Actor PPE_Q3RocketLauncher : PPE_Quake3WeaponBase
{
	Weapon.SelectionOrder 2400
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 2
	Weapon.AmmoType1 "PPE_Rockets"
	Weapon.SlotNumber 5
	Weapon.SlotPriority 8
	Tag "$TAG_Q3_RL"
	PPE_WeaponBase.SlotAlt1 "PPE_Q3RocketLauncherS7"
	PPE_WeaponBase.SpawnerId1 3,2
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready.Ready:
		Q3RG A 1 A_WeaponReady
		Loop
	Deselect:
		Q3RG A 1 A_Lower(18)
		Loop
	Select:
		Q3RG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/active",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/quake3/rocket/fire",1)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3Rocket",0,1,0,1,1,0)
		Q3RF ABCDE 2 bright
		Q3RG A 18
		TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.4)
		Goto LightDone
	Spawn:
		Q3RP A -1
		Stop
   }
}

Actor PPE_Q3RocketLauncherS7 : PPE_Q3RocketLauncher { Weapon.SlotNumber 7 Weapon.SlotPriority 3 PPE_WeaponBase.SlotAlt1 "PPE_Q3RocketLauncher" }

Actor PPE_Q3Rocket : PPE_SlowMissileBase
{
	Speed 30
	ProjectileKickBack 100
	Damage (100)
	DamageType "Quake3SplashImpact"
	Decal Scorch
	SeeSound ""
	DeathSound "weapons/quake3/rocket/explode"
	Obituary "$OB_MPROCKET"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/quake3/rocket/fly",6,1,1)
	Loopy:
		QAMS AABBCCDD 1 bright A_SpawnItemEx("PPE_Q3RocketTrail")
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(128,128,1,0,0,0,0,"None","Quake3Splash")
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

Actor PPE_Q3RocketHaste : PPE_Q3Rocket
{
	Speed 39
}