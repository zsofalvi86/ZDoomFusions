Actor PPE_Q3ChainGun : PPE_Quake3WeaponBase
{
	Tag "$TAG_Q3_CG"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 6
	Weapon.SlotPriority 8
	Weapon.AmmoType "PPE_Bullets"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Decal "BulletChip"
	PPE_WeaponBase.SlotAlt1 "PPE_Q3ChainGunS5"
	PPE_WeaponBase.SlotAlt2 "PPE_Q3ChainGunS4"
	PPE_WeaponBase.SpawnerId1 2,2
	PPE_WeaponBase.SpawnerId2 2,4
	PPE_WeaponBase.SpawnerId3 14,2
	States
	{
	Spawn:
		Q3CW A -1
		Stop
	Ready.Ready:
		Q3CG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_SetInventory("PPE_ChaingunSpinQ3",0)
	DeseLoop:
		Q3CG A 1 A_Lower(18)
		Loop
	Select:
		Q3CG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/active",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_JumpIfInventory("PPE_ChaingunSpinQ3",0,"FireSpeed")
		TNT1 A 0 A_PlaySound("weapons/quake3/chaingun/run/slow",6,1.0,1)
		TNT1 A 0 A_GiveInventory("PPE_ChaingunSpinQ3",3)
		Q3CF A 2 bright
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/chaingun/fire")
		TNT1 A 0 A_FireBullets(3.3,3.1,-1,(9),"PPE_Quake3TABulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3CGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_GiveInventory("PPE_ChaingunSpinQ3",2)
		Q3CF B 2 bright
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/chaingun/fire")
		TNT1 A 0 A_FireBullets(3.3,3.1,-1,(9),"PPE_Quake3TABulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3CGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_ReFire("Hold")
		Q3CF CD 2 bright
		Goto WindDown
	FireSpeed:
		TNT1 A 0 A_PlaySound("weapons/quake3/chaingun/run/fast",6,1.0,1)
		Q3CF A 1 bright
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/chaingun/fire")
		TNT1 A 0 A_FireBullets(6.3,6.1,-1,(9),"PPE_Quake3TABulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3CGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_GunFlash
		Q3CF B 1 bright
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/chaingun/fire")
		TNT1 A 0 A_FireBullets(6.3,6.1,-1,(9),"PPE_Quake3TABulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3CGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_ReFire("Hold")
		Q3CF CD 1 bright
		Goto WindDown
	Hold:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/active",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_JumpIfInventory("PPE_ChaingunSpinQ3",0,"HoldSpeed")
		TNT1 A 0 A_GiveInventory("PPE_ChaingunSpinQ3",3)
		Q3CF C 2 bright
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/chaingun/fire")
		TNT1 A 0 A_FireBullets(3.3,3.1,1,(9),"PPE_Quake3TABulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3CGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_GiveInventory("PPE_ChaingunSpinQ3",2)
		Q3CF D 2 bright
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/chaingun/fire")
		TNT1 A 0 A_FireBullets(3.3,3.1,1,(9),"PPE_Quake3TABulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3CGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_ReFire("Fire")
		Goto WindDown
	HoldSpeed:
		Q3CF C 1 bright
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/chaingun/fire")
		TNT1 A 0 A_FireBullets(6.3,6.1,1,(9),"PPE_Quake3TABulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3CGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_GunFlash
		Q3CF D 1 bright
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_PlayWeaponSound("weapons/quake3/chaingun/fire")
		TNT1 A 0 A_FireBullets(6.3,6.1,1,(9),"PPE_Quake3TABulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3CGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_ReFire("Fire")
		Goto WindDown
	WindDown:
		TNT1 A 0 A_PlaySound("weapons/quake3/chaingun/cool",6)
		TNT1 A 0 A_SetInventory("PPE_ChaingunSpinQ3",0)
		Q3CX ABCD 2 bright
		Q3CG BBCC 1
		Q3CG DDD 1
		Goto Ready.Ready
	Flash:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,1,2)
		TNT1 A 1 A_Light1
		Goto LightDone
		TNT1 A 1 A_Light2
		Goto LightDone
	}
}

Actor PPE_Q3ChaingunS5 : PPE_Q3Chaingun { Weapon.SlotNumber 5 Weapon.SlotPriority 6 PPE_WeaponBase.SlotAlt1 "PPE_Q3Chaingun" PPE_WeaponBase.SlotAlt2 "PPE_Q3ChaingunS4" }

Actor PPE_Q3ChaingunS4 : PPE_Q3Chaingun { Weapon.SlotNumber 4 Weapon.SlotPriority 8 PPE_WeaponBase.SlotAlt1 "PPE_Q3Chaingun" PPE_WeaponBase.SlotAlt2 "PPE_Q3ChaingunS5" }

Actor PPE_ChaingunSpinQ3 : Inventory
{
	Inventory.MaxAmount 35
}

Actor PPE_Q3CGBulletCasingShooter : PPE_CasingShooterBase
{
	Speed 8
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BulletCasing",0,0,FRandom(60,75),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-13,-22))
		Stop
	}
}