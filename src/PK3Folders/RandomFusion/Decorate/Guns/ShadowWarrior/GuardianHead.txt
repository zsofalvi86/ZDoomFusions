Actor PPE_SWGuardianHead : PPE_SWWeaponBase
{
	+WEAPON.NOAUTOAIM
	Tag "$TAG_SW_HEAD"
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 8
	Weapon.SlotPriority 9
	Weapon.AmmoUse 2
	Weapon.AmmoGive 40
	Weapon.AmmoType "PPE_Cells"
	Weapon.UpSound "weapons/sw/head/up"
	Weapon.KickBack 100
	PPE_WeaponBase.SlotAlt1 "PPE_SWGuardianHeadS0"
	PPE_WeaponBase.SpawnerId1 4,2
	States
	{
	Ready.Ready:
		TNT1 A 0 A_JumpIfMode(2, "Ready.M2")
		TNT1 A 0 A_JumpIfMode(1, "Ready.M1")
	Ready.M0:
		SGHG A 1 A_WeaponReady
		Loop
	Ready.M1:
		SGHG B 1 A_WeaponReady
		Loop
	Ready.M2:
		SGHG C 1 A_WeaponReady
		Loop
  	Deselect:
    	SGHG A 1 A_Lower(12)
    	Loop
  	Select:
    	SGHG A 1 A_Raise(12)
    	Loop
  	Fire:
		TNT1 A 0 A_JumpIfMode(2, "FireWave")
		TNT1 A 0 A_JumpIfMode(1, "FireCircle")
	FireBalls:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/sw/head/fire")
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadBall",0,1,0,2)
		SGHG AAA 1
		TNT1 A 0 A_ReFire
		Goto Ready.M0
	FireCircle:
		//TNT1 A 0 A_JumpIfinventory("PPE_MagicAmmo",40,1)
		//Goto FireBalls
		//TNT1 A 0 A_TakeInventory("PPE_MagicAmmo",39,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/sw/head/fire")
		TNT1 A 0 A_GunFlash
		SGHG BBB 1
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadCircleFireball",0,1,0,4,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadCircleFireball",45,0,0,4,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadCircleFireball",90,0,0,4,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadCircleFireball",135,0,0,4,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadCircleFireball",180,0,0,4,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadCircleFireball",225,0,0,4,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadCircleFireball",270,0,0,4,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadCircleFireball",315,0,0,4,1,0)
		SGHG B 32
		TNT1 A 0 A_ReFire
		Goto Ready.M1
	FireWave:
		//TNT1 A 0 A_JumpIfinventory("PPE_MagicAmmo",40,1)
		//Goto FireBalls
		//TNT1 A 0 A_TakeInventory("PPE_MagicAmmo",39,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/sw/head/fire")
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadWaveBall",0,1,0,2)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadWaveBall",0,0,-24,2)
		TNT1 A 0 A_FireCustomMissile("PPE_GuardianHeadWaveBall",0,0,24,2)
		SGHG CCC 1
		SGHG C 32
		TNT1 A 0 A_ReFire
		Goto Ready.M2
	AltFire:
		TNT1 A 0 A_JumpIfMode(2, "User1")
		TNT1 A 0 A_JumpIfMode(1, "User3")
		Goto User2
	User1:
		TNT1 A 0 A_JumpIfMode(0, "Ready.M0")
		TNT1 A 0 A_SwapMode(0)
		TNT1 A 0 A_ChangeAmmoUse(2)
		TNT1 A 0 A_PlaySound("weapons/sw/head/switch")
		SGHG A 6 A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
		Goto Ready.M0
	User2:
		TNT1 A 0 A_JumpIfMode(1, "Ready.M1")
		TNT1 A 0 A_SwapMode(1)
		TNT1 A 0 A_ChangeAmmoUse(40)
		TNT1 A 0 A_PlaySound("weapons/sw/head/switch")
		SGHG B 6 A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
		Goto Ready.M1
	User3:
		TNT1 A 0 A_JumpIfMode(2, "Ready.M2")
		TNT1 A 0 A_SwapMode(2)
		TNT1 A 0 A_ChangeAmmoUse(40)
		TNT1 A 0 A_PlaySound("weapons/sw/head/switch")
		SGHG C 6 A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
		Goto Ready.M2
  	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_SetPitch(pitch-0.05)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.05)
		Goto LightDone
  	Spawn:
		SGHP A -1
		Stop
	}
}

Actor PPE_SWGuardianHeadS0 : PPE_SWGuardianHead { Weapon.SlotNumber 0 Weapon.SlotPriority 4 PPE_WeaponBase.SlotAlt1 "PPE_SWGuardianHead" }

Actor PPE_GuardianHeadCircleFireball : PPE_GuardianHeadFireCircle
{
	var int user_countdown;
	Damage (200)
	ProjectileKickBack 600
	+NOEXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_countdown",60)
	Loopy:
		TNT1 A 0 A_JumpIf(user_countdown==0,"Death")
		//TNT1 A 0 A_SpawnItemEx("PPE_UnmakerFireballHitter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_SETMASTER)
		GHFB ABCD 1 bright 
		{
			A_Warp(AAPTR_TARGET, 96, 0, 32, 6, WARPF_NOCHECKPOSITION|WARPF_USECALLERANGLE|WARPF_COPYINTERPOLATION);
			A_TraceFireball();
		}
		TNT1 A 0 A_SetUserVar("user_countdown",user_countdown-1)
		loop
	Death:
		GHFB ABCD 1 bright
		stop
	}
}

Actor PPE_GuardianHeadWaveBall : FastProjectile
{
	+BLOODLESSIMPACT
	+PAINLESS
	+GETOWNER
	+RIPPER
	SeeSound ""
	DeathSound ""
	Radius 4
	Height 9
	Speed 64
	RenderStyle "Normal"
	Damage (0)
	States
	{
	Spawn:
		GHFB CDA 1 bright
	Loopy:
		TNT1 A 0 A_PlaySound("weapons/sw/head/napalm")
		GHFB BCCDA 1 bright A_SpawnProjectile("PPE_GuardianHeadWaveSplosion",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Loop
	Death:
		TNT1 A 0 A_PlaySound("weapons/sw/head/napalm")
		TNT1 A 0 A_SpawnProjectile("PPE_GuardianHeadWaveSplosion",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Stop
	}
}

Actor PPE_GuardianHeadWaveSplosion : FastProjectile
{
	+FLOORHUGGER
	+EXTREMEDEATH
	+FORCERADIUSDMG
	+OLDRADIUSDMG
	Projectile
	Speed 0
	ProjectileKickBack 5
	States
	{
	Spawn:
	Death:
		TNT1 A 0
		TNT1 A 0 A_Explode(96,96,0)
		DKXP ABCDEFGHIJK 2 bright
		Stop
	}
}

Actor PPE_HeadMode : Inventory
{
	Inventory.MaxAmount 2
}