Actor PPE_Shuriken: PPE_SWWeaponBase
{
	Weapon.SlotNumber 2
	Weapon.SlotPriority 3
	Weapon.SelectionOrder 2100
	Weapon.AmmoUse 15
	Weapon.AmmoType "PPE_Bullets"
	Weapon.AmmoGive 200
	Inventory.PickupSound "weapons/sw/pickup"
	Weapon.UpSound "weapons/sw/shuriken/up"
	Weapon.ReadySound ""
	Inventory.Icon "SHRMA0"
	Tag "$TAG_SW_SHR"
	States
	{
	Ready.Ready:
		SWSH A 1 A_WeaponReady
		Loop
	Deselect:
		SWSH A 1 A_Lower(12)
		Loop
	Select:
		SWSH A 1 A_Raise(12)
		Loop
	Fire:
		TNT1 A 0 A_PlayWeaponSound("weapons/sw/shuriken/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_ShurikenMissile",0,1,0,3,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_ShurikenMissile",-4,0,0,3,1,0)
		TNT1 A 0 A_FireCustomMissile("PPE_ShurikenMissile",4,0,0,3,1,0)
		SWSH BBB 2
		SWSH CC 2
    	TNT1 A 0 A_ReFire
		Goto Ready.Ready
  }
}

Actor PPE_ShurikenMissile : PPE_SlowMissileBase
{
    +FLATSPRITE
	+SKYEXPLODE
	var int user_missiletime;
	Damage (5)
	Speed 45
	SeeSound ""
	DeathSound ""
	BounceSound "weapons/sw/shuriken/impact"
	BounceType "Doom"
	BounceCount 2
	BounceFactor 0.9
	WallBounceFactor 0.9
    Radius 6
    Height 4
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_missiletime",300)
	MissileLoop:
		TNT1 A 0 A_JumpIf(user_missiletime==0,"StopDeath")
		TNT1 A 0 A_SetUserVar("user_missiletime",user_missiletime-1)
		SHRM A 1
		Loop
    StopDeath:
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
    XDeath:
		TNT1 A 0 A_SpawnItemEx("PPE_SWBloodChunk",0,0,0,FRandom(-3,3),FRandom(-3,3),FRandom(6,10),0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_TRANSFERTRANSLATION ,128)
	Death.Sky:
        TNT1 A 0
		stop
	Crash:
	Death:
        TNT1 A 0 A_SpawnItem("PPE_ShurikenRecoveryBladeStuck")
		stop
	}
}

ACTOR PPE_ShurikenRecoveryBladeStuck : SwitchingDecoration
{
  +DONTGIB +FLATSPRITE +NOGRAVITY +USESPECIAL
  Activation THINGSPEC_Activate | THINGSPEC_ThingTargets
  Radius 6
  Height 4
  states
  {
  Spawn:
    SHRM A 1 
	TNT1 A 0 A_CheckBlock("Spawn",CBF_NOACTORS,AAPTR_DEFAULT,radius+1)
    goto Disappear
  Active:
	TNT1 A 0 A_PlaySound("items/sw/pickup")
	TNT1 A 0 A_GiveToTarget("PPE_Bullets", 5)
	stop
  Disappear:
	TNT1 A 0
	stop
  }
}
