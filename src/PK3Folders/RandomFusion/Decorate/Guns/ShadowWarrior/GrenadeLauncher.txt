Actor PPE_SWGrenadeLauncher : PPE_SWWeaponBase
{
	+WEAPON.NOAUTOAIM
	+WEAPON.ALT_USES_BOTH
	Tag "$TAG_SW_GL"
	Weapon.SelectionOrder 2100
	Weapon.SlotNumber 5
	Weapon.SlotPriority 6
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 10
	Weapon.AmmoGive1 2
	Weapon.AmmoType1 "PPE_Rockets"
	Weapon.AmmoType2 "PPE_Cells"
	Inventory.PickupSound "q/bigweapon"
	Weapon.KickBack 160
	PPE_WeaponBase.SlotAlt1 "PPE_SWGrenadeLauncherS6"
	PPE_WeaponBase.SpawnerId1 3,2
	States
	{
	Ready.Ready:
		SWGL A 1 A_WeaponReady
		Loop
  	Deselect:
    	SWGL A 1 A_Lower(12)
    	Loop
  	Select:
    	SWGL A 1 A_Raise(12)
    	Loop
  	Fire:
		TNT1 A 0 A_Jump(216,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/sw/grenade/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_SWGrenade",0,1,0,4,1,3)
		Goto FireAnim
	AltFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/sw/grenade/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_SWGasGrenade",0,1,0,4,1,3)
		Goto FireAnim
	FireAnim:
		SWGF ABCDE 2 bright
		SWGL B 3 Offset(0,42)
		SWGL C 3 Offset(0,36)
		SWGL A 9 Offset(0,32)
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
		TNT1 A 0 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.7)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.4)
		Goto LightDone
  	Spawn:
		SWGP A -1
		Stop
	}
}

Actor PPE_SWGrenadeLauncherS6 : PPE_SWGrenadeLauncher { Weapon.SlotNumber 6 Weapon.SlotPriority 4 PPE_WeaponBase.SlotAlt1 "PPE_SWGrenadeLauncher" }

Actor PPE_SWGrenade : PPE_SlowMissileBase
{
  SeeSound ""
  BounceSound "weapons/sw/grenade/bounce"
  DeathSound "weapons/sw/grenade/explode"
  BounceFactor 0.75
  WallBounceFactor 0.75
  BounceCount 3
  Gravity 0.2
  Speed 30
  Damage (48)
  +FORCERADIUSDMG +USEBOUNCESTATE
  	+USEBOUNCESTATE
	-ROCKETTRAIL
	-GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
  States
  {
  Spawn:
		DGRD A 1 A_SpawnItem("PPE_SWGLSmokeTrail")
		Loop
  Bounce:
		DGRR ABCDEFGHIJKL 2 A_SpawnItem("PPE_SWGLSmokeTrail")
		Loop
  Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_Explode(192,192)
		TNT1 A 0 A_Quake(7,6,0,292,"")
		TNT1 A 0 A_Quake(4,7,0,804,"")
		TNT1 A 0 A_Quake(2,8,0,4096,"")
		TNT1 AAAAAA 0 A_SpawnItemEx("PPE_GLFireChunk",Random(-12,12),Random(-12,12),Random(12,42),FRandom(-8,8),FRandom(-8,8),FRandom(5,9),0,SXF_NOCHECKPOSITION,64)
		BMZP A 4 bright
		BMZP BCDEF 3 bright
		Stop
  }
}

Actor PPE_SWGasGrenade : PPE_SlowMissileBase
{
  var int user_timedlife;
  SeeSound ""
  BounceSound "weapons/sw/gas/bounce"
  DeathSound "weapons/sw/gas/explode"
  BounceFactor 0.2
  WallBounceFactor 0.2
  BounceCount 2
  Gravity 0.2
  Speed 19
  Damage (0)
  DamageType "PoisonCloud"
  +FORCERADIUSDMG +BOUNCEONACTORS +NODAMAGETHRUST +OLDRADIUSDMG
    +USEBOUNCESTATE
	-ROCKETTRAIL
	-GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
  States
  {
  	Spawn:
		GGRD A 1 A_SpawnItem("PPE_SWGLSmokeTrail")
		Loop
	Death:
		GGRD A 10
		TNT1 A 0 A_SetUserVar("user_timedlife",300)
		TNT1 A 0 A_PlaySound("weapons/sw/gas/loop",6,1,1)
		GGRD A 2
	GasLoop:
		"####" "#" 2 
		{
			if (user_timedlife % 4 == 0) {A_SpawnItemEx("PPE_GasCloud",FRandom(-10,10),FRandom(-10,10),1,0,0,FRandom(4.2,4.9),0,SXF_TRANSFERPOINTERS);}
			A_Explode(5,192,1,0,192);
			A_SetUserVar("user_timedlife",user_timedlife-1);
		}
		"####" "#" 0 A_JumpIf(user_timedlife==0,"TrueDeath")
		Loop
	TrueDeath:
		TNT1 A 0 A_StopSound(6)
		//DGRD A -1
		Stop
  }
}

Actor PPE_NukeGas : PPE_SWGasGrenade
{
	BounceType "None"
	DeathSound ""
	Speed 0
	States
	{
	Spawn:
	Death:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_timedlife",200)
		Goto GasLoop
	}
}

Actor PPE_GLFireChunk
{
	Health 1
	Radius 8
	Height 1
	Gravity 1.1
	+CORPSE
	+NOTELEPORT
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0
		BAL1 AB 2 bright A_SpawnItem("PPE_GLFireChunkTrailTrail",0,0,0,1)
		loop
	Crash:
		TNT1 A 0 A_ScaleVelocity(0)
		BAL1 CDE 3 bright
		stop
	}
}

Actor PPE_GLFireChunkTrailTrail : PPE_TrailBase
{
	RenderStyle Translucent
	Alpha 0.6
	States
	{
	Spawn:
		TNT1 A 2
		FLAR BCDEFGH 2 bright
		Stop
	}
}

Actor PPE_GasCloud
{
	Radius 32
	Height 12
	RenderStyle "Translucent"
	Alpha 0.75
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0
		GSCD ABCDEFGHIJ 2 bright
		stop
	}
}

Actor PPE_SWGLSmokeTrail : PPE_TrailBase
{
	RenderStyle "Translucent"
	Alpha 0.3
	States
	{
  	Spawn:
		TNT1 A 2
    	WTRL ABCDEF 3
    	Stop
	}
}