Actor PPE_FistsOfFury: PPE_SWWeaponBase
{
	+Weapon.AMMO_OPTIONAL
    +Weapon.NOAUTOAIM
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 2200
	Inventory.PickupSound "weapons/sw/pu"
	Weapon.UpSound "weapons/sw/fist/up"
	Weapon.ReadySound ""
	Tag "$TAG_SW_FOF"
	States
	{
	Ready.Ready:
		TNT1 A 0 A_Jump(96,"ReadyLoop")
		TNT1 A 0 A_PlaySound("q/fistup",5)
	ReadyLoop:
		TNT1 A 0 A_Jump(128,"ReadyLoop2")
	ReadyLoop1:
		SFOF A 1 A_WeaponReady
		Loop
	ReadyLoop2:
		SFOF B 1 A_WeaponReady
		Loop
	Deselect:
		SFOF A 1 A_Lower(12)
		Loop
	Select:
		SFOF A 1 A_Raise(12)
		Loop
	Fire:
		"####" "#" 1 Offset(0, 32)
		"####" "#" 1 Offset(0, 48)
		"####" "#" 1 Offset(0, 64)
		"####" "#" 1 Offset(0, 79)
	ContFireRight:
		TNT1 A 0 A_Jump(236,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_Jump(192,"JabLeft")
		Goto JabRight
	ContFireLeft:
		TNT1 A 0 A_Jump(236,2)
		TNT1 A 0 A_PlaySound("q/yell",5|CHAN_NOSTOP)
		TNT1 A 0 A_Jump(192,"JabRight")
		Goto JabLeft
	JabRight:
		TNT1 A 0 A_PlayWeaponSound("weapons/sw/fist/swing")
		SFOF C 1 Offset(-24, 96)
		SFOF C 1 Offset(-12, 64)
		SFOF C 1 Offset(0, 32)
		TNT1 A 0 A_JumpIf(CountInv("PowerInvisibility"),"JRShadow")
		TNT1 A 0 A_FireBullets(-8,0,-1,(10),"PPE_FistPuff",FBF_NORANDOM|FBF_EXPLICITANGLE,72)
		Goto JabRightEnd
	JRShadow:
		TNT1 A 0 A_FireBullets(-8,0,-1,(50),"PPE_FistPuffShadow",FBF_NORANDOM|FBF_EXPLICITANGLE,124)
	JabRightEnd:
		SFOF C 1 Offset(10, 64)
		SFOF C 1 Offset(20, 96)
		TNT1 A 0 A_ReFire("ContFireLeft")
		Goto EndFire
	JabLeft:
		TNT1 A 0 A_PlayWeaponSound("weapons/sw/fist/swing")
		SFOF D 1 Offset(24, 96)
		SFOF D 1 Offset(12, 64)
		SFOF D 1 Offset(0, 32)
		TNT1 A 0 A_JumpIf(CountInv("PowerInvisibility"),"JLShadow")
		TNT1 A 0 A_FireBullets(8,0,-1,(10),"PPE_FistPuff",FBF_NORANDOM|FBF_EXPLICITANGLE,72)
		Goto JabLeftEnd
	JLShadow:
		TNT1 A 0 A_FireBullets(8,0,-1,(50),"PPE_FistPuffShadow",FBF_NORANDOM|FBF_EXPLICITANGLE,124)
	JabLeftEnd:
		SFOF D 1 Offset(-10, 64)
		SFOF D 1 Offset(-20, 96)
		TNT1 A 0 A_ReFire("ContFireRight")
	EndFire:
		TNT1 A 0 Offset(0, 80)
		SFOF A 1 Offset(0, 79)
		SFOF A 1 Offset(0, 64)
		SFOF A 1 Offset(0, 48)
		SFOF A 1 Offset(0, 32)
		Goto ReadyLoop
	Spawn:
		Stop
	}
}

Actor PPE_FistPuff : BulletPuff
{
	+PUFFONACTORS
	+PUFFGETSOWNER
	SeeSound "weapons/sw/fist/impact"
	AttackSound "weapons/sw/fist/impact"
	States
	{
	Crash: //wall
		TNT1 A 0
		TNT1 A 0 A_Jump(192,"Melee")
		TNT1 A 0 A_DamageTarget(1,"Normal")
		Goto Melee
	Spawn: //actor
	Melee:
		PUFF CD 2
		Stop
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("PPE_SWBloodChunk",0,0,0,FRandom(-3,3),FRandom(-3,3),FRandom(6,10),0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_TRANSFERTRANSLATION ,144)
		Stop
  }
}

Actor PPE_FistPuffShadow : BulletPuff
{
	+PUFFONACTORS
	-ALLOWPARTICLES
	SeeSound "weapons/sw/fist/hit"
	AttackSound "weapons/sw/fist/impact"
	VSpeed 0
	States
	{
	Spawn:
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal)
		TNT1 A 0 A_SpawnItemEx("PPE_SWBloodChunk",0,0,0,FRandom(-3,3),FRandom(-3,3),FRandom(6,10),0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_TRANSFERTRANSLATION ,144)
		SRNG ABCDEFGHIJKLMNOPQR 1 bright
		Stop
	Crash:
	Melee:
		TNT1 A 0
		TNT1 A 0 A_ChangeVelocity(0,0,1)
		PUFF CD 2
		Stop
  }
}