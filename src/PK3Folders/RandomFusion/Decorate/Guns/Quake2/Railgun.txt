Actor PPE_Q2RailGun : PPE_Quake2WeaponBase
{
	+WEAPON.NOAUTOAIM
	Tag "$TAG_Q2_RG"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 9
	Weapon.SlotPriority 9
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoUse 3
	Weapon.AmmoGive 8
	Decal "PlasmaScorch"
	DamageType "Quake2Slug"
	PPE_WeaponBase.SlotAlt1 "PPE_Q2RailGunS7"
	PPE_WeaponBase.SpawnerId1 0,3
	PPE_WeaponBase.SpawnerId2 1,2
	States
	{
	Spawn:
		2RGW AAAABBCCDDDCCBB 1
		Loop
	Ready.Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_RailgunZoom",1,"ReadyScope")
		2RGG EEEDDCCBBAAAABBCCDD 1 A_WeaponReady
		Loop
	ReadyScope:
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RailgunZoom",0)
	DeseLoop:
		2RGG A 1 A_Lower(18)
		Loop
	Select:
		2RGG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/quake2/railgun/fire",1,1-(CountInv("PowerSilencer")*0.7)) // 150
		TNT1 A 0 A_RailAttack((240),0,1,6666FF,888888,RGF_FULLBRIGHT|RGF_SILENT,0,"PPE_Q2RailPuff")
		TNT1 A 0 A_JumpIfinventory("PPE_RailgunZoom",1,"FireScope")
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
	FireNormal:
		2RGF ABCDE 1 bright
		2RGG EEDCB 2
		2RGG A 31
		2RGG BCD 2
		TNT1 A 0 A_Refire
		Goto Ready.Ready
	FireScope:
		SSSS ZZZZZ 2 bright
		SSSS Z 16 bright
		SSSS ZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS ZZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready.Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_RailgunZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_RailgunZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RailgunZoom",0)
		SSSS Z 5 bright
		2RGG A 10
		Goto Ready.Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor PPE_Q2RailGunS7 : PPE_Q2RailGun { Weapon.SlotNumber 7 PPE_WeaponBase.SlotAlt1 "PPE_Q2RailGun" }

Actor PPE_RailgunZoom : Inventory
{
	Inventory.MaxAmount 1
}

Actor PPE_Q2RailPuff : BulletPuff
{
	-ALLOWPARTICLES
	+ALWAYSPUFF
	+PUFFONACTORS
	+HITTARGET
	+PAINLESS
	RenderStyle None
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		//TNT1 A 0 A_TransferPointer(AAPTR_TRACER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_MASTER)
		TNT1 A 0 //A_PlaySound("weapons/railgun/hit")
		TNT1 A 0 // landing state
		//TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_MASTER,AAPTR_NULL) == True, "UnawareDamage")
		Stop
	}
}