ACTOR PPE_Q2Blaster : PPE_Quake2WeaponBase
{
  Weapon.SelectionOrder 2000
  Weapon.SlotNumber 1
  Weapon.SlotPriority 1
  Obituary "$OB_MPBLASTER"
  +WEAPON.AMMO_OPTIONAL
  +WEAPON.WIMPY_WEAPON
  Tag "$TAG_Q2_BLS"
  PPE_WeaponBase.SlotAlt1 "PPE_Q2BlasterS2"
  PPE_WeaponBase.SpawnerId1 6,2
  States
  {
  Spawn:
	BSTW A -1
    Stop
  Ready.Ready:
    BSTG A 1 A_WeaponReady
    Loop
  Deselect:
    BSTG A 1 A_Lower(18)
    Loop
  Select:
    BSTG A 1 A_Raise(18)
    Loop
  Fire:
	TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_FireCustomMissile("PPE_BlasterBolt",0,0,0,5,1,0)
	TNT1 A 0 A_PlaySound("weapons/quake2/blaster/fire",1)
	TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
	TNT1 A 0 A_GunFlash
    BSTF ABC 2 bright
    BSTG CBBAA 2
    TNT1 A 0 A_ReFire
    Goto Ready.Ready
  Flash:
    TNT1 A 1 A_Light1
	TNT1 AAA 1 A_SetPitch(pitch-0.16)
	TNT1 AA 1 A_SetPitch(pitch+0.24)
    Goto LightDone
  }
}

Actor PPE_Q2BlasterS2 : PPE_Q2Blaster { Weapon.SlotNumber 2 PPE_WeaponBase.SlotAlt1 "PPE_Q2Blaster" }

Actor PPE_BlasterBolt : PPE_SlowMissileBase
{
	SeeSound ""
	DeathSound "weapons/quake2/blaster/hit"
	Speed 35
	Damage (14)//10 - should 2 shot both zombieman and shotgunners
	DamageType "Quake2Blaster"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/quake2/blaster/fly",6,1.0,1)
	Loopy:
		OLAP A 1 bright
		TNT1 AA 0 A_SpawnParticle(orange,SPF_FULLBRIGHT|SPF_RELATIVE,20,7,180,7,0,0,FRandom(0,1),FRandom(-0.2,0.2),FRandom(-0.2,0.2))
		loop
	Crash:
	Death:
		TNT1 A 0 A_StopSound(6)
		OPSX ABCDE 3 bright
		stop
	XDeath:
		TNT1 A 1
		stop
	}
}