Actor PPE_Q2HyperBlaster : PlasmaRifle
{
	+DONTGIB
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	Decal Q2CellScorch
	Inventory.Pickupmessage "$GOT_PPE_HYB"
	Inventory.PickupSound "items/q2weapon"
	Tag "$TAG_PPE_HYB"
	Weapon.SelectionOrder 105
	Weapon.SlotNumber 8
	Weapon.AmmoUse 1
	Weapon.AmmoType "PPE_Cells"
	Weapon.AmmoGive 40
	States
	{
	Ready:
    	HHYB A 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
    	HHYB A 1 A_Lower
    	Loop
  	Select:
    	HHYB A 1 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/windup",5,1-(CountInv("PowerSilencer")*0.7))
		TNT1 A 0 A_SetInventory("PPE_XPos",3)
		TNT1 A 0 A_SetInventory("PPE_YPos",6)
	FireHyperblaster:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/fire",1,1-(CountInv("PowerSilencer")*0.7))
		TNT1 A 0 A_FireCustomMissile("PPE_HyperBlasterBolt",0,1,CountInv("PPE_XPos")-3,CountInv("PPE_YPos")+1,1,0)
		Goto FireAnimation
	FireAnimation:
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",3,"FireCont3")
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",2,"FireCont2")
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",1,"FireCont1")
		TNT1 A 0 A_Jump(256,"FireCont")
	// Remember, each section sets the next section's x and y
	FireCont: // HybState: 0, frames: 4
		TNT1 A 0 A_GiveInventory("PPE_HybState",1)
		HHYF A 1 bright
		HHYF B 1 bright
		HHYF C 1 bright
		HHYQ A 1 bright
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
		TNT1 A 0 A_SetInventory("PPE_YPos",3)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/loop",6,1-(CountInv("PowerSilencer")*0.7),1)
		TNT1 A 0 A_ReFire("FireHyperblaster")
		Goto Spindown
	FireCont1: // HybState: 1, frames: 3
		TNT1 A 0 A_GiveInventory("PPE_HybState",1)
		HHYF D 1 bright
		HHYF E 1 bright
		HHYF F 1 bright
		TNT1 A 0 A_SetInventory("PPE_XPos",3)
		TNT1 A 0 A_SetInventory("PPE_YPos",0)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/loop",6,1-(CountInv("PowerSilencer")*0.7),1)
		TNT1 A 0 A_ReFire("FireHyperblaster")
		goto Spindown
	FireCont2: // HybState: 2, frames: 4
		TNT1 A 0 A_GiveInventory("PPE_HybState",1)
		HHYF G 1 bright
		HHYF H 1 bright
		HHYF I 1 bright
		HHYQ B 1 bright
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
		TNT1 A 0 A_SetInventory("PPE_YPos",3)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/loop",6,1-(CountInv("PowerSilencer")*0.7),1)
		TNT1 A 0 A_ReFire("FireHyperblaster")
		Goto Spindown
	FireCont3: // HybState: 3, frames: 3
		TNT1 A 0 A_TakeInventory("PPE_HybState",255)
		HHYF J 1 bright
		HHYF K 1 bright
		HHYF L 1 bright
		TNT1 A 0 A_SetInventory("PPE_XPos",3)
		TNT1 A 0 A_SetInventory("PPE_YPos",6)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/loop",6,1-(CountInv("PowerSilencer")*0.7),1)
		TNT1 A 0 A_ReFire("FireHyperblaster")
		goto Spindown
	Spindown:
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",3,"SpindownStartSV2")
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",2,"SpindownStartNV1")
		TNT1 A 0 A_JumpIfInventory("PPE_HybState",1,"SpindownStartNV2")
		HHYB BC 1
		HHYQ A 1
		Goto SpindownStartNV1
	SpindownStartSV2:
		HHYB DEABC 1
		HHYQ A 1
	SpindownStartNV1:
		HHYQ BCA 1
	SpindownStartNV2:
		HHYQ BCAB 1
	SpindownEnd:
		//TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlaySound("weapons/hyperblaster/winddown",6,1-(CountInv("PowerSilencer")*0.7))
		HHYB DEABC 1
		HHYQ ABC 1
		HHYQ ABC 2
		HHYB D 3
		HHYB E 4
		HHYB A 1
		TNT1 A 0 A_SetInventory("PPE_XPos",0)
		TNT1 A 0 A_SetInventory("PPE_YPos",0)
		TNT1 A 0 A_SetInventory("PPE_HybState",0)
		Goto Ready
  	Flash:
    	TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.4)
		Goto LightDone
  	Spawn:
		HYBG AAAAAABBCCDDCCBB 1
		Loop
	}
}

Actor PPE_HyperBlasterBolt : PPE_SlowMissileBase
{
	Decal Q2CellScorch
	SeeSound ""
	DeathSound "weapons/laser/hit"
	Speed 35
	Damage (25) // 20 - upscaled to match plasma
	DamageType "Quake2Cell"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/laser/fly",6,1.0,1)
	Loopy:
		GLAP A 1 bright
		loop
	Crash:
	XDeath:
	Death:
		TNT1 A 0 A_StopSound(6)
		GPSX ABCDE 3 bright
		stop
	}
}

Actor PPE_HybState : Inventory
{
	Inventory.MaxAmount 3
}

Actor PPE_XPos : Inventory
{
	Inventory.MaxAmount 100
}

Actor PPE_YPos : Inventory
{
	Inventory.MaxAmount 100
}