Actor PPE_SSMiniGun : PPE_SeriousSamWeaponBase
{
	Tag "$TAG_SS_MG"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 6
	Weapon.SlotPriority 8
	Weapon.AmmoType "PPE_Bullets"
	Weapon.AmmoUse 2
	Weapon.AmmoGive 20
	Decal "BulletChip"
	DamageType "SeriousBullet"
	PPE_WeaponBase.SlotAlt1 "PPE_SSMiniGunS4"
	PPE_WeaponBase.SpawnerId1 2,2
	PPE_WeaponBase.SpawnerId2 2,4
	PPE_WeaponBase.SpawnerId3 14,2
	States
	{
	Spawn:
		SSMW A -1
		Stop
	Ready.Ready:
		SSMG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
		SSMG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		SSMG A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("weapons/ss/minigun/trigger",5)
		TNT1 A 0 A_SetInventory("PPE_MiniCounter",2)
	WindUp1:
		TNT1 A 0 A_PlaySound("weapons/ss/minigun/windup",6)
		SSMG AB 2
		TNT1 A 0 A_ReFire("WindUp2")
		TNT1 A 0 A_PlaySound("weapons/ss/minigun/winddown",6)
		Goto WindDownF2
	WindUp2:
		SSMG CD 1
		TNT1 A 0 A_ReFire("WindUp3")
		TNT1 A 0 A_PlaySound("weapons/ss/minigun/winddown",6)
		Goto WindDownF1
	WindUp3:
		SSMG ABCD 1
		TNT1 A 0 A_ReFire("Hold1")
		TNT1 A 0 A_PlaySound("weapons/ss/minigun/winddown",6)
		Goto WindDownF1
	Hold1:
		TNT1 A 0 A_PlaySound("weapons/ss/minigun/loop",6,1.0,1)
		TNT1 A 0 A_AlertMonsters // 20
		TNT1 A 0 A_FireBullets(0,0,1,(20),"PPE_MinigunPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireCustomMissile("PPE_SSMGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_FireCustomMissile("PPE_SSMGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_ReFire("Hold2")
		SSMF ABC 1 bright A_GunFlash
		Goto WindDown
	Hold2:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireBullets(0,0,1,(20),"PPE_MinigunPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireCustomMissile("PPE_SSMGBulletCasingShooter",0,0,0,-4)
		TNT1 A 0 A_FireCustomMissile("PPE_SSMGBulletCasingShooter",0,0,0,-4)
		SSMF ABCD 1 bright A_GunFlash
		TNT1 A 0 A_ReFire("Hold1")
	WindDown:
		TNT1 A 0 A_PlaySound("weapons/ss/minigun/winddown",6)
		SSMX ABCD 1 bright
	WindDownF1:
		SSMG AB 1
		TNT1 A 0 A_ReFire("WindUp2")
	WindDownF2:
		SSMG CD 2
		TNT1 A 0 A_ReFire("WindUp1")
		SSMG ABCD 2
		TNT1 A 0 A_ReFire("WindUp1")
		SSMG D 1
		Goto Ready.Ready
	Flash:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("PPE_MiniCounter",0,"SoundAndReset")
		TNT1 A 0 A_GiveInventory("PPE_MiniCounter",1)
		Goto ChooseLight
	SoundAndReset:
		TNT1 A 0 A_PlaySound("weapons/ss/minigun/fire",4)
		TNT1 A 0 A_SetInventory("PPE_MiniCounter",0)
	ChooseLight:
		TNT1 A 0 A_Jump(256,1,2)
		TNT1 A 1 A_Light1
		Goto LightDone
		TNT1 A 1 A_Light2
		Goto LightDone
	}
}

Actor PPE_SSMiniGunS4 : PPE_SSMiniGun { Weapon.SlotNumber 4 Weapon.SlotPriority 10 PPE_WeaponBase.SlotAlt1 "PPE_SSMiniGun" }

Actor PPE_MiniCounter : Inventory
{
	Inventory.MaxAmount 2
}

Actor PPE_MinigunPuff : PPE_SSBulletPuff {} // searched for in zscript to make close gibbing

Actor PPE_SSMGBulletCasingShooter : PPE_CasingShooterBase
{
	Speed 8
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_BulletCasing",0,0,FRandom(-60,-75),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-13,-22))
		Stop
	}
}