Actor PPE_SSShotgun : PPE_SeriousSamWeaponBase
{
	Tag "$TAG_SS_SG"
	Weapon.SelectionOrder 1300
	Weapon.SlotNumber 3
	Weapon.SlotPriority 4
	Weapon.AmmoUse 8
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoGive 80
	Decal "BulletChip"
	PPE_WeaponBase.SlotAlt1 "PPE_SSShotgunS2"
	PPE_WeaponBase.SpawnerId1 0,1
	PPE_WeaponBase.SpawnerId2 15,1
	States
	{
	Ready.Ready:
    	SSSG A 1 A_WeaponReady
    	Loop
  	Deselect:
		SSSG A 1 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
		SSSG A 1 A_Raise
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		// Top row, 3 pellets
		TNT1 A 0 A_FireBullets(0,-0.35,-1,(10),"PPE_ShotgunPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(2,-0.35,-1,(10),"PPE_ShotgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-2,-0.35,-1,(10),"PPE_ShotgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// Bottom row, 4 pellets
		TNT1 A 0 A_FireBullets(0.6,0.35,-1,(10),"PPE_ShotgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-0.6,0.35,-1,(10),"PPE_ShotgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(3,0.35,-1,(10),"PPE_ShotgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-3,0.35,-1,(10),"PPE_ShotgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_PlayWeaponSound("weapons/ss/shotgun/fire")
    	SSSF ABC 2 bright
		SSSG B 2 Offset(25,52)
		SSSG B 2 Offset(0,32)
		SSSG C 2 Offset(0,32)
		SSSG C 2 Offset(-4,37)
		TNT1 A 0 A_FireCustomMissile("PPE_SSSGShellCasingShooter",0,0,0,6)
		SSSG C 2 Offset(-8,42)
		SSSG D 2 Offset(0,32)
		SSSG D 2 Offset(-4,39)
		SSSG D 2 Offset(0,32)
		SSSG C 2 Offset(-8,42)
		SSSG C 2 Offset(-4,37)
		SSSG C 2 Offset(0,32)
		SSSG B 2 Offset(0,32)
		SSSG B 2 Offset(25,52)
		SSSG A 2 Offset(-3,34)
		SSSG A 2 Offset(-1,32)
		TNT1 A 0 A_ReFire
		Goto Ready.Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAA 1 A_SetPitch(pitch-0.35)
		TNT1 A 0 A_Light2
		TNT1 AA 1 A_SetPitch(pitch+0.7)
		Goto LightDone
  	Spawn:
    	SSSW A -1
    	Stop
	}
}

Actor PPE_SSShotgunS2 : PPE_SSShotgun { Weapon.SlotNumber 2 Weapon.SlotPriority 8 PPE_WeaponBase.SlotAlt1 "PPE_SSShotgun" }

Actor PPE_ShotgunPuff : PPE_SSBulletPuff {} // searched for in zscript to make close gibbing

Actor PPE_SSSGShellCasingShooter : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_ShellCasing",0,0,FRandom(-62,-45),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-16,-22))
		Stop
	}
}