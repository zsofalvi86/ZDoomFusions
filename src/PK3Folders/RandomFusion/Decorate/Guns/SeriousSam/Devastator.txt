Actor PPE_SSDevastator : PPE_SeriousSamScoped
{
	+WEAPON.NOAUTOAIM
	Tag "$TAG_SS_DEV"
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 9
	Weapon.SlotPriority 5
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoUse 3
	Weapon.AmmoGive 8
	Decal "BulletChip"
	DamageType "SeriousSniper"
	PPE_WeaponBase.SlotAlt1 "PPE_SSDevastatorS7"
	PPE_WeaponBase.SpawnerId1 0,3
	PPE_WeaponBase.SpawnerId2 1,2
	States
	{
	Drop:
	Spawn:
		SDEV W -1
		Stop
	Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_SniperZoom",1,"ReadyScope")
		SDVG A 1 A_WeaponReady
		Loop
	ReadyScope:
		TNT1 A 0 A_RollScope(false)
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_ClearOverlays(11,11)
		TNT1 A 0 A_SetInventory("PPE_SniperZoom",0)
	DeseLoop:
		SDVG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		SDVG A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("weapons/ss/devastator/fire")
		TNT1 A 0 A_FireCustomMissile("PPE_DevastatorShot",0,1,0,1,1,0)
		TNT1 A 0 A_JumpIfinventory("PPE_SniperZoom",1,"FireScope")
	FireNormal:
		SDVF ABCDE 2 bright
		SDVG BCDA 2
		TNT1 A 0 A_Refire
		Goto Ready
	FireScope:
		SSSS YYYYY 2 bright
		SSSS YYYYYYZZ 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_SniperZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_SniperZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/ss/sniper/zoom",6,1.0,1)
		TNT1 A 0 A_RollScope(true)
		TNT1 A 0 A_Overlay(11,"ScopeStart",false)
		TNT1 A 0 A_OverlayFlags(11,PSPF_ALPHA|PSPF_RENDERSTYLE|PSPF_ADDWEAPON,true)
		TNT1 A 0 A_OverlayRenderstyle(11,STYLE_Translucent)
		TNT1 A 0 A_OverlayAlpha(11, 0.75)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 4 bright
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_RollScope(false)
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/ss/sniper/zoom",6)
		TNT1 A 0 A_ZoomFactor(1)
		SSSS Z 5 bright
		TNT1 A 0 A_ClearOverlays(11,11)
		TNT1 A 0 A_SetInventory("PPE_SniperZoom",0)
		SDVG A 10
		Goto Ready
	ScopeStart:
		TNT1 A 0 A_SetScopeSprite
	ScopeWall:
		SXHS "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeGreen:
		SXHT "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeYellow:
		SXHU "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeRed:
		SXHV "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor PPE_SSDevastatorS7 : PPE_SSDevastator { Weapon.SlotNumber 7 Weapon.SlotPriority 8 PPE_WeaponBase.SlotAlt1 "PPE_SSDevastator" }

Actor PPE_DevTrail : PPE_TrailBase
{
	RenderStyle "Translucent"
	Alpha 0.2
	States
	{
  	Spawn:
		TNT1 A 2
    	WTRS ABCDEF 3
    	Stop
	}
}