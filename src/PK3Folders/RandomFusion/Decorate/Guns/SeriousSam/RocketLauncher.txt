actor PPE_SSRocketLauncher : PPE_SeriousSamWeaponBase
{
	Weapon.SelectionOrder 2400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 5
	Weapon.SlotPriority 6
	Tag "$TAG_SS_RL"
	PPE_WeaponBase.SpawnerId1 3,1
	+NOAUTOFIRE
	States
	{
	Ready.Ready:
		SSRL A 1 A_WeaponReady
		Loop
	Deselect:
		SSRL A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		SSRL A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_SSRocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/ss/rocket/fire",1)
		SSRF A 10 bright
		SSRL BCD 3
		SSRL A 5
		TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.4)
		Goto LightDone
	Spawn:
		SSRW A -1
		Stop
   }
}

Actor PPE_SSRocket : Rocket
{
	+FORCERADIUSDMG
	Speed 17
	Damage (140)
	SeeSound ""
	DeathSound "weapons/ss/explode"
	Obituary "$OB_MPROCKET"
	DamageType "SeriousRocket"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/ss/rocket/fly",6,1,1)
	Loopy:
		SMSL A 1 bright
		Loop
	Death:
		TNT1 A 0 A_StopSound(6) // 100, 50
		TNT1 A 0 A_Explode(70,168)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}