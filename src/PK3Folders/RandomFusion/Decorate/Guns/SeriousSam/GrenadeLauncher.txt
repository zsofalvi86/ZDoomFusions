actor PPE_SSGrenadeLauncher : PPE_SeriousSamWeaponBase
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 5
	Weapon.SlotPriority 5
	Tag "$TAG_SS_GL"
	PPE_WeaponBase.SpawnerId1 3,2
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready.Ready:
		SSGL A 1 A_WeaponReady
		Loop
	Deselect:
		SSGL A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		SSGL A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		SSGL A 3
		TNT1 A 0 A_SetInventory("PPE_GL_Charge",0)
		TNT1 A 0 A_SetInventory("PPE_GL_UnloadTolerance",0)
		Goto GrenadeChargeInitial
	GrenadeChargeLoopy:
		SSGL A 2 
	GrenadeChargeInitial:
	    TNT1 A 0 A_JumpIfInventory("PPE_GL_Charge",0,"GrenadeChargeReleaseLoopy")
		TNT1 A 0 A_GiveInventory("PPE_GL_Charge",1)
		TNT1 A 0 A_ReFire("GrenadeChargeLoopy")
		Goto GrenadeChargeRelease
	GrenadeChargeReleaseLoopy:
		TNT1 A 0 A_JumpIfInventory("PPE_GL_UnloadTolerance",0,"GrenadeChargeRelease")
		TNT1 A 0 A_GiveInventory("PPE_GL_UnloadTolerance",1)
		SSGL A 1 
		TNT1 A 0 A_ReFire("GrenadeChargeReleaseLoopy")
		Goto GrenadeChargeRelease
	GrenadeChargeRelease:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("weapons/ss/grenade/fire",1)
		TNT1 A 0 A_JumpIfInventory("PPE_GL_Charge",10,"GrenadeChargeRelease3")
		TNT1 A 0 A_JumpIfInventory("PPE_GL_Charge",5,"GrenadeChargeRelease2")
		TNT1 A 0 A_FireCustomMissile("PPE_SSGrenade1",0,1,0,0,1,6)
		Goto Anim
	GrenadeChargeRelease2:
		TNT1 A 0 A_FireCustomMissile("PPE_SSGrenade2",0,1,0,0,1,5)
		Goto Anim
	GrenadeChargeRelease3:
		TNT1 A 0 A_FireCustomMissile("PPE_SSGrenade3",0,1,0,0,1,4)
	Anim:
		SSGL FEDCB 2
		TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.15)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.3)
		Goto LightDone
	Spawn:
		SSGL W -1
		Stop
   }
}

Actor PPE_SSGrenadeBase : Rocket
{
	+USEBOUNCESTATE
	-ROCKETTRAIL
	+GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
	SeeSound ""
	BounceSound "weapons/ss/grenade/bounce"
	DeathSound "weapons/ss/explode"
	BounceFactor 0.6
	WallBounceFactor 0.6
	Gravity 0.4
	BounceCount 4
	Speed 15
	Damage (70) //98
	DamageType "SeriousGrenade"
	States
	{
	Spawn:
		DGRD A 1
		Loop
	Bounce:
		DGRR ABCDEFGHIJKL 2
		Loop
	Death:
		TNT1 A 0 A_NoGravity // 75, 100
		TNT1 A 0 A_Explode(130,192)
		TNT1 A 0 A_Quake(4,5,0,256,"")
		TNT1 A 0 A_Quake(2,6,0,720,"")
		TNT1 A 0 A_Quake(1,6,0,4096,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

Actor PPE_SSGrenade1 : PPE_SSGrenadeBase
{
	Speed 15
}

Actor PPE_SSGrenade2 : PPE_SSGrenadeBase
{
	Damage (88) //123
	Speed 30
}

Actor PPE_SSGrenade3 : PPE_SSGrenadeBase
{
	Damage (105) //149
	Speed 45
}

Actor PPE_GL_Charge : Inventory
{
	Inventory.MaxAmount 10
}

Actor PPE_GL_UnloadTolerance : Inventory
{
	Inventory.MaxAmount 30
}