Actor PPE_SSFlamethrower : PPE_SeriousSamWeaponBase
{
	+DONTGIB
	+WEAPON.NOAUTOAIM
	Tag "$TAG_SS_FT"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 6
	Weapon.SlotPriority 9
	Weapon.AmmoUse 1
	Weapon.AmmoType "PPE_Cells"
	Weapon.AmmoGive 40
	PPE_WeaponBase.SlotAlt1 "PPE_SSFlamethrowerS8"
	PPE_WeaponBase.SpawnerId1 4,3
	States
	{
	Ready.Ready:
    	SSFT BBCCDD 1 A_WeaponReady
    	Loop
  	Deselect:
		TNT1 A 0 A_StopSound(6)
	DeseLoop:
    	SSFT A 1 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
    	SSFT A 1 A_Raise
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_PlayWeaponSound("weapons/ss/flame/start")
	Hold:
		TNT1 A 0 A_AlertMonsters
		SSFT E 1 bright A_FireCustomMissile("PPE_FlameMissile",FRandom(-2,2),1,0,4,1,FRandom(-0.75,0.75))
    	SSFT F 1 bright A_FireCustomMissile("PPE_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
        SSFT G 1 bright A_FireCustomMissile("PPE_FakeFlameMissile",FRandom(-2,2),1,0,4,1,FRandom(-0.75,0.75))
        //SSFT E 1 bright A_FireCustomMissile("PPE_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_PlaySound("weapons/ss/flame/fire",6,1,1)
		SSFT F 1 bright A_FireCustomMissile("PPE_FlameMissile",FRandom(-2,2),1,0,4,1,FRandom(-0.75,0.75))
    	SSFT G 1 bright A_FireCustomMissile("PPE_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
    	SSFT E 1 bright A_FireCustomMissile("PPE_FakeFlameMissile",FRandom(-2,2),1,0,4,1,FRandom(-0.75,0.75))
    	SSFT F 1 bright A_FireCustomMissile("PPE_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
    	TNT1 A 0 A_ReFire
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlayWeaponSound("weapons/ss/flame/stop")
    	SSFT GHIJ 1 bright A_FireCustomMissile("PPE_FakeFlameMissile",FRandom(-2,2),0,0,4,1,FRandom(-0.75,0.75))
		Goto Ready.Ready
  	Flash:
    	TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.45)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.6)
		Goto LightDone
  	Spawn:
		SSFT W -1
		Stop
	}
}

Actor PPE_SSFlamethrowerS8 : PPE_SSFlamethrower { Weapon.SlotNumber 8 Weapon.SlotPriority 8 PPE_WeaponBase.SlotAlt1 "PPE_SSFlamethrower" }

Actor PPE_FlameMissile : PPE_SlowMissileBase
{
	+RIPPER
	+BLOODLESSIMPACT
	//BounceType Doom
	//BounceFactor 0.15
	//WallBounceFactor 0.12
	SeeSound ""
	DeathSound ""
	RenderStyle Normal
	Alpha 1.0
	Speed 26
	Damage (2)
	DamageType "SeriousFlame"
	ProjectileKickBack 0
	States
	{
	Spawn:
		TNT1 A 0
		FLPR ABCDE 1 bright A_CustomMissile("PPE_IgniteMissile",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_SAVEPITCH)
		FLSS ABCDEFGHIJK 1 bright A_CustomMissile("PPE_IgniteMissile",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_SAVEPITCH)
	Death:
		TNT1 A 0
		stop
	}
}

Actor PPE_IgniteMissile : PPE_SlowMissileBase
{
	var int user_countdown;
	+HITTRACER
	Radius 18
	Height 10
	SeeSound ""
	DeathSound ""
	RenderStyle None
	Speed 0
	Damage (0)
	DamageType "SeriousFlame"
	ProjectileKickBack 0
	States
	{
	Spawn:
		TNT1 A 2
		stop
	Death:
		//TNT1 A 0
		//stop
	XDeath:
		TNT1 A 0 A_SetUserVar("user_countdown",25)
	BurnLoop:
		TNT1 A 0 A_JumpIf(user_countdown==0,"BurnEnd")
		TNT1 AA 1 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION )
		TNT1 A 0 A_CheckProximity("KillOther","PPE_IgniteMissile",10,1,CPXF_SETMASTER)
		Goto Resume
	KillOther:
		TNT1 A 0 A_RemoveMaster(RMVF_EVERYTHING)
	Resume:
		TNT1 A 0 A_SpawnItemEx("PPE_FT_FireChunk",Random(-16,16),Random(-16,16),12,0,0,3,0,0,64)
		TNT1 A 0 A_Explode(5,20,0,0,20)
		TNT1 A 0 A_SetUserVar("user_countdown",user_countdown-1)
		Goto BurnLoop
	BurnEnd:
		TNT1 A 0
		stop
	}
}

Actor PPE_FT_FireChunk : ArchvileFire
{
	RenderStyle Normal
	Alpha 1.0
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/ss/flame/burn")
		TNT1 A 0 A_Jump(255,"A","B")
		A:
		SFIR ABCDEFGH 2 bright
		Stop
		B:
		SFIR IJKLMNOP 2 bright
		Stop
	}
}

Actor PPE_FakeFlameMissile : PPE_FlameMissile
{
	Damage (0)
	+NOPAIN
	States
	{
	Spawn:
		TNT1 A 0
		FLPR ABCDE 1 bright
		FLSS ABCDEFGHIJK 1 bright
		stop
	}
}
