Actor PPE_SSCoachGun : PPE_SeriousSamWeaponBase
{
	Tag "$TAG_SS_SSG"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 3
	Weapon.SlotPriority 7
	Weapon.AmmoUse 2
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoGive 8
	Decal "BulletChip"
	DamageType "SeriousSuperShell"
	PPE_WeaponBase.SpawnerId1 0,2
	PPE_WeaponBase.SpawnerId2 1,1
	PPE_WeaponBase.SpawnerId3 1,3
	States
	{
	Ready.Ready:
    	SSS2 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
  	Deselect:
		SSS2 A 1 A_Lower
		TNT1 A 0 A_Lower
    	Loop
  	Select:
		SSS2 A 1 A_Raise
		TNT1 A 0 A_Raise
    	Loop
  	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		//TNT1 A 0 A_FireBullets(7.8,3.6,14,(13),"PPE_BulletPuff",FBF_USEAMMO|FBF_NORANDOM)
		// 1st row, 3 pellets
		TNT1 A 0 A_FireBullets(0,-1.9,-1,(15),"PPE_CoachgunPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(3.2,-1.9,-1,(14),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-3.2,-1.9,-1,(14),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 2nd row, 4 pellets
		TNT1 A 0 A_FireBullets(1.6,-0.7,-1,(15),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-1.6,-0.7,-1,(15),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(4.6,-0.7,-1,(14),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-4.6,-0.7,-1,(14),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 3rd row, 3 pellets
		TNT1 A 0 A_FireBullets(0,0.7,-1,(15),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(3.2,0.7,-1,(14),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-3.2,0.7,-1,(14),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		// 4th row, 4 pellets
		TNT1 A 0 A_FireBullets(1.6,1.9,-1,(15),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-1.6,1.9,-1,(15),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(4.6,1.9,-1,(14),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets(-4.6,1.9,-1,(14),"PPE_CoachgunPuff",FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_PlayWeaponSound("weapons/ss/coachgun/fire")
		SSS2 BCD 2 bright
		SSS2 A 2 Offset(0,34)
		SSS2 G 2 Offset(10,32)
		SSS2 E 2 Offset(22,36)
		SSS2 E 2 Offset(26,32)
		SSS2 F 2 Offset(5,38)
		SSS2 F 2 Offset(7,34)
		SSS2 F 1 Offset(8,32)
		TNT1 A 0 A_PlaySound("weapons/ss/coachgun/load",6)
		SSS2 F 1 Offset(8,33)
		SSS2 G 2 Offset(5,32)
		SSS2 G 2 Offset(-8,39)
		SSS2 H 2 Offset(1,35)
		SSS2 H 2 Offset(-11,42)
		TNT1 A 0 Offset(0,32)
		TNT1 A 0 A_FireCustomMissile("PPE_SSSSGShellCasingShooter",0,0,0,6)
		TNT1 A 0 A_FireCustomMissile("PPE_SSSSGShellCasingShooter",0,0,0,6)
		SSS2 IIJJKKKLLL 1
		SSS2 M 2 Offset(0,36)
		SSS2 M 2 Offset(-4,32)
		SSS2 G 2 Offset(6,36)
		SSS2 G 2 Offset(0,32)
		SSS2 E 2 Offset(-3,50)
		SSS2 E 2 Offset(3,42)
		SSS2 A 2 Offset(3,35)
		SSS2 A 2 Offset(2,34)
		SSS2 A 2 Offset(1,33)
		SSS2 A 2 Offset(0,32)
		//SSS2 GHIJ 2
		//SSS2 KL 4
		//SSS2 MGE 3
		//SSS2 A 12
		Goto Ready.Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.2)
		Goto LightDone
  	Spawn:
    	SSS2 W -1
    	Stop
	}
}

Actor PPE_CoachgunPuff : PPE_SSBulletPuff {} // searched for in zscript to make close gibbing

Actor PPE_SSSSGShellCasingShooter : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_ShellCasing",0,0,FRandom(70,40),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-16,-22))
		Stop
	}
}