Actor PPE_SSSniperRifle : PPE_SeriousSamScoped
{
	+WEAPON.NOAUTOAIM
	Tag "$TAG_SS_RIF"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 7
	Weapon.SlotPriority 8
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoUse 8
	Weapon.AmmoGive 80
	Weapon.Kickback 10
	Decal "BulletChip"
	DamageType "SeriousSniper"
	PPE_WeaponBase.SlotAlt1 "PPE_SSSniperRifleS9"
	PPE_WeaponBase.SpawnerId1 0,3
	PPE_WeaponBase.SpawnerId1 1,2
	States
	{
	Spawn:
		SSSS W -1
		Stop
	Ready.Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_SniperZoom",1,"ReadyScope")
		SSSS A 1 A_WeaponReady
		Loop
	ReadyScope:
		TNT1 A 0 A_RollScope(false)
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_ClearOverlays(11,11)
		TNT1 A 0 A_SetInventory("PPE_SniperZoom",0)
	DeseLoop:
		SSSS A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		SSSS A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("Weapons/ss/sniper/fire")
		TNT1 A 0 A_JumpIfinventory("PPE_SniperZoom",1,"FireScope")
		TNT1 A 0 A_Jump(256,"FireNormal")
	FireNormal:
	    TNT1 A 0 A_FireBullets(0,0,1,(70),"PPE_SniperPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		SSSS BCD 2 bright
		SSSS A 2 Offset(0,38)
		SSSS A 2 Offset(0,36)
		SSSS A 2 Offset(0,34)
		SSSS A 2 Offset(0,32)
		SSSS A 35
		TNT1 A 0 A_Refire
		Goto Ready.Ready
	FireScope:
		TNT1 A 0 A_JumpIfinventory("PPE_Shells",30,1)
		Goto DeZoom
		TNT1 A 0 A_TakeInventory("PPE_Shells",22)
	    TNT1 A 0 A_FireBullets(0,0,1,(300),"PPE_SniperPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		SSSS YYY 2 bright
		SSSS Y 13 bright
		SSSS YYYYYYYYYYY 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS YYYYYYYYYYY 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS YYYYYYYYYZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready.Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_SniperZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_SniperZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/ss/sniper/zoom",6,1.0,1)
		TNT1 A 0 A_RollScope(true)
		TNT1 A 0 A_Overlay(11,"ScopeStart",false)
		TNT1 A 0 A_OverlayFlags(11,PSPF_ALPHA|PSPF_RENDERSTYLE|PSPF_ADDWEAPON,true)
		TNT1 A 0 A_OverlayRenderstyle(11,STYLE_Translucent)
		TNT1 A 0 A_OverlayAlpha(11, 0.75)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 4 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 4 bright
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_RollScope(false)
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/ss/sniper/zoom",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_DeRollScope
		SSSS Z 5 bright
		TNT1 A 0 A_ClearOverlays(11,11)
		TNT1 A 0 A_SetInventory("PPE_SniperZoom",0)
		SSSS A 10
		Goto Ready.Ready
	ScopeStart:
		TNT1 A 0 A_SetScopeSprite
	ScopeWall:
		SXHS "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeGreen:
		SXHT "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeYellow:
		SXHU "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	ScopeRed:
		SXHV "#" 1 bright Offset(0,-32)
		Goto ScopeStart
	Flash:
		TNT1 A 1 A_Light1
		//TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		//TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor PPE_SSSniperRifleS9 : PPE_SSSniperRifle 
{ 
	Weapon.SlotNumber 9 Weapon.SlotPriority 7 PPE_WeaponBase.SlotAlt1 "PPE_SSSniperRifle"
	Weapon.AmmoUse 8
	States
	{
	FireNormal:
	    TNT1 A 0 A_FireBullets(0,0,1,(70),"PPE_SniperPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		SSSS BCD 2 bright
		SSSS A 2 Offset(0,38)
		SSSS A 2 Offset(0,36)
		SSSS A 2 Offset(0,34)
		SSSS A 2 Offset(0,32)
		SSSS A 35
		TNT1 A 0 A_Refire
		Goto Ready.Ready
	FireScope:
		TNT1 A 0 A_JumpIfinventory("PPE_Shells",40,1)
		Goto DeZoom
		TNT1 A 0 A_TakeInventory("PPE_Shells",32)
	    TNT1 A 0 A_FireBullets(0,0,1,(400),"PPE_SniperPuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		SSSS YYY 2 bright
		SSSS Y 13 bright
		SSSS YYYYYYYYYYY 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS YYYYYYYYYYY 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		SSSS YYYYYYYYYZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
		TNT1 A 0 A_ReFire
		Goto Ready.Ready
	}
}

Actor PPE_SniperPuff : PPE_SSBulletPuff
{
	+NOEXTREMEDEATH
}

Actor PPE_SniperZoom : Inventory
{
	Inventory.MaxAmount 1
}