Actor PPE_SSCannon : PPE_SeriousSamWeaponBase
{
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	-WEAPON.NOAUTOFIRE
	Tag "$TAG_SS_CAN"
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 0
	Weapon.SlotPriority 10
	Weapon.AmmoType "PPE_Cells"
	Weapon.AmmoUse 20
	Weapon.AmmoGive 40
	PPE_WeaponBase.SpawnerId1 5,1
	PPE_WeaponBase.SlotAlt1 "PPE_SSCannonS7"
	PPE_WeaponBase.SlotAlt2 "PPE_SSCannonS9"
	States
	{
	Spawn:
		CNNP A -1
		Stop
	Ready.Ready:
		CNNG A 1 A_WeaponReady
		Loop
	Deselect:
		CNNG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		CNNG A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("weapons/ss/cannon/charge",6)
		CNNG A 3
		TNT1 A 0 A_SetInventory("PPE_MC_Charge",0)
		TNT1 A 0 A_SetInventory("PPE_MC_UnloadTolerance",0)
		Goto CannonChargeInitial
	CannonChargeLoopy:
		CNNG A 2 A_WeaponOffset(Random(-1,1),32+Random(0,2)+CountInv("PPE_MC_Charge"))//A_Quake(1,3,0,1,"")
	CannonChargeInitial:
	    TNT1 A 0 A_JumpIfInventory("PPE_MC_Charge",0,"CannonChargeReleaseLoopy")
		TNT1 A 0 A_GiveInventory("PPE_MC_Charge",1)
		TNT1 A 0 A_ReFire("CannonChargeLoopy")
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_JumpIfInventory("PPE_MC_Charge",10,"CannonChargeRelease3")
		TNT1 A 0 A_JumpIfInventory("PPE_MC_Charge",5,"CannonChargeRelease2")
		Goto CannonChargeRelease
	CannonChargeReleaseLoopy:
		TNT1 A 0 A_JumpIfInventory("PPE_MC_UnloadTolerance",0,"CannonChargeRelease")
		TNT1 A 0 A_GiveInventory("PPE_MC_UnloadTolerance",1)
		CNNG A 1 A_WeaponOffset(Random(-2,2),32+Random(0,3)+CountInv("PPE_MC_Charge"))//A_Quake(2,2,0,1,"")
		TNT1 A 0 A_ReFire("CannonChargeReleaseLoopy")
		Goto CannonChargeRelease
	CannonChargeRelease:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_JumpIfInventory("PPE_MC_Charge",10,"CannonChargeRelease3")
		TNT1 A 0 A_JumpIfInventory("PPE_MC_Charge",5,"CannonChargeRelease2")
		TNT1 A 0 A_Jump("256","CannonChargeRelease1")
		Goto CannonChargeRelease1
	CannonChargeRelease1:
		//TNT1 AAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall1",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(20)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL1",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonChargeRelease2:
		//TNT1 AAAAAAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall2",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(25)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL2",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonChargeRelease3:
		//TNT1 AAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall3",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(30)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL3",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonReleaseAnim:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("weapons/ss/cannon/fire",CHAN_WEAPON)
		CNNF ABCDEFG 2 bright
		CNNG C 2 offset(0,40)
		CNNG C 2 offset(0,36)
		CNNG B 2 offset(0,34)
		CNNG A 2 offset(0,32)
		Goto Ready.Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.2)
		Goto LightDone
	}
}

Actor PPE_SSCannonS7 : PPE_SSCannon 
{ 
	Weapon.SlotNumber 7 Weapon.SlotPriority 10 
	PPE_WeaponBase.SlotAlt1 "PPE_SSCannon" 
	PPE_WeaponBase.SlotAlt1 "PPE_SSCannonS9" 
	Weapon.AmmoUse 12
	States
	{
	CannonChargeRelease1:
		//TNT1 AAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall1",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(12)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL1S7",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonChargeRelease2:
		//TNT1 AAAAAAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall2",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(15)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL2S7",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonChargeRelease3:
		//TNT1 AAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall3",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(18)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL3S7",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	}
}

Actor PPE_SSCannonS9 : PPE_SSCannon 
{ 
	Weapon.SlotNumber 9 Weapon.SlotPriority 8 
	PPE_WeaponBase.SlotAlt1 "PPE_SSCannon" 
	PPE_WeaponBase.SlotAlt1 "PPE_SSCannonS7" 
	Weapon.AmmoUse 16
	States
	{
	CannonChargeRelease1:
		//TNT1 AAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall1",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(16)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL1S9",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonChargeRelease2:
		//TNT1 AAAAAAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall2",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(20)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL2S9",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	CannonChargeRelease3:
		//TNT1 AAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("PPE_CannonBall3",0,0,0,-2,1,3)
		TNT1 A 0 A_ChangeAmmoUse(24)
		TNT1 A 0 A_FireCustomMissile("PPE_CannonBallL3S9",0,1,0,-2,1,3)
		Goto CannonReleaseAnim
	}
}

Actor PPE_CannonBallL1 : PPE_CannonBall
{
  Speed 27
}

Actor PPE_CannonBallL2 : PPE_CannonBall
{
  Speed 39
}

Actor PPE_CannonBallL3 : PPE_CannonBall
{
  Speed 50
}

Actor PPE_CannonBallL1S7 : PPE_CannonBallL1
{
  Damage (12)
}

Actor PPE_CannonBallL2S7 : PPE_CannonBallL2
{
  Damage (12)
}

Actor PPE_CannonBallL3S7 : PPE_CannonBallL3
{
  Damage (12)
}

Actor PPE_CannonBallL1S9 : PPE_CannonBallL1
{
  Damage (16)
}

Actor PPE_CannonBallL2S9 : PPE_CannonBallL2
{
  Damage (16)
}

Actor PPE_CannonBallL3S9 : PPE_CannonBallL3
{
  Damage (16)
}

Actor PPE_CannonTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 3
		TNT1 A 0 A_Jump(256,"A","B","C")
	A:
		MFC1 ABCD 2 bright
		Stop
	B:
		MFC2 ABCD 2 bright
		Stop
	C:
    	MFC3 ABCD 2 bright
		Stop
	}
}

Actor PPE_MC_Charge : Inventory
{
	Inventory.MaxAmount 10
}

Actor PPE_MC_UnloadTolerance : Inventory
{
	Inventory.MaxAmount 100
}