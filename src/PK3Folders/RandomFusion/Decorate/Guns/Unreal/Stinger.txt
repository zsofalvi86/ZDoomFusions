Actor PPE_UnrStinger : PPE_UWeaponBase
{
	+WEAPON.NOAUTOAIM
	Tag "$TAG_UNR_STG"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 3
	Weapon.SlotPriority 3
	Weapon.UpSound "weapons/unr/stinger/up"
	Weapon.AmmoType1 "PPE_Bullets"
	Weapon.AmmoUse1 14
    Weapon.AmmoType2 "PPE_Bullets"
	Weapon.AmmoUse2 75
	Weapon.AmmoGive1 200
	PPE_WeaponBase.SlotAlt1 "PPE_UnrStingerS4"
	PPE_WeaponBase.SpawnerId1 2,1
	PPE_WeaponBase.SpawnerId2 2,3
	PPE_WeaponBase.SpawnerId3 14,1
	States
	{
	Spawn:
		NGNW A -1
		Stop
	Ready.Ready:
		USTS AABBCCCC 1 A_WeaponReady
		Loop
	Deselect:
		USTS A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		USTS A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
	StartFire:		
        TNT1 A 0 A_PlayWeaponSound("weapons/unr/stinger/firestart")
    FireCont:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("FlashStinger")
		TNT1 A 0 A_FireCustomMissile("PPE_StingerShard",FRandom(-0.8,0.8),1,0,4,1,FRandom(-0.6,0.6))
		USTT AABBCCC 1 bright
		TNT1 A 0 A_ReFire("ReFire")
		Goto Spindown
	ReFire:
		TNT1 A 0 A_PlayWeaponSound("weapons/unr/stinger/firecont")
		TNT1 A 0 A_AlertMonsters		
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_GunFlash("FlashStinger")
		TNT1 A 0 A_FireCustomMissile("PPE_StingerShard",FRandom(-1.2,1.2),1,0,4,1,FRandom(-0.9,0.9))
		USTT AABBCCC 1 bright
		TNT1 A 0 A_ReFire("ReFire")
    Spindown:
		TNT1 A 0 A_PlayWeaponSound("weapons/unr/stinger/fireend")
		USTS CBA 1
		Goto Ready
	AltFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash("FlashStingerAlt")
        TNT1 A 0 A_PlayWeaponSound("weapons/unr/stinger/alt")
		TNT1 A 0 A_FireCustomMissile("PPE_ShotgunStingerShard",FRandom(-4.2,4.2),1,0,4,1,FRandom(-2.9,2.9))
		TNT1 AAAA 0 A_FireCustomMissile("PPE_ShotgunStingerShard",FRandom(-4.2,4.2),0,0,4,1,FRandom(-2.9,2.9))
		USTT DEF 3 bright
		USTS ABCD 1 bright
		USTS EFGHHHHH 2
		USTS FGE 1
		USTS DCB 1
		Goto Ready
	FlashStinger:
    	TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(pitch-0.2)
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(pitch+0.4)
		Goto LightDone
	FlashStingerAlt:
    	TNT1 A 0 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.22)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.44)
		Goto LightDone
	}
}

Actor PPE_UnrStingerS4 : PPE_UnrStinger { Weapon.SlotNumber 4 Weapon.SlotPriority 5 PPE_WeaponBase.SlotAlt1 "PPE_UnrStinger" }

Actor PPE_StingerShard : FastProjectile
{
	Decal "BulletHole"
	+BLOODSPLATTER
	Speed 45
	Radius 4
	Height 3
	Damage (14)
	MissileHeight 8
	States
	{
	Spawn:
	    SSHD A 4 bright
		Loop
	Crash:
	Death:
		TNT1 A 0
		TNT1 A 0 //A_SpawnItem("PPE_PuffSmoke")
		TNT1 A 0 A_PlaySound("weapons/unr/stinger/impact",3)
		TNT1 A 0 A_Jump(160,"SkipRicochet")
		TNT1 A 0 A_PlaySound("weapons/unr/stinger/ric",3)
	SkipRicochet:
		PUFF AB 2 bright
		PUFF CD 2 bright
		stop
	XDeath:
		TNT1 A 0
		Stop
	}
}

Actor PPE_ShotgunStingerShard : PPE_StingerShard
{
	Damage (15)
}