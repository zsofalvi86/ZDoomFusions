ACTOR PPE_UnrRifle : PPE_UWeaponBase
{
	Weapon.AmmoUse 18
	Weapon.AmmoGive 80
	Weapon.AmmoType "PPE_Shells"
	Weapon.SlotNumber 9
	Weapon.SlotPriority 7
	Obituary "$OB_MPPISTOL"
	-WEAPON.WIMPY_WEAPON
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	Tag "$TAG_UNR_RIF"
	Weapon.UpSound "weapons/unr/rifle/up"
	PPE_WeaponBase.SlotAlt1 "PPE_UnrRifleS7"
	PPE_WeaponBase.SpawnerId1 0,3
	PPE_WeaponBase.SpawnerId1 1,2
	States
	{
	Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_RifleZoom",1,"ReadyScope")
		RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	ReadyScope:
		TNT1 A 0 A_StopSound(6)
	ReadyScopeLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB)
		Loop
	Deselect:
		TNT1 A 0 A_Lower
		RIFG A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Raise
		RIFG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfinventory("PPE_RifleZoom",1,"FireScope")
		TNT1 A 0 A_FireBullets(0,0,1,(90),"PPE_URiflePuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/unr/rifle/fire")
		RIFF ABC 3 bright
		RIFG A 12
		TNT1 A 0 A_ReFire
		Goto Ready
	FireScope:
	    TNT1 A 0 A_FireBullets(0,0,1,(90),"PPE_URiflePuff",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_PlayWeaponSound("weapons/unr/rifle/fire")
		TNT1 A 0 A_GunFlash
		SSSS ZZZZZZ 2 bright
		SSSS Z 12 bright
		TNT1 A 0 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_RifleZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_RifleZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyScope
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyScope
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyScope
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RifleZoom",0)
		SSSS Z 5 bright
		SSSS A 10
		Goto Ready
  Flash:
    TNT1 A 0 A_Light1
	TNT1 A 1
	TNT1 AAA 1 A_SetPitch(pitch-0.82)
	TNT1 A 0 A_Light2
	TNT1 AAA 1 A_SetPitch(pitch+0.82)
	Goto LightDone
  Spawn:
	RIFL A -1
	Stop
  }
}

Actor PPE_UnrRifleS7 : PPE_UnrRifle 
{ 
	Weapon.SlotNumber 7 Weapon.SlotPriority 8 PPE_WeaponBase.SlotAlt1 "PPE_UnrRifle"
	Weapon.AmmoUse 14
	States
	{
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfinventory("PPE_RifleZoom",1,"FireScope")
		TNT1 A 0 A_FireBullets(0,0,1,(70),"PPE_URiflePuffS7",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlayWeaponSound("weapons/rifle/fire")
		RIFF ABC 3 bright
		RIFG A 12
		TNT1 A 0 A_ReFire
		Goto Ready
	FireScope:
	    TNT1 A 0 A_FireBullets(0,0,1,(70),"PPE_URiflePuffS7",FBF_USEAMMO|FBF_NORANDOM|FBF_EXPLICITANGLE)
		TNT1 A 0 A_PlayWeaponSound("weapons/rifle/fire")
		TNT1 A 0 A_GunFlash
		SSSS ZZZZZZ 2 bright
		SSSS Z 12 bright
		TNT1 A 0 A_ReFire
		Goto Ready
	}
}

Actor PPE_URiflePuff : PPE_HeadShot
{
	+NOGRAVITY
	//+PUFFONACTORS
	Damage (90)
	PPE_HeadShot.HeadShotDamage 200
	States
	{
	Headshot:
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("Blood",0,0,0,FRandom(-3,3),FRandom(-3,3),FRandom(-3,3))
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(190,"SkipRicochet")
		TNT1 A 0 A_PlaySound("weapons/ric",3)
	SkipRicochet:
		PUFF AB 3 Bright
	Melee:
		PUFF CD 3
		Stop
  }
}

Actor PPE_URiflePuffS7 : PPE_URiflePuff
{
	Damage (70)
	PPE_HeadShot.HeadShotDamage 160
}

Actor PPE_RifleZoom : Inventory
{
	Inventory.MaxAmount 1
}