actor PPE_QRocket : Rocket
{
	+FORCERADIUSDMG
	Speed 35
	Damage (96)
	SeeSound ""
	DeathSound "weapons/quake1/explode"
	Obituary "$OB_MPROCKET"
	DamageType "Quake1Rocket"
	States
	{
	Death: // 40, 80
		TNT1 A 0 A_Explode(40,128,1,0,128,0,0,"None","Normal")
		TNT1 A 0 A_Explode(80,128,1,0,0,0,0,"None","Normal")
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor PPE_Q1RocketLauncher : PPE_Quake1WeaponBase
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 5
	Tag "$TAG_Q1_RL"
	+NOAUTOFIRE
	States
	{
	Ready:
		QRLG A 1 A_WeaponReady
		Loop
	Deselect:
		QRLG A 1 A_Lower(18)
		Loop
	Select:
		QRLG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_QRocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/quake1/rocket/fire",1)
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QRLF ABCD 2 bright
		QRLG A 20
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	Spawn:
		QRLP A -1
		Stop
   }
}