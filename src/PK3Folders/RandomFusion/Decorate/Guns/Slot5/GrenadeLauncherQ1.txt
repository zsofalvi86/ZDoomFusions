actor PPE_QGrenade : Rocket
{
	+FORCERADIUSDMG
	+USEBOUNCESTATE
	-ROCKETTRAIL
	+GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
	SeeSound ""
	BounceSound "weapons/quake1/grenade/bounce"
	DeathSound "weapons/quake1/explode"
	BounceFactor 0.6
	WallBounceFactor 0.6
	Gravity 0.4
	BounceCount 4
	Speed 25
	Damage (96)
	DamageType "Quake1Rocket"
	States
	{
	Spawn:
		DGRD A 1
		Loop
	Bounce:
		DGRR ABCDEFGHIJKL 2
		Loop
	Death:
		TNT1 A 0 A_NoGravity // 40, 80
		TNT1 A 0 A_Explode(40,128,1,0,128,0,0,"None","Normal")
		TNT1 A 0 A_Explode(80,128,1,0,0,0,0,"None","Normal")
		TNT1 A 0 A_Quake(4,5,0,256,"")
		TNT1 A 0 A_Quake(2,6,0,720,"")
		TNT1 A 0 A_Quake(1,6,0,4096,"")
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor PPE_Q1GrenadeLauncher : PPE_Quake1WeaponBase
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 5
	Tag "$TAG_Q1_GL"
	+NOAUTOFIRE
	+NOAUTOAIM
	States
	{
	Ready:
		QGLG A 1 A_WeaponReady
		Loop
	Deselect:
		QGLG A 1 A_Lower(18)
		Loop
	Select:
		QGLG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_QGrenade",0,1,0,0,1,8)
		TNT1 A 0 A_PlaySound("weapons/quake1/grenade/fire",1)    	
		TNT1 A 0 A_PlaySound("items/quad/use",2,CountInv("PowerQuadDamage"))
		QGLF ABCD 2 bright
		QGLG A 13
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.5)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.0)
		Goto LightDone
	Spawn:
		QGLW A -1
		Stop
   }
}