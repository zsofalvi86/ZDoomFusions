actor PPE_Q2GrenadeLauncher : RocketLauncher
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "PPE_Grenades"
	Weapon.SlotNumber 6
	Inventory.PickupSound "items/q2weapon"
	Inventory.Pickupmessage "$GOT_PPE_GL"
	Tag "$TAG_PPE_GL"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		2GLG A 1 A_WeaponReady
		Loop
	Deselect:
		2GLG A 1 A_Lower
		Loop
	Select:
		2GLG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerSilencer",0,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/amp",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/grenade/fire",1,1-(CountInv("PowerSilencer")*0.7))
		TNT1 A 0 A_FireCustomMissile("PPE_LauncherGrenade",0,1,0,0,1,10)
		2GLF AB 2 bright
		TNT1 A 0 A_PlaySound("weapons/grenade/reload",6,1-(CountInv("PowerSilencer")*0.7))
		2GLF CDE 2 bright
		2GLG BCDEEEEFGHI 2
		2GLG A 10
		TNT1 A 0 A_ReFire
    	Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 A_SetPitch(pitch-0.5)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 A_SetPitch(pitch+1.0)
		Goto LightDone
	Spawn:
		2GLW A -1
		Stop
   }
}

Actor PPE_LauncherGrenade : PPE_SlowMissileBase
{
	+USEBOUNCESTATE
	-ROCKETTRAIL
	+GRENADETRAIL
	-NOGRAVITY
	BounceType Doom
	SeeSound ""
	BounceSound "weapons/grenade/bounce"
	DeathSound "weapons/grenade/explode"
	BounceFactor 0.7
	WallBounceFactor 0.7
	Gravity 0.4
	BounceCount 4
	Speed 22
	Damage (100)
	DamageType "Quake2Rocket"
	States
	{
	Spawn:
		DGRD A 1
		Loop
	Bounce:
		DGRR ABCDEFGHIJKL 2
		Loop
	Death:
		TNT1 A 0 A_NoGravity //40, 72 - upscaled to match rocket
		TNT1 A 0 A_SpawnItem("PPE_RedQuakeExplo")
		TNT1 A 0 A_Explode(40,128,1,0,128)
		TNT1 A 0 A_Explode(72,128)
		TNT1 A 0 A_Quake(4,5,0,256,"")
		TNT1 A 0 A_Quake(2,6,0,720,"")
		TNT1 A 0 A_Quake(1,6,0,4096,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}