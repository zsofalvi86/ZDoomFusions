Class PPE_RpgPlayerBase : DoomPlayer
{
	int tic;
	double bobtilt;
	double tilt; double desiredTilt;
	const maxtilt = 1.5;

	Override void Tick ()
	{
		Super.Tick();
		if (!player || !player.mo || player.mo != self){return;}
		
		int insm = GetPlayerInput(MODINPUT_SIDEMOVE);

		bool left = insm < -1000;
		bool right = insm > 1000;
		
		SetTilt(left, right);
		Timer();
		
		CVar q3ah = CVar.FindCVar('q3ahealth');
		let uarmor = BasicArmor(FindInventory("BasicArmor"));
		if (tic == 0 && self.Health > 0 && q3ah.GetBool() && 
		(self.Health > 100 || (uarmor != null && uarmor.Amount > 100)) &&
		!CheckInventory("PPE_GuardPowerToken",1))
		{
			if (self.Health > 100 && !CheckInventory("PowerRegen",1)) {A_SetHealth(self.Health-1);}
			if (uarmor != null && uarmor.Amount > 100) {uarmor.Amount--;}
		}
		
		if (tic == 0 && CheckInventory("PowerRegen",1))
		{
			if (self.Health < 100) {A_SetHealth(self.Health+15); A_PlaySound("items/regen/active",4);}
			else if (self.Health < 200) {A_SetHealth(self.Health+5); A_PlaySound("items/regen/active",4);}
			if (self.Health > 200) {A_SetHealth(200);}
		}
	}
	
	Override void FireWeapon(State stat)
	{
		shots++;
		Super.FireWeapon(stat);
	}
	
	void SetTilt(bool left, bool right)
	{
		if (left && !right) {desiredTilt = -maxtilt;}
		else if (right && !left) {desiredTilt = maxtilt;}
		else {desiredTilt = 0;}
		if(tilt < desiredTilt) {tilt += 0.25;}
		if(tilt > desiredTilt) {tilt -= 0.25;}
		
		let angle = Level.maptime / (20 * TICRATE / 35.) * 360.;
		let cbob = player.bob * sin(angle) * 0.5f;
		bobtilt = cbob * 0.125f;
		
		FCheckPosition fcp; CheckPosition((pos.x,pos.y),false,fcp);
		if (fcp.floorz == self.pos.z) {A_SetRoll(tilt - bobtilt);}
		else {A_SetRoll(tilt);}
	}
	
	void Timer()
	{
		if (tic <= 0) {tic = 35;}
		else {tic--;}
	}
	
	int lastkill;
	int killcount;
	int gibcount;
	int hpbuffer;
	
	void WorldThingDied (WorldEvent e)
	{
		if (e.Thing && e.Thing.Target
		&& (e.Thing.bIsMonster || e.Thing.Player))
		{
			if (e.Inflictor && e.Inflictor.GetClassName() == "PPE_GauntletPuff" && (e.Thing.GetSpawnHealth() > 200 || e.Thing.Player))
			{
				GiveInventory("PPE_Humiliation",1);
			}
			
			if (e.Thing.health < -1*min(e.Thing.GetSpawnHealth(),150)) { gibcount++; }
			hpbuffer += e.Thing.GetSpawnHealth();
			killcount++;
			
			if (level.time < lastkill + 70)
			{
				if (hpbuffer > 500 || killcount > 3 || gibcount > 1)
				{
					GiveInventory("PPE_Excellent",1);
					hpbuffer = 0;
					killcount = 0;
					gibcount = 0;
				}
			}
			else
			{
				hpbuffer = 0;
				killcount = 0;
				gibcount = 0;
			}
			lastkill = level.time;
		}
	}
	
	int lastrail;
	int lasthit;
	int shots;
	int hits;
	int dmgbuffer;
	
	void WorldThingDamaged (WorldEvent e)
	{	
		if (e.Thing 
		&& (e.Thing.bIsMonster || e.Thing.Player))
		{
			if (e.Inflictor && e.Inflictor.GetClassName() == "PPE_Q3RailPuff")
			{
				if ((level.time < lastrail + 35) || (level.time < lastrail + 70 && e.Thing.Player))
				{
					GiveInventory("PPE_Impressive",1);
				}
				else if (level.time > lastrail + 70 && e.Thing.Distance3D(self) > 1024)
				{
					GiveInventory("PPE_Accuracy",1);
				}
				lastrail = level.time;
			}
			else if (e.Thing.Distance3D(self) > 1024)
			{
				dmgbuffer += e.Damage;
			}
			
			if (shots > 0 && shots + hits >= 100)
			{
				double ratio = double(hits) / double(shots);
				if (ratio > 0.75)
				{
					console.printf("%s%f", "ratio:", ratio);
					GiveInventory("PPE_Accuracy",1);
				}
				shots = 0;
				hits = 0;
			}
			
			//console.printf("%s%i", "pn:", e.DamageSource.PlayerNumber()+1);
			//console.printf("%s%i", "lh:", lasthit[e.DamageSource.PlayerNumber()]);
			if (lasthit != level.time)
			{
				hits++;
				//console.printf("%s%i", "hit:", e.DamageSource.CountInv("PPE_AccHits"));
				//console.printf("%s%i", "shot:", e.DamageSource.CountInv("PPE_AccShots"));
			}
			
			if (lasthit < level.time + 70)
			{
				if (dmgbuffer >= 200) 
				{
					GiveInventory("PPE_Accuracy",1);
					dmgbuffer = 0;
				}
			}
			else
			{
				dmgbuffer = 0;
			}
			lasthit = level.time;
		}
	}

	Default
	{
		Mass 100;
		PainChance 255;
		Gravity 1.0;
		Species "Player";
		Player.StartItem "PPE_Q3MachineGun";
		Player.StartItem "PPE_Bullets", 75;
		Player.StartItem "PPE_Gauntlet";
		Player.StartItem "PPE_Q3DamageMod";
		Player.DisplayName "Doom Slayer";
		Player.SoundClass "PPE_Quake3Player";
		
		Player.WeaponSlot 1, "PPE_Q3Gauntlet";
		Player.WeaponSlot 2, "PPE_Q3MachineGun", "PPE_TAChainGun";
		Player.WeaponSlot 3, "PPE_Q3Shotgun", "PPE_TANailShotgun";
		Player.WeaponSlot 4, "PPE_TAProximityLauncher", "PPE_Q3GrenadeLauncher";
		Player.WeaponSlot 5, "PPE_Q3RocketLauncher";
		Player.WeaponSlot 6, "PPE_Q3LightningGun";
		Player.WeaponSlot 7, "PPE_Q3RailGun";
		Player.WeaponSlot 8, "PPE_Q3PlasmaGun";
		Player.WeaponSlot 9, "PPE_Q3Bfg";
	}
	
	States
	{
	Spawn:
		PLAY A -1;
		Loop;
	See:
		PLAY ABCD 4;
		Loop;
	Missile:
		PLAY E 12;
		Goto Spawn;
	Melee:
		PLAY F 6 BRIGHT;
		Goto Missile;
	Pain:
		PLAY G 4;
		PLAY G 4 A_Pain();
		Goto Spawn;
	Death:
		PLAY H 0 A_PlayerSkinCheck("AltSkinDeath");
	Death1:
		PLAY H 10;
		PLAY I 10 A_PlayerScream();
		PLAY J 10 A_NoBlocking();
		PLAY KLM 10;
		PLAY N -1;
		Stop;
	XDeath:
		PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath");
	XDeath1:
		PLAY O 5;
		PLAY P 5 A_XScream();
		PLAY Q 5 A_NoBlocking();
		PLAY RSTUV 5;
		PLAY W -1;
		Stop;
	AltSkinDeath:
		PLAY H 6;
		PLAY I 6 A_PlayerScream();
		PLAY JK 6;
		PLAY L 6 A_NoBlocking();
		PLAY MNO 6;
		PLAY P -1;
		Stop;
	AltSkinXDeath:
		PLAY Q 5 A_PlayerScream();
		PLAY R 0 A_NoBlocking();
		PLAY R 5 A_SkullPop();
		PLAY STUVWX 5;
		PLAY Y -1;
		Stop;
	}
}
