Actor PPE_DNRPG : PPE_DNWeaponBase
{
	Weapon.SelectionOrder 2400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "PPE_Rockets"
	Weapon.SlotNumber 5
	Weapon.UpSound "weapons/select"
	Inventory.PickupSound "weapons/pickup/quote"
	Inventory.Pickupmessage "$GOT_PPE_DNRPG"
	Tag "$TAG_PPE_DNRPG"
	Weapon.SisterWeapon "PPE_DNRPG_Seeker"
	+NOAUTOFIRE
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready.Ready:
		RPGG A 1 A_WeaponReady
		Loop
	Deselect:
		RPGG A 1 A_Lower(12)
		Loop
	Select:
		RPGG A 1 A_Raise(12)
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_DukeRocket",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/dnrpg/fire",1)
		RPGF ABCDE 2 bright
		RPGG A 14
		TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	Flash:
    	TNT1 A 1 A_Light1
		TNT1 AAAAAA 1 //A_SetPitch(pitch-0.4)
		TNT1 A 0 A_Light2
		TNT1 AAA 1 //A_SetPitch(pitch+0.8)
		Goto LightDone
	Spawn:
		RPGW A -1
		Stop
   }
}

Actor PPE_DNRPG_Seeker : PPE_DNRPG
{
	Weapon.AmmoType "PPE_Rockets_Seeker"
	Weapon.AmmoGive 0
	Tag "$TAG_PPE_DNRPG_S"
	States
	{
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PPE_DukeRocket_Seeker",0,1,0,1,1,0)
		TNT1 A 0 A_PlaySound("weapons/dnrpg/fire",1)
		RPGF ABCDE 2 bright
		RPGG A 20
		TNT1 A 0 A_ReFire
    	Goto Ready.Ready
	}
}

actor PPE_DukeRocket : PPE_SlowMissileBase
{
	Speed 27
	Damage (90)
	DamageType Explosive
	SeeSound ""
	DeathSound "weapons/explosion"
	Obituary "$OB_MPROCKET"
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("PPE_SmokeTrail")
		MISL A 1 bright 
		Loop
	Death:
		TNT1 A 0 A_Explode(128,128)
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BMXP A 4 bright
		BMXP BCDEF 3 bright
		Stop
	}
}

actor PPE_DukeRocket_Seeker : PPE_DukeRocket
{
	+SEEKERMISSILE
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("PPE_SmokeTrail")
		MISL A 1 bright A_SeekerMissile(5,3,SMF_LOOK|SMF_PRECISE,256,6)
		Loop
	}
}