Actor PPE_DNPipebomb : PPE_DNWeaponBase
{
	+WEAPON.NOALERT +WEAPON.NOAUTOAIM +WEAPON.NOAUTOFIRE
	Weapon.SelectionOrder 2600
	Inventory.Pickupmessage "$GOT_PPE_DNPB"
	Tag "$TAG_PPE_DNPB"
	Weapon.SlotNumber 6
	Weapon.AmmoUse 1
	Weapon.AmmoType "PPE_Pipebombs"
	Weapon.AmmoGive 0
	Weapon.SisterWeapon "PPE_DNDetonator"
	States
	{
	Ready.Ready:
		PIPE A 1 A_WeaponReady
    	Loop
	Deselect:
    	PIPE A 1 A_Lower(12)
    	Loop
	Deselect.Instant:
		TNT1 A 0 A_Lower(48)
		Loop
  	Select:
    	PIPE A 1 A_Raise(12)
    	Loop
  	Fire:
    	PIPE B 1
		TNT1 A 0 A_AlertMonsters
		PIPE B 4 A_WeaponOffset(0,38,WOF_ADD|WOF_INTERPOLATE)
	ThrowLoop:
		PIPE B 1 
		TNT1 A 0 A_ReFire("ThrowLoop")
	Throw:
		PIPE B 2 A_WeaponOffset(0,-16,WOF_ADD|WOF_INTERPOLATE)
		TNT1 A 0 A_FireCustomMissile("PPE_PipeBombMissile",0,1,0,1,1,-4)
		PIPE C 2 A_WeaponOffset(0,-16,WOF_ADD|WOF_INTERPOLATE)
		PIPE C 14 A_WeaponOffset(0,64,WOF_ADD|WOF_INTERPOLATE)
		TNT1 A 0 A_QueueInstantDeselect
		TNT1 A 0 A_SelectWeapon("PPE_DNDetonator")
    	Goto Ready.Ready
	}
}

Actor PPE_DNDetonator : PPE_DetonatorBase
{
	+WEAPON.NOALERT
	Weapon.SelectionOrder 2900
	Inventory.Pickupmessage "$GOT_PPE_DNPB"
	Tag "$TAG_PPE_DNPB"
	Weapon.SlotNumber 6
	Weapon.AmmoUse 0
	Weapon.AmmoType "PPE_Pipebombs"
	States
	{
	Ready.Ready:
		DETO A 1 A_WeaponReady
    	Loop
	Deselect:
    	DETO A 1 A_Lower(12)
    	Loop
  	Select:
    	DETO A 1 A_Raise(12)
    	Loop
  	Fire:
    	DETO A 1
		DETO B 2
		DETO C 2
		TNT1 A 0 A_DetonatePB
		DETO B 2
		DETO A 2
		TNT1 A 0 A_CheckForAmmo(1)
		TNT1 A 0 A_SelectWeapon("PPE_DNPipebomb",SWF_SELECTPRIORITY)
    	Goto Ready.Ready
	}
}

Actor PPE_PipeBombMissile : Rocket
{
	-ROCKETTRAIL
	-NOGRAVITY
	BounceType "Doom"
	SeeSound ""
	BounceSound "weapons/pipebomb/bounce"
	DeathSound ""
	BounceFactor 0.5
	WallBounceFactor 0.5
	Gravity 0.5
	BounceCount 3
	Speed 15
	Damage 0
	
	States
	{
	Spawn:
		RPIS A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("PPE_PipeBombBomb",0,0,0,0,0,0,0,SXF_ORIGINATOR|SXF_TRANSFERPOINTERS)
		Stop
	}
}
