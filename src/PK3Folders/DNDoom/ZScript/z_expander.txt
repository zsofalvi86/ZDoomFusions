Class PPE_ExpanderPuffBase : BulletPuff
{
	Override int DoSpecialDamage(Actor target, int damage, name damagetype)
	{
		let oldHp = target.health;
		let dd = super.DoSpecialDamage(target, damage, damagetype);
		if (target.bIsMonster && !(target is 'MorphedMonster') && oldHp > 0 && oldHp-dd <= 0)
		{
			dd = dd * 100;
			let exp = Spawn("PPE_ExpanderExpansion",target.pos);
			exp.target = target;
			exp.sprite = target.sprite;
			exp.frame = target.frame;
			exp.A_SetRenderStyle(target.alpha,target.GetRenderStyle());
		}
		return dd;
	}
}

Class PPE_ExpanderExpansion : Actor
{
	int oldrenderstyle;
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_PlaySound("weapons/expander/expand",5);
		"####" "#" 0 {oldrenderstyle = target.GetRenderStyle();}
		"####" "#" 0 {target.A_SetRenderStyle(1,STYLE_None);target.A_ScaleVelocity(0);}
		"####" "#################" 2 A_SetScale(scale.x+0.0175,scale.y+0.0175);
		TNT1 A 0 A_SetScale(1,1);
		TNT1 A 0 A_NoGravity();
		TNT1 A 0 {SetOrigin(Vec3Offset(0,0,40),false);A_SetRenderStyle(1,STYLE_Normal);}
		TNT1 A 0 A_PlaySound("weapons/explosion",6);
		TNT1 A 0 A_Explode(128,192);
		TNT1 A 0 {if(target){target.A_ScaleVelocity(0);}}
		TNT1 A 0 A_Quake(4,5,0,224,"");
		TNT1 A 0 A_Quake(2,6,0,512,"");
		TNT1 A 0 A_Quake(1,6,0,3072,"");
		TNT1 A 0 {if(target){target.A_SetRenderStyle(1,oldrenderstyle);}}
		BMXP A 4 bright;
		BMXP BCDEF 3 bright;
		Stop;
	}
}