Actor PPE_Q3RailGun : Chaingun
{
	+WEAPON.NOALERT
	Inventory.Pickupmessage "$GOT_PPE_RG"
	Tag "$TAG_PPE_RG"
	Inventory.PickupSound "items/q3weapon"
	Weapon.UpSound "items/q3switch"
	Weapon.SelectionOrder 800
	Weapon.SlotNumber 7
	Weapon.AmmoType "PPE_Slugs"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 4
	Weapon.BobSpeed 2
	Decal "BulletChip"
	DamageType "Quake3Rail"
	States
	{
	Drop:
	Spawn:
		3RGW A -1
		Stop
	Ready:
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"ReadyZoom")
		3RGI AABBCCDD 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	ReadyZoom:
		TNT1 A 0 A_StopSound(6)
	ReadyZoomLoop:
		SSSS Z 1 bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RailZoom",0)
	DeseLoop:
		3RGA A 1 A_Lower(18)
		Loop
	Select:
		3RGA A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/active",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlayWeaponSound("weapons/railgun/fire")
		TNT1 A 0 A_RailAttack((100),0,1,"none","none",RGF_SILENT,0,"PPE_Q3RailPuff",0,0,3500,0,96,0,"PPE_Q3RailRing",0)
		TNT1 A 0 A_RailAttack((0),0,0,"none","none",RGF_SILENT,0,"PPE_InvisiPuff",0,0,3500,0,8,0,"PPE_Q3RailTrail",0)
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"FireZoom")
	FireNormal:
		TNT1 A 0 A_JumpIfInventory("PowerHaste",1,"CoolH")
		3RGC ABCDE 2 bright
		3RGG A 20
		3RGG DCB 2
		3RGI AABBCCDD 1
		3RGI AABBCCDD 1
		TNT1 A 0 A_Refire
		Goto Ready
	CoolH:
		3RGC ABCDE 2 bright
		3RGG A 16
		3RGG DCB 2
		3RGI AABBCCDD 1
		TNT1 A 0 A_Refire
		Goto Ready
	FireZoom:
	    TNT1 A 0 A_JumpIfInventory("PowerHaste",1,"CoolHZoom")
		SSSS ZZZZZ 2 bright
		SSSS Z 20 bright
		SSSS ZZZ 2 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_ALLOWZOOM|WRF_NOSWITCH)
		SSSS ZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_ALLOWZOOM|WRF_NOSWITCH)
		SSSS ZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_ALLOWZOOM|WRF_NOSWITCH)
		TNT1 A 0 A_ReFire
		Goto Ready
	CoolHZoom:
		SSSS ZZZZZ 2 bright
		SSSS Z 16 bright
		SSSS ZZZ 2 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_ALLOWZOOM|WRF_NOSWITCH)
		SSSS ZZZZZZZZ 1 bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_ALLOWZOOM|WRF_NOSWITCH)
		TNT1 A 0 A_ReFire
		Goto Ready
	Zoom:
	AltFire:
		TNT1 A 0 A_JumpIfinventory("PPE_RailZoom",1,"DeZoom")
		TNT1 A 0 A_SetInventory("PPE_RailZoom",1)
		TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6,1.0,1)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom1")
		Goto ReadyZoom
	Zoom1:
		TNT1 A 0 A_ZoomFactor(2)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom2")
		Goto ReadyZoom
	Zoom2:
		TNT1 A 0 A_ZoomFactor(3)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyZoom
	Zoom3:
		TNT1 A 0 A_ZoomFactor(4)
		SSSS Z 3 bright
		TNT1 A 0 A_ReFire("Zoom3")
		Goto ReadyZoom
	DeZoom:
		TNT1 A 0 A_PlaySound("Weapons/sssniperz",6)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_SetInventory("PPE_RailZoom",0)
		SSSS Z 5 bright
		3RGI AABBCCDD 1
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		//TNT1 AA 1 A_SetPitch(pitch-0.4)
		TNT1 A 1 A_Light2
		//TNT1 A 1 A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

Actor PPE_Q3RailTrail : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(64,"Despawn")
    	Q3RT AABBCCCBBBBAAAAA 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_Q3RailRing : PPE_TrailBase
{
	States
	{
  	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(64,"Despawn")
    	PLSX AABBBCCCDDDEEE 1 bright
		Stop
	Despawn:
		TNT1 A 0
    	Stop
	}
}

Actor PPE_Q3RailPuff : BulletPuff
{
	+ALWAYSPUFF
	RenderStyle Normal
	VSpeed 0
	States
	{
	Crash:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/railgun/impact")
		PLCR ABCD 4 bright
		Stop
	Spawn:
	Melee:
		TNT1 A 0
		Stop
  }
}

Actor PPE_InvisiPuff : BulletPuff
{
	-ALLOWPARTICLES
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		Stop
	}
}

Actor PPE_RailZoom : Inventory
{
	Inventory.MaxAmount 1
}