Actor PPE_BulletPuffBaseQ3 : BulletPuff
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(190,"SkipRicochet")
		TNT1 A 0 A_PlaySound("weapons/ricbq3",3)
	SkipRicochet:
		PUFF AB 2 Bright
	Melee:
		PUFF CD 2
		Stop
  }
}

Actor PPE_BulletPuffTA : BulletPuff
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(190,"SkipRicochet")
		TNT1 A 0 A_PlaySound("weapons/ricta",3)
	SkipRicochet:
		PUFF AB 2 Bright
	Melee:
		PUFF CD 2
		Stop
  }
}

Actor PPE_TrailBase
{
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	Radius 1
	Height 1
}

Actor PPE_DodgeableMissileBase
{
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Projectile
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

Actor PPE_SlowMissileBase : PPE_DodgeableMissileBase
{
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Projectile
	SeeSound "weapons/rocklf"
	DeathSound "weapons/rocklx"
	Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
	+BLOODSPLATTER
}

Actor PPE_NailBase : PPE_SlowMissileBase
{
	Decal BulletChip
	SeeSound ""
	DeathSound ""
	Speed 55
	Radius 4
	Height 3
	States
	{
	Spawn:
	    NALN A 4
		Loop
	Crash:
	Death:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/nailx",4)
		TNT1 A 0 A_Jump(160,"SkipRicochet")
		TNT1 A 0 A_PlaySound("weapons/nailric",3)
	SkipRicochet:
		PUFF AB 2 bright
		PUFF CD 2 bright
		stop
	XDeath:
		TNT1 A 0
		Stop
	}
}

Actor PPE_ShotNailBase : PPE_NailBase
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/nailgun/fly",6,1,1)
	Loopy:
	    NALN A 1 A_SpawnItem("PPE_ShotNailTrail")
		Loop
	Crash:
	Death:
		TNT1 A 0
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlaySound("weapons/nailgun/impact",4)
		PUFF AB 2 bright
		PUFF CD 2
		stop
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_PlaySound("Weapons/nailgun/hit",4)
		stop
	}
}

Actor PPE_ShotNailTrail : PPE_TrailBase
{
	RenderStyle "Translucent"
	Alpha 0.15
	States
	{
  	Spawn:
		TNT1 A 2
    	WTRS ABCDEF 3
    	Stop
	}
}

Actor PPE_SmokeTrail : PPE_TrailBase
{
	RenderStyle "Translucent"
	Alpha 0.3
	States
	{
  	Spawn:
		TNT1 A 2
    	WTRL ABCDEF 3
    	Stop
	}
}

Actor PPE_RocketTrail : PPE_TrailBase
{
	RenderStyle "Translucent"
	Alpha 0.45
	States
	{
  	Spawn:
		TNT1 A 2
    	WTRL ABCDEF 5
    	Stop
	}
}

Actor PPE_CasingBase : PPE_SlowMissileBase
{
	Damage (0)
	Speed 10
	-NOGRAVITY
	+PAINLESS
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	Gravity 0.5
	Radius 4
	Height 9
	States
	{
	Death:
		"####" "#" 1 A_SpawnItemEx("PPE_CasingSpot",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME)
		Stop
	}
}

Actor PPE_CasingSpot
{
	Radius 10
	Height 2
	+NOTELEPORT
	+NOBLOCKMAP
	+MOVEWITHSECTOR
	+NOTONAUTOMAP
	-NOGRAVITY
	States
	{
	Spawn:
		"####" "#" 100
		Stop
	}
}

Actor PPE_CasingShooterBase : PPE_SlowMissileBase
{
	Damage (0)
	SeeSound ""
	DeathSound ""
	+PAINLESS
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	+NOTONAUTOMAP
}

Actor PPE_BulletCasing : PPE_CasingBase
{
	SeeSound ""
	DeathSound "casing/bullet"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,1,2,3,4)
	Loopy:
		BCAS ABCDEFGH 2
		Loop
	}
}

Actor PPE_ShellCasing : PPE_CasingBase
{
	SeeSound ""
	DeathSound "casing/shell"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,1,2,3,4)
	Loopy:
		SCAS ABCDEFGH 2
		Loop
	Death:
		"####" "#" 400
		Stop
	}
}