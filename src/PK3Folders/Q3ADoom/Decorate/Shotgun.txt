Actor PPE_Q3Shotgun : Shotgun
{
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	Inventory.Pickupmessage "$GOT_PPE_SG"
	Tag "$TAG_PPE_SG"
	Inventory.PickupSound "items/q3weapon"
	Weapon.UpSound "items/q3switch"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 3
	Weapon.AmmoUse 1
	Weapon.AmmoType "PPE_Shells"
	Weapon.AmmoGive 6
	Decal "BulletChip"
	States
	{
	Ready:
    	Q3SG A 1 A_WeaponReady
    	Loop
  	Deselect:
		Q3SG A 1 A_Lower(18)
    	Loop
  	Select:
		Q3SG A 1 A_Raise(18)
    	Loop
  	Fire:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("items/quad/active",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_FireBullets(6.5,3.9,11,(10),"PPE_BulletPuffBaseQ3",FBF_USEAMMO|FBF_NORANDOM)
		TNT1 A 0 A_PlayWeaponSound("weapons/shotgun/fire")
		Q3SF ABC 2 bright
		Q3SG A 6 Offset(0,37)
		Q3SG B 1 Offset(0,32)
		Q3SG BCC 1
		TNT1 A 0 A_FireCustomMissile("PPE_Q3SGShellCasingShooterL",0,0,0,6)
		TNT1 A 0 A_FireCustomMissile("PPE_Q3SGShellCasingShooterR",0,0,0,6)
		TNT1 A 0 A_JumpIfInventory("PowerHaste",1,"AnimH")
	Anim:
		Q3SG DDEEFFGGHH 1
		Q3SG AAAAAAAAA 1
		TNT1 A 0 A_ReFire
		Goto Ready
	AnimH:
		Q3SG DDEEFFGGHH 1
		Q3SG A 1
		TNT1 A 0 A_ReFire
		Goto Ready
  	Flash:
    	TNT1 A 1 A_Light1
		TNT1 A 4 A_Light2
		Goto LightDone
  	Spawn:
    	Q3SP A -1
    	Stop
	}
}

Actor PPE_Q3SGShellCasingShooterL : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_ShellCasing",0,0,FRandom(62,45),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-16,-22))
		Stop
	}
}

Actor PPE_Q3SGShellCasingShooterR : PPE_CasingShooterBase
{
	Speed 14
	States
	{
	Spawn:
		TNT1 A 1
	Death:
		TNT1 A 0 A_SpawnProjectile("PPE_ShellCasing",0,0,FRandom(-62,-45),CMF_AIMDIRECTION|CMF_TRACKOWNER,FRandom(-16,-22))
		Stop
	}
}