Actor PPE_Q3Bfg : BFG9000
{
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	-WEAPON.NOAUTOFIRE
	Inventory.Pickupmessage "$GOT_PPE_BFG"
	Tag "$TAG_PPE_BFG"
	Inventory.PickupSound "items/q3weapon"
	Weapon.UpSound "items/q3switch"
	Weapon.SelectionOrder 500
	Weapon.SlotNumber 9
	Weapon.AmmoType "PPE_Nukes"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 4
	States
	{
	Spawn:
		3BFG W -1
		Stop
	Ready:
		3BFG AABBCCDD 1 A_WeaponReady
		Loop
	Deselect:
		3BFG A 1 A_Lower(18)
		Loop
	Select:
		3BFG A 1 A_Raise(18)
		Loop
	Fire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("items/quad/active",2,CountInv("PowerQuadDamage"))
		TNT1 A 0 A_PlaySound("weapons/bfg/fire",1)
		TNT1 A 0 A_JumpIfInventory("PowerHaste",1,"FireHaste")
		TNT1 A 0 A_FireCustomMissile("PPE_Q3BfgBall",0,1,0,1,1,0)
		3BFF ABCD 1 bright
		3BFG EFG 1
		TNT1 A 0 A_ReFire
		Goto Ready
	FireHaste:
		TNT1 A 0 A_FireCustomMissile("PPE_Q3BfgBallHaste",0,1,0,1,1,0)
		3BFF BCD 1 bright
		3BFG FG 1
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_Light1
		TNT1 AA 1 //A_SetPitch(pitch-0.6)
		TNT1 A 0 A_Light2
		TNT1 A 1 //A_SetPitch(pitch+0.8)
		Goto LightDone
	}
}

actor PPE_Q3BfgBall : PPE_SlowMissileBase
{
	Speed 65
	ProjectileKickBack 100
	Damage (256)
	DamageType "Quake3SplashImpact"
	Decal BFGLightning
	SeeSound ""
	DeathSound "weapons/q3exp"
	Obituary "$OB_MPROCKET"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/rocket/fly",6,1,1)
	Loopy:
		BFTK ABCD 1 bright A_SpawnItem("PPE_Q3BfgTrail")
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(128,128,1,0,0,0,0,"None","Quake3Splash")
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BFE3 ABCDE 3 Bright
		Stop
	}
}

actor PPE_Q3BfgBallHaste : PPE_Q3BfgBall
{
	Speed 84
}

actor PPE_Q3BfgBallDoom : PPE_Q3BfgBall
{
	Damage (0)
	Decal BFGLightning
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/rocket/fly",6,1,1)
	Loopy:
		BFTK ABCD 1 bright A_SpawnItem("PPE_Q3BfgTrail")
		loop
	Death:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Explode(128,128,0,0,0,0,0,"None","Quake3Splash")
		TNT1 A 0 A_Quake(4,5,0,224,"")
		TNT1 A 0 A_Quake(2,6,0,512,"")
		TNT1 A 0 A_Quake(1,6,0,3072,"")
		BFE3 ABC 3 Bright
		TNT1 A 0 A_BFGSpray("PPE_Q3BFGExtra",20,15,90,1024,32,33)
		BFE3 DE 3 Bright
		Stop
	}
}

actor PPE_Q3BfgBallDoomHaste : PPE_Q3BfgBallDoom
{
	Speed 84
}

Actor PPE_Q3BfgTrail : PPE_TrailBase
{
	States
	{
	Spawn:
		TNT1 A 2
		BFTT ABCDE 2 bright
		Stop
	}
}

Actor PPE_Q3BFGExtra : BFGExtra
{
	RenderStyle Normal Alpha 1.0
	DamageType "BFGSplash"
	States
	{
	Spawn: BFE2 ABCD 4 Bright
	stop
	}
}